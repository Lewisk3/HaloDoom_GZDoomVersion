class HaloMessage ui
{
	Font fnt;
	uint stage;
	string msg;
	double alpha;
	Color tint;
	vector2 scale, pos;   // Initalize here.
	double desired_alpha;
	vector2 desired_scale, desired_pos;
	
	static HaloMessage CreateMessage(Font fnt, string msg, vector2 pos, vector2 scale, double alpha = 1.0, Color tint = 0)
	{
		let hudmsg = HaloMessage(new("HaloMessage"));
		if(hudmsg)
		{
			hudmsg.fnt = fnt;
			hudmsg.msg = msg;
			hudmsg.pos = hudmsg.desired_pos = pos;
			hudmsg.desired_scale = scale * 1.5;
			hudmsg.desired_alpha = alpha;
			hudmsg.tint = tint;
		}
		return hudmsg;
	}
	
	void Draw(double dt)
	{
		double ispd = 0.25 * dt;
		scale.x = HaloMath.Lerp(scale.x, desired_scale.x, ispd);
		scale.y = HaloMath.Lerp(scale.y, desired_scale.y, ispd);
		pos.x = HaloMath.Lerp(pos.x, desired_pos.x, ispd);
		pos.y = HaloMath.Lerp(pos.y, desired_pos.y, ispd);
		HLSBS.DrawString(fnt, msg, pos, alpha:alpha, scale:scale, tint:tint);		
		
		
		switch(stage)
		{
			// Scale up then back down.
			case 0:
				if(abs(scale.Length() - desired_scale.Length()) <= 0.01)
				{
					desired_scale *= 0.75;
					stage++;
				}
				alpha = HaloMath.Lerp(alpha, desired_alpha, ispd);
			break;
			
			// Begin fadeout.
			case 1:
				alpha = 3.0;
				stage = 2;
			break;
			
			// Process fadeout completion.
			case 2:
				alpha -= 0.01 * dt;
				if(alpha <= 0) msg = "";
			break;
		}	
	}
}

class HaloStatusBar : BaseStatusBar
{
	const ANIM_FPS = 60;
	
	HaloPlayer haloplr;
	
	// Delta-time logic.
	double prevMS; 
	double deltatime;
	
	// Crosshair vars
	double curbloom;
	
	// Interpolated vars
	double curshields;
	double dimalpha;
	double curbattery;
	
	// Timers
	double lowshield_timer;
	bool shieldwarn;
	
	// Font(s)
	HUDFont mHUDFont;
	Font fnt;
	Font fntHaloAmmo;
	
	// Opt Features
	bool draw_distance;
	
	// Message handling
	const MSGMAX = 4;
	double msgfade;
	Array<HaloMessage> messages;
	vector2 msgpos;
	
	// CVars
	transient CVar showminimap;
	
	// Basic Functions
	mixin BasicHUDFeatures;

	override void Init()
	{
		Super.Init();
		SetSize(0, 320, 200);
		if(CPlayer) haloplr = HaloPlayer(CPlayer.mo);
		
		// Create the font used for the fullscreen HUD
		fnt = "BigFont";
		fntHaloAmmo = "HaloAmmoFont";
		mHUDFont = HUDFont.Create(fnt);
		
		// Messages setup
		msgpos = (80, 600);
	}
	
	// Override message behavior
	override bool ProcessNotify(EPrintLevel printlevel, String outline)
	{
		bool logprint = (printlevel & PRINT_LOG);
		bool chatmsg  = (printlevel & (PRINT_CHAT|PRINT_TEAMCHAT));
		bool validprint = (logprint && chatmsg) || (printlevel < PRINT_HIGH);
		if(!validprint) return false;
		
		PushMessage(outline);
		return true;
	}
		
	void PushMessage(String msg)
	{
		Color msgcol = 0x4002a7de;
		vector2 msgscale = (1.0, 0.8);
		double msg_spacing = 30;
		uint count = messages.Size()-1;
		// Generate
		let msg = HaloMessage.CreateMessage("HaloFont", msg, msgpos, msgscale, tint:msgcol);
		if(count >= MSGMAX) 
		{
			messages.Delete(0);
			count--;
		}
		
		// Push other messages down
		for(int i = 0; i <= count; i++)
			messages[i].desired_pos.y += msg_spacing;
			
		messages.Push(msg);
	}
	
	
	void DrawMessages()
	{
		uint count = messages.Size()-1;
		for(int i = count; i >= 0; i--)
		{
			let msg = messages[i];
			if(msg.msg == "") 
			{
				messages.Delete(i);
				continue;
			}
			msg.Draw(deltatime);
		}
	}
	
	override void Draw (int state, double TicFrac)
	{
		if(!automapactive) Super.Draw(state, TicFrac);
		
		// Fetch CVars
		if(!showminimap) showminimap = CVar.GetCVar("halodoom_showminimap", CPlayer);
		
		// Always draw HUD
		BeginHUD();
		DrawMainBar (TicFrac);
	}
	
	void DrawMainBar (double TicFrac)
	{
		if(CPlayer && CPlayer.mo)
		{	
			if(!haloplr) 
			{
				haloplr = HaloPlayer(CPlayer.mo);
				return;	
			}
			
			// Vitals
			DrawBars(); 
			
			// Weapons
			if(!automapactive)
			{
				DrawCompass();
				DrawCrosshairs();
				DrawWeaponsAmmo();
				DrawRadar();
				DrawSight();
			}
			
			// Information
			DrawMessages();
			
			// Keys and Objectives
			DrawKeys(CPlayer, 50, 10, 30,30);			
			DrawObjectives();
			
			// Keep track of time, always.
			if(!prevMS)
			{
				prevMS = MSTime();
				return;
			}
			double ftime = MSTime()-prevMS;
			prevMS = MSTime();
			double dtime = 1000.0 / ANIM_FPS;
			deltatime = (ftime/dtime);
		}
	}
	
	// 24 ammo per line.
	const AMMO_MAX = 16.;
	const AMMOG_SIZE = 10.;
	void DrawWeaponsAmmo()
	{
		Weapon curweap = haloplr.player.ReadyWeapon;
		if(!curweap) return; // Just in case somehow the player has no weapon
		
		// Find other weapon manually.
		Weapon otherweap = haloplr.nextweapon;

		Halogun halo_curweap, halo_otherweap;
		halo_curweap = HaloGun(curweap);
		halo_otherweap = HaloGun(otherweap);
		
		String weap1_icon = "";
		String weap2_icon = "";
		vector2 weap1_scale, weap2_scale;
		vector2 weap1_offs, weap2_offs;
		[weap1_icon, weap1_scale] = GetWeaponUIDetails(curweap,   (1.5,1.5));
		[weap2_icon, weap2_scale] = GetWeaponUIDetails(otherweap, (0.6,0.6), (64,32));
		
		if(!halo_curweap) weap1_offs = (50,100);
		if(!halo_otherweap && otherweap) weap2_offs = (80,30);
			
		// Weapon Icons
		HLSBS.DrawImage(weap1_icon, (1700,100)+weap1_offs, 0, 0.8, weap1_scale);
		if(otherweap) HLSBS.DrawImage(weap2_icon, (1770,50)+weap2_offs, 0, 0.8, weap2_scale);
		
		if(halodoom_unlimitedbackpack)
		{
			// TODO: Draw all weapons.
			Weapon weap = NULL;
			Array<int> slots;
			slots.Resize(9);
			
			for(let i = haloplr.Inv; i; i = i.Inv)
			{
				weap = Weapon(i);
				if(!weap) continue;
				
				double num = weap.slotnumber;
				if(num < 0) num = 0;
				
				double cnt = slots[num];
				vector2 offs = (
					0, (num*100) + cnt*20
				);
				if(!HaloGun(weap)) 
				{
					offs.x += 25;
					offs.y += 20;
				}
				
				vector2 scale;
				string icon;
				[icon, scale] = GetWeaponUIDetails(weap, (1.0,1.0));
				HLSBS.DrawImage(icon, (1850,200)+offs, 0, 0.8, scale*0.5);
				slots[num]++;
			}	
			for(int i = 1; i <= slots.Size(); i++)
			{
				HLSBS.DrawString(fntHaloAmmo, i.."", (1800, 280+((i-1)*100)), scale:(0.05,0.05));
			}
		}
		
		// Ammo display
		vector2 ampos = (1640, 160);
		if(halo_curweap && halo_curweap.uses_battery)
		{
			double cur_charge = haloplr.countinv(curweap.AmmoType2);
			curbattery = HaloMath.Lerp(curbattery, cur_charge, 0.15 * deltatime);
			
			double max_charge = GetDefaultByType(curweap.AmmoType2).MaxAmount;
			double charge_perc = curbattery/max_charge;

			if(charge_perc > 0)
			{
				HLSBS.SetClipRect(1587, 150, 230 * charge_perc, 20);
				HLSBS.DrawImage("WI_BTRY", ampos+(60,0), 0, 0.8, (2,1.5));
				HLSBS.ClearClipRect();
			}
		}
		else if(curweap.AmmoType2 && curweap.AmmoType2 != curweap.AmmoType1)
		{
			double cur_ammo  = haloplr.countinv(curweap.AmmoType2);
			double clip_ammo = GetDefaultByType(curweap.AmmoType2).MaxAmount;
			int ymax = ceil(cur_ammo/AMMO_MAX);
			vector2 amoffs;
			for(int i = 0; i < clip_ammo; i++)
			{
				double ammo_diff = clip_ammo-cur_ammo;
				if(ammo_diff <= i) HLSBS.DrawImage("WI_AMMO", ampos+amoffs, 0, 0.8, (1,1));
				amoffs.x += AMMOG_SIZE;
				if(amoffs.x >= AMMOG_SIZE*AMMO_MAX) 
				{
					amoffs.y += AMMOG_SIZE*1.25;
					amoffs.x = 0;
				}
			}
		}
	// Ammo Count
		HLSBS.DrawString(fntHaloAmmo, haloplr.countinv(curweap.AmmoType1).."", (1732, 60), scale:(0.18,0.15));
	}
	
	string, vector2 GetWeaponUIDetails(Weapon weap, vector2 uiscale = (1,1), vector2 desired_scale = (128,64))
	{	
		if(!weap) return "", (0,0);
	
		let haloweap = HaloGun(weap);
		if(haloweap) 
		{
			return haloweap.UI_Icon, uiscale;
		}
	
		State wspawn = weap.FindState("Spawn");
		TextureID tex;
		tex = wspawn.GetSpriteTexture(0);
		string icon = TexMan.GetName(tex);
		
		vector2 size;
		[size.x, size.y] = TexMan.GetSize(tex);
		
		double def_aspect = desired_scale.x / desired_scale.y;
		double aspect = (size.x / size.y) / (def_aspect);
		
		vector2 scaleto = (
			desired_scale.x / size.x,
			desired_scale.y / size.y
		);
		
		size.x = scaleto.x * aspect;
		size.y = scaleto.y;
		
		return icon, size;
	}

	void DrawCrosshairs()
	{
		let curvehicle = Veh_Manager(haloplr.FindInventory("Veh_Manager"));
		if(curvehicle && curvehicle.disableweaps)
		{
			bool lowerxhair = haloplr.CheckCrosshairLowered();
			string crosshair = curvehicle.crosshair;
			vector2 retsize = curvehicle.crosshair_scale;

			vector2 midpos = (0,0);
			if(lowerxhair) midpos.y = haloplr.xhair_lowpos;
			HLSBS.DrawImage(crosshair, midpos, HLSBS.SS_SCREEN_CENTER, 0.65, scale:retsize);
			return;
		}
		
		HaloGun weap = HaloGun(haloplr.player.ReadyWeapon);
		if(!weap) return;
		
		bool lowerxhair = haloplr.CheckCrosshairLowered();
		string crosshair = weap.wcrosshair;
		double bloom = clamp(weap.spreadmod, 0, 1.25);
		vector2 retsize = (0.65,0.65);
		vector2 midpos = (0,0);
		if(lowerxhair) midpos.y = haloplr.xhair_lowpos;
		retsize *= 1.0+curbloom;
		HLSBS.DrawImage(crosshair, midpos, HLSBS.SS_SCREEN_CENTER, 0.65, scale:retsize);
		double ispd = 0.12;
		if(bloom > curbloom) ispd = 0.5; 
		curbloom = HaloMath.Lerp(curbloom, bloom, ispd*deltatime);
		
		if(draw_distance)
		{
			string dist = String.Format("%.0f", haloplr.aimdist);
			HLSBS.DrawString("BigFont", dist, midpos+(0,32), HLSBS.SS_SCREEN_CENTER); 
		}
	}

	void DrawSight()
	{
		HaloGun weap = HaloGun(haloplr.player.ReadyWeapon);
		if(!weap) return;
		if(weap.zoomed)
		{
			vector2 midpos = ( 1920 / 2. , 1080 / 2. );
			bool lowerxhair = haloplr.CheckCrosshairLowered();
			if(lowerxhair) midpos.y += haloplr.xhair_lowpos;
			
			string sight = weap.UI_Sight;
			double scale = weap.sightscale * 2.;
			double sightalpha = weap.sightalpha > 0 ? weap.sightalpha : 0.9;
			Color dark = 0xFF000000;
			
			// There's no way to implement a "negative" cliprect or fill,
			// because GZDoom's UI library is aids, so we've gotta do it
			// manually... PAIN peko.
			
			let tex = TexMan.CheckForTexture(sight);
			let texsize = TexMan.GetScaledSize(tex);
			texsize *= scale;
			texsize.x *= HLSBS.GetAspectScale();
			vector2 texmid = texsize / 2.;
			
			// -- :Begin quad fill: -- \\
			// Left
			HLSBS.Fill(dark, 0,0, midpos.x-texmid.x + 1, 1080, sightalpha); 
			// Right
			HLSBS.Fill(dark, 
				midpos.x+texmid.x - 1, 0, 
				1920, 1080,
				sightalpha
			); 
			// Top
			HLSBS.Fill(dark, 
				midpos.x-texmid.x, 0, 
				texsize.x, midpos.y-texmid.y + 1,
				sightalpha
			); 
			// Bottom
			HLSBS.Fill(dark, 
				midpos.x-texmid.x, midpos.y+texmid.y, 
				texsize.x, 1080,
				sightalpha
			); 
			
			HLSBS.DrawImage(sight, midpos, 0, 1.0, scale:(scale,scale));
		}
	}

	const BLINKRATE = ANIM_FPS/6.;
	void DrawBars()
	{
		if(haloplr.shields <= 0)
		{
			lowshield_timer -= 1*deltatime;	
			if(lowshield_timer < 0) 
			{
				lowshield_timer = BLINKRATE; 
				shieldwarn = !shieldwarn;
			}
		}
		else
		{
			lowshield_timer = BLINKRATE;
			shieldwarn = false;
		}
		
		if( haloplr.regen_timer >= (haloplr.shield_regentime*35.) && dimalpha > 0 &&
			haloplr.shields < haloplr.maxshields )
		{
			dimalpha = HaloMath.Lerp(dimalpha, 0, 0.025*deltatime);
			Screen.Dim("42ADF5",dimalpha,0,0,Screen.GetWidth(),Screen.getHeight());
		}
		else
			dimalpha = 0.4;

		// Constants
		string shbg  = shieldwarn ? "sh_rbg"  : "sh_nbg";
		string shou  = shieldwarn ? "sh_rout" : "sh_nout";
		string shgfx = "sh_bar";
		uint flags = HLSBS.SS_NOASPECTCORRECTION;
		vector2 shieldpos  = (958, 88); //(1550, 50);
		vector2 healthpos  = (577, 77); //(1740, 77);
		vector2 shieldmin  = (958,  0); //(958,   0);  
		vector2 shieldclip = (706,504); //(706, 504);
		
		// Interpolate Shields
		if(curshields != haloplr.shields) 
		{
			double ispd = (haloplr.shields > curshields) ? 0.05 : 0.2;
			curshields = HaloMath.Lerp(curshields, haloplr.shields, ispd*deltatime);
		}
		if(curshields == 0) curshields = haloplr.shields;
		double shieldperc = curshields/haloplr.maxshields;
		
		// Setup shield parameters
		if(shieldperc >= 2.0) 
		{
			shbg = "sh_bar1";
			shgfx = "sh_bar2";
			shieldperc -= 2.0;
		}
		else if(shieldperc >= 1.0) 
		{
			shbg = "sh_bar";
			shgfx = "sh_bar1";
			shieldperc -= 1.0;
		}
		
		// Shield outline and background
		HLSBS.DrawImage(shou, shieldpos, flags, 0.8, scale:(1.0,1.35));
		HLSBS.DrawImage(shbg, shieldpos, flags, 0.8, scale:(1.0,1.35));
		
		// Shield clip
		shieldclip.x = HaloMath.Lerp(shieldclip.x, shieldmin.x, 1.-shieldperc);
		shieldclip.y = HaloMath.Lerp(shieldclip.y, shieldmin.y, 1.-shieldperc);
		
		// Shield graphics
		HLSBS.SetClipRect(shieldclip.x,32,shieldclip.y,86);
		HLSBS.DrawImage(shgfx, shieldpos, flags, 0.8, scale:(1.0,1.35));
		HLSBS.ClearClipRect();	

		// Health
		double hpratio = haloplr.health / double(haloplr.GetMaxHealth()); 
		int healthbars = clamp(ceil(hpratio * 9), 1, 9);
		HLSBS.DrawImage("ui_hbar"..healthbars, healthpos, flags, 0.8, scale:(-1.0,1.15));
		healthpos += (508,0);
		HLSBS.DrawImage("ui_hbar"..healthbars, healthpos, flags, 0.8, scale:(1.0,1.15));
	}

	void drawRadarLine( double x0, double y0, double x1, double y1, Color col, vector2 origin = (0,0), double maxdist = 128)
	{	
		double scrw = Screen.getWidth();
		double scrh = Screen.getHeight();
		double xres = scrw/1920.;
		double yres = scrh/1080.;
		double acor = HLSBS.GetAspectScale();
				
		if(maxdist < scrw)
		{
			double fromdist = HaloMath.Distance2D((x0,y0), origin);
			double todist = HaloMath.Distance2D((x1,y1), origin);
			if( (fromdist > maxdist || todist > maxdist) ) 
			{
				if(fromdist > maxdist*1.2 && todist > maxdist*1.2) return;
				double lineangle = atan2(y1-y0, x1-x0);
				double dist = HaloMath.Distance2D((x0,y0),(x1,y1));
				double step = 1;
				for(int i = 0; i < dist; i+=step)
				{
					vector2 pixpos = (x0+cos(lineangle)*i, y0+sin(lineangle)*i);
					double fulldist = HaloMath.Distance2D(pixpos, origin);
					if(fulldist > maxdist) continue;
					HLSBS.DrawImage("HPXL", pixpos, 0, 1.0, (0.5,0.5), tint:col);
				}
				return;
			}
		}
		Screen.DrawThickLine(x0*xres,y0*yres,x1*xres,y1*yres,2,col,alpha*255);
	}	
	
	
	vector2 SwapVector(vector2 vec)
	{
		double oldx = vec.x;
		vec.x = vec.y;
		vec.y = oldx;
		return vec;
	}
	void DrawRadar()
	{
		// -- CVars / Constants --
		double zoom = 7;
		double radarsize = 120;
		double minimap_renderdist = 800;
		double acor = HLSBS.GetAspectScale();
		Vector2 baseoffs = ( 230, 894 );
		Vector2 plrpos2D = (haloplr.pos.x, haloplr.pos.y);
		Color linecol = Color( 0xFF, 0x79, 0xC1, 0xC9);
		Color radrcol = Color( 0xFF, 0x2D, 0x3E, 0x5B);
		Vector2 dp = Actor.RotateVector( (0,0), haloplr.angle-90 );
		Vector2 map_middle = (dp.x+baseoffs.x,dp.y+baseoffs.y);
			
		// Draw radar bg
		HLSBS.DrawImage("RADARBG", baseoffs);
		
		// Draw Minimap
		if( showminimap && showminimap.getBool() )
		{
			for (int i = 0; i < Level.Lines.Size(); i++)
			{
				Line ln = Level.Lines[i];
				if( !(ln.flags & Line.ML_MAPPED) ) continue;
				
				Vector2 ln_pos = ln.v1.p;
				Vector2 ln_len = ln.v2.p;
				
				// Check render distance
				if( abs(ln_pos.x-plrpos2D.x) > minimap_renderdist && abs(ln_len.x-plrpos2D.x) > minimap_renderdist || 
					abs(ln_pos.y-plrpos2D.y) > minimap_renderdist && abs(ln_len.y-plrpos2D.y) > minimap_renderdist    )
					continue;
				
				ln_pos = Actor.RotateVector(SwapVector(ln_pos-plrpos2D),haloplr.angle+180);
				ln_len = Actor.RotateVector(SwapVector(ln_len-plrpos2D),haloplr.angle+180);					
				ln_pos += baseoffs*zoom;
				ln_len += baseoffs*zoom;
				ln_pos /= zoom;
				ln_len /= zoom;
				ln_pos.x *= acor;
				ln_len.x *= acor;
				ln_pos.x += map_middle.x * (1 - acor);
				ln_len.x += map_middle.x * (1 - acor);
				
				vector2 mid = map_middle;
				mid.x *= acor;
				mid.x += map_middle.x * (1 - acor);
				drawRadarLine(ln_pos.x,ln_pos.y,ln_len.x,ln_len.y,linecol, mid, radarsize*0.94);
			}
		}
		
		// Draw doomguy and Radar	
		double maxdist = 500;
		ThinkerIterator act_it = ThinkerIterator.Create("Actor");
		Actor obj;		
		while(obj = Actor(act_it.Next()))
		{		
			if(maxdist <= 0) break;
			if(!obj.bSHOOTABLE) continue;
			double x = haloplr.pos.x - obj.pos.x;
			double y = haloplr.pos.y - obj.pos.y;
			
			int dis = obj.Distance2D(haloplr);
			if( dis < (maxdist*10.) )
			{
				double frontangle = haloplr.angle+180;
				vector2 delta = Actor.RotateVector(SwapVector((x,y)), frontangle);
				delta.x /= zoom;
				delta.y /= zoom;
				if( !(obj is haloplr.GetClass()) && obj.target )
				{
					if(obj.health <= 0 || obj.bCORPSE || !obj.bISMONSTER) continue;
					String basegraphic = "ALLYDOT"; 
					if(obj.target != NULL && !obj.bFRIENDLY && obj.bSOLID && obj.bSHOOTABLE)
						basegraphic = "ENEMYDOT";
					else if(obj.bFRIENDLY && obj.bSHOOTABLE)
						basegraphic = "ALLYDOT";
					
					vector2 finalpos = ((map_middle.x-delta.x), (map_middle.y-delta.y));
					double drawdist; 
					[finalpos, drawdist] = HaloMath.ClampVector(finalpos, map_middle, radarsize*0.98);
					finalpos.x *= acor;
					finalpos.x += map_middle.x * (1 - acor);
					
					vector2 dScale = (1.0, 1.0);
					dScale *= clamp(1.0-(drawdist/512.), 0.5, 1.0);
					
					HLSBS.DrawImage(basegraphic, finalpos, 0, (drawdist>radarsize ? 0.6 : 1.0), dScale);
				}
			}
		}
		
		// Draw Radar middle, "player"
		HLSBS.DrawImage("RADARMID", baseoffs);	
	}

	// Planned to implement, currently unimplemented.
	void DrawObjectives() {}
	void DrawCompass() {}

	void DrawFullScreenStuff (double TicFrac) {}
}

// Standard HUD Functions.
mixin class BasicHUDFeatures 
{
	void DrawImageToBox(TextureID tex, int x, int y, int w, int h, double trans = 0.75, bool animate = false)
	{
		double scale1, scale2;

		if (tex)
		{
			let texsize = TexMan.GetScaledSize(tex);

			scale1 = w / texsize.X;
			scale2 = h / texsize.Y;
			scale1 = min(scale1, scale2);
			if (scale2 < scale1) scale1=scale2;

			x += w >> 1;
			y += h;

			w = (int)(texsize.X * scale1);
			h = (int)(texsize.Y * scale1);

			screen.DrawTexture(tex, animate, x, y,
				DTA_KeepRatio, true,
				DTA_VirtualWidth, 1920, DTA_VirtualHeight, 1080, DTA_Alpha, trans, 
				DTA_DestWidth, w, DTA_DestHeight, h, DTA_CenterBottomOffset, 1);
		}
	}
	// Key Stuff
	virtual bool DrawOneKey(int xo, int x, int y, int w, int h, Key inv, in out int c)
	{
		TextureID icon, nosprite;
		nosprite = TexMan.CheckForTexture("TNT1A0", TexMan.Type_Sprite);
		
		if (!inv) return false;
		
		TextureID AltIcon = inv.AltHUDIcon;
		if (!AltIcon.Exists()) return false;	// Setting a non-existent AltIcon hides this key.

		if (AltIcon.isValid()) 
		{
			icon = AltIcon;
		}
		else if (inv.SpawnState && inv.SpawnState.sprite!=0)
		{
			let state = inv.SpawnState;
			if (state != null) icon = state.GetSpriteTexture(0);
			else icon.SetNull();
		}
		// missing sprites map to TNT1A0. So if that gets encountered, use the default icon instead.
		if (icon.isNull() || icon == nosprite) icon = inv.Icon; 

		if (icon.isValid())
		{
			DrawImageToBox(icon, x, y, w, h);
			return true;
		}
		return false;
	}
	virtual int DrawKeys(PlayerInfo CPlayer, int x, int y, int w, int h)
	{
		int yo = y;
		int xo = x;
		int i;
		int c = 0;
		Key inv;

		if (!deathmatch)
		{
			int count = Key.GetKeyTypeCount();
			
			// Go through the key in reverse order of definition, because we start at the right.
			for(int i = count-1; i >= 0; i--)
			{
				if ((inv = Key(CPlayer.mo.FindInventory(Key.GetKeyType(i)))))
				{
					if (DrawOneKey(xo, x - (w+1), y, w, h, inv, c))
					{
						x += (w+1);
						if (++c >= 6)
						{
							x = xo;
							y += (h+1);
							c = 0;
						}
					}
				}
			}
		}
		if (x == xo && y != yo) y += 11;	// undo the last wrap if the current line is empty.
		return y - 11;
	}
}

// Dirty message hack
class DoHudMessage : Inventory
{
	string msg;
	
	override string PickupMessage()
	{
		if(!msg) msg = "Hax";
		return msg;
	}

	static void Send(Actor to, string msg)
	{
		let hudmsg = DoHudMessage(Actor.Spawn("DoHudMessage", to.pos));
		hudmsg.msg = msg;
		to.AddInventory(hudmsg);
	}
	
	override void DoEffect()
	{
		PrintPickupMessage(Owner.CheckLocalView(), msg);
		Owner.RemoveInventory(self);
		GoAwayAndDie();
	}
}

/* Old Shield code, when it used to use halves.
vector2 shieldpos = (957,50); //(1550, 50);
vector2 healthpos = (577, 77); //(1740, 77);
uint leftflags = BaseStatusBar.SHADER_REVERSE;
HLSBS.DrawBar("sh_barL",shbg.."L",curshields,haloplr.maxshields,shieldpos-(253,0), 0,leftflags, 0.8,1.0, (1.0,1.35));
HLSBS.DrawBar("sh_barR",shbg.."R",curshields,haloplr.maxshields,shieldpos, 0,0, 0.8,1.0, (1.0,1.35));

shieldpos += (126, 42);
HLSBS.DrawImage(shou.."R",shieldpos,scale:(1.0,1.3));
HLSBS.DrawImage(shou.."L",shieldpos-(251,0),scale:(1.0,1.3));
*/
