/*
Copyright 2020 Lewisk3

Permission is hereby granted, free of charge, to any person obtaining a copy 
of this software and associated documentation files (the "Software"), to deal 
in the Software without restriction, including without limitation the rights 
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell 
copies of the Software, and to permit persons to whom the Software is 
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all 
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR 
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, 
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE 
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER 
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN 
THE SOFTWARE.

~ MIT License ~
*/
class HaloPickup_EVH : EventHandler
{
	ui WeaponPickupProxy aim_weap;

	ui void DrawPickupString(HLViewProjection viewproj, RenderEvent e, string str, Font fnt = "smallfont", int fcol = Font.CR_UNTRANSLATED, int linenum = 1, double scale = 0.5)
	{
		bool infront;
		vector2 projectedPos;
		
		[infront, projectedPos] = HLSBS.GetActorHUDPos (
			viewproj,
			aim_weap, 16, 0, (aim_weap.height/3) + (12 - ((linenum-1)*3))
		);
		
		if(infront)
		{
			double dist = e.Camera.Distance3D(aim_weap);
			HLSBS.DrawString3D(fnt, 
				str, 
				projectedPos, 0,
				fcol, 
				scale:(scale,scale),
				distance: dist
			);
		}
	}
	
	override void RenderOverlay(RenderEvent e)
	{
		Actor rendersrc = e.Camera;
		// Draw HUD projections
		aim_weap = NULL;
		let plr = HaloPlayer(e.Camera);
		if(plr) aim_weap = plr.pickuptarget;	
		
		if(aim_weap && !halodoom_unlimitedbackpack)
		{
			bool infront;
			HLViewProjection viewproj = HLSBS.GetEventViewerProj(e);
			
			vector2 _unusedpos;
			[infront, _unusedpos] = HLSBS.GetActorHUDPos(viewproj,aim_weap, 16, 0, (aim_weap.height/3));

			if(infront && aim_weap.trueitem) 
			{
				string weapname = GetDefaultByType(aim_weap.trueitem).GetTag();
				let reserveAmmoType = GetDefaultByType(aim_weap.trueitem).ammotype1;
				let clipAmmoType = GetDefaultByType(aim_weap.trueitem).ammotype2;
				double defClipAmmo = clipAmmoType ? GetDefaultByType(clipAmmoType).maxAmount : 0;
				
				int ammogive1 = GetDefaultByType(aim_weap.trueitem).ammogive1;
				double reserves = plr.countInv(reserveAmmoType) + (aim_weap.noammogive ? 0 : (ammogive1 * aim_weap.ammogive_mod));
				
				string pickup1 = PICKUP_STR1;
				string pickup2 = PICKUP_STR2;
				string pickupClip = PICKUP_STR3;
				string reserveAmmo = PICKUP_STR4;
				pickupClip.Replace("$LOADEDAMMO", String.Format("%d", aim_weap.clipammo < 0 ? defClipAmmo : aim_weap.clipammo));
				reserveAmmo.Replace("$RESERVEAMMO", String.Format("%d", reserves));
				pickup1.Replace("$WEAPNAME",""..weapname);
	
				DrawPickupString(viewproj, e, pickup1, fcol:Font.CR_GRAY, linenum: 1, scale:0.75);
				DrawPickupString(viewproj, e, pickup2, linenum: 2);
				
				if(defClipAmmo > 0) 
					DrawPickupString(viewproj, e, pickupClip,  linenum: 3);
				if(reserveAmmoType != clipAmmoType) 
					DrawPickupString(viewproj, e, reserveAmmo, linenum: 4);
			}
		}
	}
	
	override void PlayerEntered(PlayerEvent e)
	{
		PlayerInfo plr = players[e.PlayerNumber];
		if(plr && plr.mo && !plr.mo.FindInventory("WeaponLimitProxy"))
		{
			plr.mo.GiveInventory("WeaponLimitProxy", 1);
		}
	}
	
	override void WorldThingSpawned(WorldEvent e)
	{
		let it = Weapon(e.Thing);
		if(!it) return;
		
		if(it.GetAge() <= 1)
		{
			// Attempt to look-up dropper.
			let actIt = ThinkerIterator.Create();
			Actor act;
			double dist = it.radius * it.radius;
			Actor dropper = NULL;
			while(act = Actor(actIt.Next()))
			{
				if(it.Distance3DSquared(act) > dist) continue;
				bool isDying = act.bISMONSTER && act.health <= 0;
	
				if(isDying) dropper = act;
			}

			// Weapon was dropped by actor, who isn't a player.
			if(dropper && !dropper.player)
				if(WeaponPickupProxy.ProcessDroppedWeapon(it)) return;
		}
		
		if(!it.Owner && !(it is "WeaponPickupProxy") && !(it.GetTag() ~== "NotAWeapon") )
		{
			WeaponPickupProxy.BecomeWeaponProxy(it);
		}
	}
}