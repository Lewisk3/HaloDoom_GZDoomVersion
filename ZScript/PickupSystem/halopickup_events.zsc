/*
Copyright 2020 Lewisk3

Permission is hereby granted, free of charge, to any person obtaining a copy 
of this software and associated documentation files (the "Software"), to deal 
in the Software without restriction, including without limitation the rights 
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell 
copies of the Software, and to permit persons to whom the Software is 
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all 
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR 
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, 
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE 
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER 
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN 
THE SOFTWARE.

~ MIT License ~
*/

class StringHelper Abstract
{
	static bool IsUpper(uint ch)
	{
		return ch != 0 ? ch == String.CharUpper(ch) : false;
	}
	
	static void SplitByUppercase(String src, out Array<String> tokens)
	{
		tokens.Clear();
		int foundat = -1;
		for(int i = 0; i <= src.Length(); i++)
		{
			if(StringHelper.IsUpper(src.ByteAt(i)))
			{
				if(foundat >= 0) tokens.Push(src.Mid(foundat, i-foundat));
				foundat = i;
			}
			else if(i >= src.Length() && foundat >= 0) 
			{
				tokens.Push(src.Mid(foundat, src.Length()));
				break;
			}
		}
	}
}

class HaloPickup_EVH : EventHandler
{
	play WeaponPickupProxy aim_weap;
	Array<Key> foundkeys;
	
	override void WorldTick()
	{
		let plr = players[consoleplayer].mo;
		if(!plr) return;
		
		let it = ThinkerIterator.Create("WeaponPickupProxy");
		WeaponPickupProxy act;	
		
		aim_weap = NULL;
		while(act = WeaponPickupProxy(it.Next()))
		{
			if(act.isaimweap) 
			{	
				aim_weap = act;
				break;
			}
		}
	}
	
	void AddNewKey(Key k)
	{
		string colors = (
			"Brick.Tan.Gray.Grey.Green.Brown.Gold.Red.Blue.Orange.White.Yellow."
			"Black.LightBlue.Cream.Olive.DarkGreen.DarkRed.DarkBrown.Purple."
			"DarkGray.Cyan.Ice.Fire.Sapphire.Teal" 
		);
		foundkeys.push(k);
		String keyname = k.GetClassName();
		Array<String> keytype;
		StringHelper.SplitByUppercase(keyname, keytype);
		// Find first valid key color.
		for(int i = 0; i < keytype.Size(); i++)
		{
			if(colors.IndexOf(keytype[i]) == -1) continue;
			Color kcol = Color(keytype[i]);
			k.SetShade(kcol);
		}
	}
	
	override void WorldLoaded(WorldEvent e)
	{
		foundkeys.Clear();
		let key_it = ThinkerIterator.Create("Key");
		Key foundkey;
		while( foundkey = Key(key_it.Next()) ) AddNewKey(foundkey);
	}
	
	override void RenderOverlay(RenderEvent e)
	{
		Actor rendersrc = e.Camera;
		// Draw HUD projections
		
		// Very important to note here that, Keys should NEVER be removed
		// from this Array. RenderOverlay runs at your NATIVE framerate and thus
		// runs faster than the Play-scopes ability to write to foundkeys.
		// This means, you can potentially get access out of bounds depending 
		// on performance which is, really bad.
		for(int i = 0; i < foundkeys.Size(); i++)
		{
			Key k = foundkeys[i];
			if(k && !k.Owner)
			{
				// Project KeyNAV
				HLViewProjection viewproj = HLSBS.GetRenderEventViewProj(e);
				bool infront;
				vector2 apos;
				[infront, apos] = HLSBS.GetActorHUDPos (
					viewproj,
					k, 0, 0, k.height*1.5
				);
				if(infront) 
				{
					int keytint = k.fillcolor;
					double dist = rendersrc.Distance3D(k);
					vector2 distscale = (0.15,0.15);
					distscale *= dist/1200.;
					distscale.x = clamp(distscale.x, 0.10, 0.20);
					distscale.y = clamp(distscale.y, 0.10, 0.20);
					HLSBS.DrawImage("HDENAV", apos, 0, 0.6, distscale, keytint, absolute:true);
					HLSBS.DrawString3D(
						"BigFont", 
						String.Format("%dm",dist/UNIT_METER), 
						apos, 0, 
						Font.CR_GOLD, 
						scale:(1.25,1.25),
						distance: 300.
					);
				}
			}
		}
		
		if(aim_weap)
		{
			bool infront;
			HLViewProjection viewproj = HLSBS.GetRenderEventViewProj(e);
			
			vector2 apos, bpos;
			[infront, apos] = HLSBS.GetActorHUDPos (
				viewproj,
				aim_weap, 16, 0, (aim_weap.height/2)-5
			);
			[infront, bpos] = HLSBS.GetActorHUDPos (
				viewproj,
				aim_weap, 16, 0, (aim_weap.height/2)-10
			);	

			if(infront) 
			{
				string weapname = GetDefaultByType(aim_weap.trueitem).GetTag();
				string pickup1 = PICKUP_STR1;
				string pickup2 = PICKUP_STR2;
				pickup1.Replace("$WEAPNAME",""..weapname);
				
				HLSBS.DrawString3D("smallfont", 
					pickup1, 
					apos, 0, 
					Font.CR_GRAY, 
					scale:(1.0,1.0),
					distance: e.Camera.Distance3D(aim_weap)
				);
				HLSBS.DrawString3D("smallfont", 
					pickup2, 
					bpos, 0, 
					0, 
					scale:(0.5,0.5),
					distance: e.Camera.Distance3D(aim_weap)
				);
			}
		}
	}
	
	override void PlayerEntered(PlayerEvent e)
	{
		PlayerInfo plr = players[e.PlayerNumber];
		if(plr && plr.mo && !plr.mo.FindInventory("WeaponLimitProxy"))
		{
			plr.mo.GiveInventory("WeaponLimitProxy", 1);
		}
	}
	
	override void WorldThingSpawned(WorldEvent e)
	{
		if(e.Thing is "Key") AddNewKey(Key(e.Thing));
		let it = Weapon(e.Thing);
		if(!it) return;
		
		if(!it.Owner && !(it is "WeaponPickupProxy"))
		{
			WeaponPickupProxy.BecomeWeaponProxy(it);
		}
	}
}