/*
Copyright 2020 Lewisk3

Permission is hereby granted, free of charge, to any person obtaining a copy 
of this software and associated documentation files (the "Software"), to deal 
in the Software without restriction, including without limitation the rights 
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell 
copies of the Software, and to permit persons to whom the Software is 
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all 
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR 
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, 
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE 
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER 
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN 
THE SOFTWARE.

~ MIT License ~
*/
class HaloPickup_EVH : EventHandler
{
	ui WeaponPickupProxy aim_weap;
	
	override void RenderOverlay(RenderEvent e)
	{
		Actor rendersrc = e.Camera;
		// Draw HUD projections
		aim_weap = NULL;
		let plr = HaloPlayer(e.Camera);
		if(plr) aim_weap = plr.pickuptarget;	
		
		if(aim_weap && !halodoom_unlimitedbackpack)
		{
			bool infront;
			HLViewProjection viewproj = HLSBS.GetEventViewerProj(e);
			
			vector2 apos, bpos, cpos;
			[infront, apos] = HLSBS.GetActorHUDPos (
				viewproj,
				aim_weap, 16, 0, (aim_weap.height/3)+15
			);
			[infront, bpos] = HLSBS.GetActorHUDPos (
				viewproj,
				aim_weap, 16, 0, (aim_weap.height/3)+10
			);	
			[infront, cpos] = HLSBS.GetActorHUDPos (
				viewproj,
				aim_weap, 16, 0, (aim_weap.height/3)+5
			);	

			if(infront && aim_weap.trueitem) 
			{
				string weapname = GetDefaultByType(aim_weap.trueitem).GetTag();
				let clipAmmoType = GetDefaultByType(aim_weap.trueitem).ammotype2;
				double defClipAmmo = clipAmmoType ? GetDefaultByType(clipAmmoType).maxAmount : 0;
				
				string pickup1 = PICKUP_STR1;
				string pickup2 = PICKUP_STR2;
				string pickupClip = PICKUP_STR3;
				pickupClip.Replace("$LOADEDAMMO", String.Format("%d", aim_weap.clipammo < 0 ? defClipAmmo : aim_weap.clipammo));
				pickup1.Replace("$WEAPNAME",""..weapname);
				
				HLSBS.DrawString3D("smallfont", 
					pickup1, 
					apos, 0, 
					Font.CR_GRAY, 
					scale:(0.75,0.75),
					distance: e.Camera.Distance3D(aim_weap)
				);
				HLSBS.DrawString3D("smallfont", 
					pickup2, 
					bpos, 0, 
					0, 
					scale:(0.5,0.5),
					distance: e.Camera.Distance3D(aim_weap)
				);
				if(defClipAmmo > 0)
				{
					HLSBS.DrawString3D("smallfont", 
						pickupClip, 
						cpos, 0, 
						0, 
						scale:(0.5,0.5),
						distance: e.Camera.Distance3D(aim_weap)
					);
				}
			}
		}
	}
	
	override void PlayerEntered(PlayerEvent e)
	{
		PlayerInfo plr = players[e.PlayerNumber];
		if(plr && plr.mo && !plr.mo.FindInventory("WeaponLimitProxy"))
		{
			plr.mo.GiveInventory("WeaponLimitProxy", 1);
		}
	}
	
	override void WorldThingSpawned(WorldEvent e)
	{
		let it = Weapon(e.Thing);
		if(!it) return;
		
		if(it.GetAge() <= 1)
		{
			// Attempt to look-up dropper.
			let actIt = ThinkerIterator.Create();
			Actor act;
			double dist = it.radius * it.radius;
			Actor dropper = NULL;
			while(act = Actor(actIt.Next()))
			{
				if(it.Distance3DSquared(act) > dist) continue;
				bool isDying = act.bISMONSTER && act.health <= 0;
	
				if(isDying) dropper = act;
			}

			// Weapon was dropped by actor, who isn't a player.
			if(dropper && !dropper.player)
				if(WeaponPickupProxy.ProcessDroppedWeapon(it)) return;
		}
		
		if(!it.Owner && !(it is "WeaponPickupProxy") && !(it.GetTag() ~== "NotAWeapon") )
		{
			WeaponPickupProxy.BecomeWeaponProxy(it);
		}
	}
}