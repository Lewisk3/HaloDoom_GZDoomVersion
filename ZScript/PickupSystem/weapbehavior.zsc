/*
Copyright 2020 Lewisk3

Permission is hereby granted, free of charge, to any person obtaining a copy 
of this software and associated documentation files (the "Software"), to deal 
in the Software without restriction, including without limitation the rights 
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell 
copies of the Software, and to permit persons to whom the Software is 
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all 
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR 
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, 
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE 
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER 
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN 
THE SOFTWARE.

~ MIT License ~
*/

class WeaponPickupProxy : Actor
{
	class<Weapon> trueitem;
	string truepickupmsg;
	sound truepickupsnd;
	HaloPlayer plr;
	bool weapdropped;
	bool noammogive;
	int clipammo;
	
	double visiblefov;
	double visibledist;
	Property VisibleParams : visiblefov, visibledist;
	
	Default
	{
		Radius PICKUP_RANGE*0.25;
		Height PICKUP_RANGE;
		WeaponPickupProxy.VisibleParams PICKUP_FOV, PICKUP_RANGE*2.;
	}
	
	static WeaponPickupProxy BecomeWeaponProxy(Weapon source, bool noammogive = false)
	{
		let wp = WeaponPickupProxy(Actor.Spawn("WeaponPickupProxy", source.pos));
		if(wp)
		{
			wp.truepickupmsg = source.PickupMessage();
			wp.truepickupsnd = source.pickupsound;
			wp.Sprite = source.Sprite;
			wp.Frame = source.Frame;
			wp.noammogive = noammogive;
			wp.trueitem = source.GetClass();
			wp.clipammo = -1;
			source.Destroy();
		}
		return wp;
	}
	
	Weapon GiveWeaponItem(Actor to)
	{
		if(!to) return null;
		let ammotype1 = GetDefaultByType(trueitem).ammotype1;
		let ammotype2 = GetDefaultByType(trueitem).ammotype2;
		int ammogive1 = GetDefaultByType(trueitem).ammogive1;
		int ammogive2 = GetDefaultByType(trueitem).ammogive2;
		let ammodef1  = ammotype1 ? GetDefaultByType(ammotype1) : NULL;
		let ammodef2  = ammotype2 ? GetDefaultByType(ammotype2) : NULL;
		
		// Do not pickup if we already own this item but we have max ammo
		// for it.
		if( to.countinv(trueitem) )
		{
			double togive;
			if(!to.countinv("backpack"))
			{
				if(ammodef1) togive += ammodef1.maxamount-to.countinv(ammotype1);
				if(ammodef2) togive += ammodef2.maxamount-to.countinv(ammotype2);
			}
			else
			{
				if(ammodef1) togive += ammodef1.backpackmaxamount-to.countinv(ammotype1);
				if(ammodef2) togive += ammodef2.backpackmaxamount-to.countinv(ammotype2);
			}
			if(togive <= 0) return Null;
		}
			
		Inventory.PrintPickupMessage(to.CheckLocalView(), truepickupmsg);
		to.A_StartSound(truepickupsnd, CHAN_ITEM, CHANF_OVERLAP);
		to.GiveInventory(trueitem, 1);
		if(noammogive) 
		{
			to.TakeInventory(ammotype1, ammogive1);
			to.TakeInventory(ammotype2, ammogive2);
		}
		// Resolve clip-ammunition.
		if(ammotype2 && clipammo != -1)
		{
			let clipdefs = GetDefaultByType(ammotype2);
			to.TakeInventory(ammotype2, clipdefs.maxamount);
			to.GiveInventory(ammotype2, clipammo);
		}
        return Weapon(to.FindInventory(trueitem));
	}
	
	void CheckPlrCollide(double distmod=1.0)
	{
		let it = ThinkerIterator.Create("HaloPlayer");
		HaloPlayer act;	
		
		if(plr) plr.pickuptarget = NULL;
		plr = NULL;
		WeaponPickupProxy closest;
		while(act = HaloPlayer(it.Next()))
		{
			// If player is colliding with a weaponproxy item, and looking at it.
			if( Distance2D(act) <= PICKUP_RANGE*distmod && 
			    act.pos.z >= pos.z && 
			    act.pos.z <= pos.z+(PICKUP_RANGE*distmod) )
			{
				
				CVar mustsee = CVar.GetCVar("pickup_checksight", act.player);
				if(mustsee && mustsee.GetBool())
				{
					FLineTraceData lt;
					act.LineTrace(act.angle, visibledist, act.pitch, TRF_ALLACTORS|TRF_ABSOFFSET, act.player.viewheight,0,0, lt);
					Actor hitact = lt.HitActor;
					if(hitact != self) continue;
				}
				
				plr = act;
				if(!plr.countinv(trueitem)) plr.pickuptarget = self;
				break;
			}
		}
	}
	
	override void Tick()
	{
		SetOrigin((pos.x,pos.y,floorz),true);
		super.Tick();
		bool hasweap = !plr ? false : plr.countinv(trueitem);
		CheckPlrCollide(hasweap ? 0.5 : 1.0);
		if(!plr) return;
			
		// If invalid sprite, use default.
		if(sprite == GetSpriteIndex("TNT1") || !sprite) 
		{
			sprite = GetSpriteIndex(NOWEAPSPR);
			frame = 0;
		}
		
		// Process pickup
		bool isaimweap = (plr.pickuptarget == self);
		if(!plr.player.ReadyWeapon || weapdropped || (!isaimweap && !hasweap)) return;
		
		let wcontrol = WeaponLimitProxy(plr.FindInventory("WeaponLimitProxy"));
		if(!wcontrol) 
		{
			plr.GiveInventory("WeaponLimitProxy",1);
			return;
		}
		
		int wcount = wcontrol.CountWeapons();
		int btns = plr.player.cmd.buttons;
		int oldbtns = plr.player.oldbuttons;
		if( ((btns & BT_USE) && !(oldbtns & BT_USE)) || hasweap )
		{
			if(wcount >= MAXWEAPS)
			{
				if(!hasweap)
				{
					wcontrol.DropSelectedWeapon(pos);
					weapdropped = true;
				}
				else
				{
					if(GiveWeaponItem(plr)) Destroy();
				}
			}
			else
			{
				let nweap = GiveWeaponItem(plr);
				if(!hasweap) nweap.Use(true);
				if(nweap) Destroy();
			}
		}
	}
	
	States
	{
		Spawn:
			"####" "#" 1 NoDelay A_JumpIf(weapdropped, "Weap.New");
		loop;
		Weap.New:
			TNT1 A 1;
			TNT1 A 0 
			{
				let nweap = GiveWeaponItem(plr); 
				if(nweap) nweap.Use(true);
			}
		stop;
	}
}

class WeaponLimitProxy : Inventory
{
	PlayerPawn plr;
	Default
	{
		inventory.maxamount 1;
		+INVENTORY.UNDROPPABLE;
	}
	
	void DropSelectedWeapon(vector3 atpos)
	{
		if(!plr || !plr.player || !plr.player.ReadyWeapon) return;
		let selweap = plr.player.ReadyWeapon;
		plr.DropWeapon();
		let temp = plr.Spawn(selweap.GetClass(), atpos);
		if(temp) 
		{
			let wp = WeaponPickupProxy.BecomeWeaponProxy(Weapon(temp), true);
			if(wp) wp.clipammo = plr.countinv(selweap.ammotype2);
		}
		plr.player.ReadyWeapon = NULL;
		
		// Interestingly, inventory items stay on the map at ALL times.
		// So, if you remove an item, and you want it completely gone be sure
		// to destroy it as well. If an item doesn't have an owner, it'll remain
		// invisible but it'll be pickupable again. Producing some really funny
		// bugs sometimes.
		plr.RemoveInventory(selweap);
		selweap.Destroy();
	}
	
	int CountWeapons()
	{
		// Count weapons. (If player is connected to this item)
		if(!plr) return -1;
		int weapcnt = 0;
		for(let item = plr.Inv; item; item = item.inv)
		{
			if(item is "Weapon" && !(item is "Fist")) weapcnt++;
		}
		return weapcnt;
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		plr = PlayerPawn(Owner);
		if(!plr || !plr.player) return;
		if(CountWeapons() > MAXWEAPS) 
		{
			double rang = ceil(frandom(0,90)*4.);
			vector3 fw = ( cos(plr.angle+rang)*32, sin(plr.angle+rang)*32, 0 );
			DropSelectedWeapon(plr.Vec3Offset(fw.x,fw.y,0));
			let nextweap = plr.PickNextWeapon();
			if(nextweap) nextweap.Use(false);
		}
	}
}