
class HaloPlayer : DoomPlayer
{
	// Misc
	WeaponPickupProxy pickuptarget;
	double soundwait;
	
	// Shields
	double shield_regentime;
	double shield_regenrate;
	double shields;
	double maxshields;
	double regen_timer;
	double movebob;
	
	
	Property Shields : maxshields, shield_regentime, shield_regenrate;
	Property MovementBob : movebob;
	
	Default
	{
		Speed 1;
		Health 45;
		Player.StartItem "Halo_AssaultRifle", 1;
		Player.StartItem "Halo_Magnum", 1;
		//Player.StartItem "Halo_Shotgun", 1;
		Player.StartItem "Halo_Clip", 320;
		Player.StartItem "Shell", 36;
		HaloPlayer.Shields 70, 5, 4; // 5 seconds to recharge, 4 seconds to max.
		HaloPlayer.MovementBob 0;
		Player.MaxHealth 45;
		Player.JumpZ 9;
	}
	
	override void BeginPlay()
	{
		regen_timer = shield_regentime*35.;
		shields = maxshields;
		super.BeginPlay();
	}
	
	override void Tick()
	{
		if(health <= 0) 
		{
			A_StopSound(CHAN_7);
			super.Tick();
			return;
		}
		
		// Used for timing certain sound fx.
		if(soundwait) soundwait--;
	
		// Shield Regen
		double shregentime = shield_regentime*35.;
		
		uint sndflags = CHANF_LOOPING|CHANF_MAYBE_LOCAL;
		if(regen_timer < shregentime)
		{
			if(shields <= 0) 
				A_StartSound("Shield/Warning", CHAN_7, flags:sndflags);
			else if(shields <= maxshields*0.25) 
				A_StartSound("Shield/Low", CHAN_7, flags:sndflags);
		}
		if(regen_timer < shregentime) 
		{	
			regen_timer += 1;
			// Useful for overshields to not regenerate.
			if(shields >= maxshields) regen_timer = shregentime;
		}
		regen_timer = clamp(regen_timer, -shregentime, shregentime);
		if(regen_timer >= shregentime && shields < maxshields) 
		{
			shields += maxshields/(shield_regenrate*35.);
			A_StopSound(CHAN_7);
			A_StartSound("Shield/Regenerate", CHAN_5, flags:sndflags);
		}
		if(shields >= maxshields)
		{
			A_StopSound(CHAN_5);
		}
		
		super.Tick();
	}
	
	override int DamageMobJ(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		if(shields > 0)
		{
			regen_timer = 0;
			shields -= damage;
			A_StopSound(CHAN_5);
			A_StartSound("Shield/TakeDamage",flags:CHANF_NOSTOP);
			if(shields <= 0) 
			{	
				A_StartSound("Shield/Explode",flags:CHANF_NOSTOP);
				double truedmg = abs(shields);
				shields = 0;
				return super.DamageMobJ(inflictor, source, truedmg, mod, flags, angle);
			}
			return 0;
		}
		return super.DamageMobJ(inflictor, source, damage, mod, flags, angle);
	}
	
	override void CalcHeight()
	{
		super.CalcHeight();
		player.viewz = pos.Z + player.viewheight + (player.bob * clamp(movebob, 0. , 1.5)); 
		if (Floorclip && player.playerstate != PST_DEAD && pos.Z <= floorz) player.viewz -= Floorclip;
		if (player.viewz > ceilingz - 4) player.viewz = ceilingz - 4;
		if (player.viewz < floorz + 4) player.viewz = floorz + 4;
	}
}