extend class HaloPlayer
{
	// Movement constants
	const GNDMAX   = 14;
	const GNDACCEL = 85;
	const GNDFRIC  = 7.0;
	const BT_DASH = BT_USER3;
	const BT_GRAPPLE = BT_USER4;
	const SLIDE_TICS = 20;
	const SLIDE_COOL = 35;
	
	const AIRMAX     = 10;
	const AIRACCEL   = 40;
	
	// Switches
	bool enable_grapple;
	bool enable_bhop;
	bool canslide;
	double aircontrol;
	double fricmod; // Inverse friction mod; 1.0 = no friction applied.
	
	// Input device(s)
	bool using_controller;
	bool block_moveinput;
	
	// Jump delay
	int prevjump;
	int fricwait;
	int maxjumps;
	
	// Footsteps
	int stepTimer;
	
	// Presistent values
	double lastmovedir;
	int jumpcounter;
	
	// Dash
	bool enable_dash;
	double dashcooldown;
	double dashpower;
	int dashcooldown_time;
	int dashmax;
	int dashcounter;
	
	// Slide
	double slidecooldown;
	
	// Grapple
	bool gpl_validsurface;
	
	// Database(s)
	transient Dictionary surfacesData;
	
	// Velocities
	vector3 oldvel;

	void DoQuakeGroundAccel(vector3 wishdir, double spd)
	{
		double curspd = wishdir dot vel;
		double addspd = clamp(GNDMAX - curspd, 0, (GNDACCEL*spd) / TICRATE);
		
		vel += addspd * wishdir;
	}
	
	void DoQuakeAirAccel(vector3 wishdir, double spd, bool dobhop)
	{
		if(dobhop)
		{
			double curspd = wishdir dot vel;
			double addspd = clamp(AIRMAX - curspd, 0, (AIRACCEL*spd) / TICRATE);
					
			vel += (addspd*AIRCONTROL) * wishdir;
			if(AIRCONTROL < 1.) 
			{
				vector2 forward = (
					cos(angle),
					sin(angle)
				) * addspd;
				vel += forward * 0.1;
			}
		}
		else
		{
			double accel = (AIRACCEL*spd) / TICRATE;
			vector3 wishvel = vel + (wishdir * accel);
			double curspd = wishvel.Length();

			double addspd = clamp(AIRMAX - curspd, 0, accel);
			vel += (addspd*AIRCONTROL) * wishdir;
		}
	}
	
	bool CheckBuoyant()
	{
		return waterlevel >= 2 || player.cheats & CF_FLY || bFly;
	}
	
	String GetFloorSurface()
	{
		if(!surfacesData) 
			surfacesData = HaloDoom_Events.GetSurfacesData();
	
		if(!player.onground) return "None";
		
		string floorName = TexMan.GetName(floorpic);
		String surfaceType = surfacesData.At(floorName);
		
		// Attempt to automatically resolve terrain type.
		if(!surfaceType || !surfaceType.length()) 
		{
			surfaceType = "Halo/Surface/Normal"; 
			
			let terrain = GetFloorTerrain();
			String tName = terrain.TerrainName;
			if(terrain.isLiquid) 
				surfaceType = "Halo/Surface/Water";
			if(tName.IndexOf("grass") != -1) 
				surfaceType = "Halo/Surface/Nature";
		}
		
		return surfaceType;
	}
	
	void PlaySurfaceSound(string type)
	{
		string surfaceType = GetFloorSurface();
		A_StartSound(String.Format("%s/%s", surfaceType, type), CHAN_AUTO);
	}
	
	void PlayFootsteps()
	{
		double curSpeed = vel.xy.Length();
		double vol = curSpeed / (15 * speed * forwardMove1);

		// Don't play if moving too slowly or sliding.
		if(curSpeed <= 1 || FindInventory("SlideAnimator")) 
		{
			stepTimer = 0;
			return;
		}
		
		String surfaceType = GetFloorSurface();
		if(--stepTimer <= 0)
		{
			A_StartSound(String.Format("%s/Footstep", surfaceType), CHAN_AUTO, volume:vol);
			stepTimer = max(4, 20 - curSpeed);
		}
	}
	
	override void MovePlayer ()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;
		bool moving = (cmd.forwardmove || cmd.sidemove);
		
		// Play sound if landed from a fall.
		double zdiff = vel.z-oldvel.z;
		if(zdiff >= 10) A_StartSound("Halo/Spartan/Land/Hard", CHAN_AUTO);
		
		// [RH] 180-degree turn overrides all other yaws
		if (player.turnticks)
		{
			player.turnticks--;
			Angle += (180. / TURN180_TICKS);
		}
		else
		{
			Angle += cmd.yaw * (360./65536.);
		}
		
		if(curvehicle) return;
		
		// Process friction every couple of tics.
		double fric = 6.0 * (1.0-fricmod);
		if( player.onground && (fricwait > 4 || !halodoom_allowbhop || !enable_bhop) && fric > 0) 
			DoQuakeFriction(fric);
			
		player.onground = (pos.z <= floorz) || bOnMobj || bMBFBouncer || (player.cheats & CF_NOCLIP2);
		if(player.onground) 
			fricwait ++;
		else 
			fricwait = 0;
		
		DetectController();
		if (moving)
		{
			// Calculate wishdir
			UserCmd cmd = player.cmd;
			vector3 wishdir;
			wishdir.xy = RotateVector((cmd.forwardmove, -cmd.sidemove), angle);
			if(block_moveinput) wishdir.xy = (0,0);
			wishdir.z = 0;
			
		    lastmovedir = atan2(wishdir.y, wishdir.x);
			
			if(wishdir.Length())
			{
				wishdir = wishdir.Unit();
				if(player.onground || bFly) 
					DoQuakeGroundAccel(wishdir, Speed);
				else
					DoQuakeAirAccel(wishdir, Speed, halodoom_allowbhop && enable_bhop);
			}
			
			if (!(player.cheats & CF_PREDICTING)) PlayRunning ();
			if (player.cheats & CF_REVERTPLEASE)
			{
				player.cheats &= ~CF_REVERTPLEASE;
				player.camera = player.mo;
			}
		}
		
		uint btns = player.cmd.buttons;
		uint obtns = player.oldbuttons;
		
		// Slide
		if( (btns & BT_CROUCH) && !(obtns & BT_CROUCH) && vel.length() > 5 && player.onground && canslide && slidecooldown <= 0)
		{
			// If slide success and not predicting movement, play slide sound.
			if( SlideAnimator.DoSlide(self, SLIDE_TICS) && !(player.cheats & CF_PREDICTING) )
			{
				slidecooldown = SLIDE_COOL;
				A_StartSound("Halo/Spartan/Slide", CHAN_BODY, CHANF_OVERLAP);
			}
		}
		
		// Dash
		if( (btns & BT_DASH) && !(obtns & BT_DASH) && dashcounter < dashmax )
		{
			vel.xy *= 0;
			DashAnimator.DoDash(self, lastmovedir, dashpower);
			if (!(player.cheats & CF_PREDICTING)) A_StartSound("Halo/Spartan/Dash", CHAN_BODY, CHANF_OVERLAP);
			dashcounter++;
			dashcooldown = dashcooldown_time * 35.0;
		}
		
		if(slidecooldown) slidecooldown--;
		if(dashcooldown) dashcooldown--;
		if(dashcooldown <= 0 && player.onground) dashcounter = 0;
		
		// Grappling hook
		let weap = HaloGun(player.ReadyWeapon);
		
		if( (btns & BT_GRAPPLE) && !(obtns & BT_GRAPPLE) && enable_grapple && (weap && !weap.handsbusy) )
		{
			if(grapple)
			{
				grapple.Retract();
				return;
			}
			else
			{
				grapple = GrapplingHook.Deploy(self, (-10,-8,-10));
			}
		}
		
		// Determine if grapple is deployable.
		if(enable_grapple && !grapple)
			gpl_validsurface = GrapplingHook.CanDeploy(self, (-5,-2,-2));
		
		player.vel = vel.xy;
		if(block_moveinput) player.vel *= 0;
		
		oldvel = vel;
		PlayFootsteps();
	}		
	
	void DetectController()
	{
		UserCmd cmd = player.cmd;
		uint movebtns = (BT_FORWARD | BT_MOVELEFT | BT_MOVERIGHT | BT_BACK);
		
		bool moving_analog  = (cmd.forwardmove | cmd.sidemove);
		bool moving_digital = (cmd.buttons & movebtns);
		bool moving = moving_analog | moving_digital;
		
		if(moving) using_controller = !moving_digital;
	}
	
	override void CheckCrouch(bool totallyfrozen)
	{	
		let player = self.player;
		uint btns = player.cmd.buttons;
		uint obtns = player.oldbuttons;
		
		if(CheckBuoyant() && (btns & BT_CROUCH))
		{
			vel.z -= bFly ? 1 : 0.25;
			return;
		}
		
		super.CheckCrouch(totallyfrozen);
	}
	
	override void CheckJump()
	{
		let player = self.player;
		uint btns = player.cmd.buttons;
		uint obtns = player.oldbuttons;
		
		if(CheckBuoyant() && (btns & BT_JUMP))
		{
			vel.z += 1;
			return;
		}
		
		bool doholdjump = halodoom_holdjump && halodoom_allowbhop;
		if(player.onground) jumpcounter = 0;
		
		if(!player.onground && (btns & BT_JUMP) && !(obtns & BT_JUMP) && jumpcounter < maxjumps)
		{
			// Double jump
			vel.z = 0;	
			if(doJump())
			{
				if (!(player.cheats & CF_PREDICTING)) A_StartSound("Halo/Spartan/DoubleJump", CHAN_BODY, CHANF_OVERLAP);
				jumpcounter++;
			}
		} 
		else if ( player.onground && (btns & BT_JUMP) && (!(obtns & BT_JUMP) || doholdjump) )
		{
			// Normal jump
			if(doJump())
			{
				if (!(player.cheats & CF_PREDICTING)) A_StartSound("*jump", CHAN_BODY, CHANF_OVERLAP);
				jumpcounter++;
			}
		}
	}
	
	bool doJump(double forcemod = 1.0)
	{
		if (player.crouchoffset != 0) player.crouching = 1;
		if (waterlevel >= 2) Vel.Z = 4 * Speed;
		if (bNoGravity) Vel.Z = 3.;
		
		if (level.IsJumpingAllowed())
		{
			if(abs(level.time - prevjump) <= 4) return false;
			prevjump = level.time;
			
			double jumpvelz = JumpZ * 35 / TICRATE;
			double jumpfac = 0;
			prevjump = level.time;

			for (let p = Inv; p != null; p = p.Inv)
			{
				let pp = PowerHighJump(p);
				if (pp) jumpfac = pp.Strength > jumpfac ? pp.Strength : jumpfac;
			}
			if (jumpfac > 0) jumpvelz *= jumpfac;
			
			bool canJumpHere = (pos.z+height < ceilingz);
		
			if(canJumpHere)
			{
				Vel.Z += jumpvelz * forcemod;
				bOnMobj = false;
				player.jumpTics = -1;
				
				// Play jump sound(s)
				PlaySurfaceSound("Jumped");
			}
			return canJumpHere;
		}
		return false;
	}
	
	// Credits: Ivory Duke (ZMovement)
	double GetFloorAngle()
	{
		// Get Floor Normal
		Vector3 floornorm;
		F3DFloor floors;
		for(int i = 0; i < FloorSector.Get3DFloorCount(); i++)
		{
			if(FloorSector.Get3DFloor(i).Top.ZAtPoint(Pos.XY) ~== FloorZ)
			{
				floors = FloorSector.Get3DFloor(i);
				break;
			}
		}
		floornorm = floors ? -floors.Top.Normal : FloorSector.FloorPlane.Normal;
		return atan2(floornorm.XY.Length(), floornorm.Z);
	}
	
	void DoQuakeFriction(double friction)
	{
		double maxspd = GNDMAX;
		double unitfric = friction / TICRATE;
		double floorangle = GetFloorAngle();
		double swimming = WaterLevel >= 2 || bNOGRAVITY;
		double minvel = (swimming) ? 0.5 : 1.0;

		// Stop velocity of too small.
		if(vel.length() < minvel)
		{
			vel.xy = (0, 0);
			return;
		}
		// Lower friction if on a slope.
		if(floorangle >= 45 && player.onground) 
		{
			maxspd *= 4;
			friction /= 4;
		}
		
		// Apply friction
		double curspeed = vel.Length();
		if(curspeed)
		{
			double control = (swimming ? curspeed : (curspeed < maxspd ? GNDFRIC : curspeed));
			double drop = control * unitfric;
			double newspeed = curspeed - drop;
			double frictionmod = (newspeed <= 0 ? 0 : newspeed) / curspeed;
		
			vel.xy *= frictionmod;
			if(swimming) vel.z *= frictionmod;
		}
	}
}

class DashAnimator : Inventory
{
	double dashdir;
	double dashstr;
	double timeout;
	
	Default
	{
		+INVENTORY.UNDROPPABLE;
		inventory.maxamount 1;
	}
	
	static DashAnimator DoDash(Actor src, double dir, double spd, double dur = 5)
	{
		if(src.FindInventory("DashAnimator")) return null;
		let dash = DashAnimator(Actor.Spawn("DashAnimator"));
		if(dash)
		{
			dash.dashdir = dir;
			dash.dashstr = spd;
			dash.timeout = dur;
			src.AddInventory(dash);
			src.vel.z = 0;
		}
		return dash;
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		let plr = HaloPlayer(Owner);
		if(!plr) return;
		
		if(GetAge() > timeout) 
		{
			plr.bNOGRAVITY = Owner.default.bNOGRAVITY;
			if(GetAge() > timeout*2.0)
			{
				plr.RemoveInventory(self);
				plr.fricmod = 0;
				plr.enable_bhop = plr.default.enable_bhop;
				GoAwayAndDie();
			}
		}
		else
		{
			vector2 addvel = AngleToVector(dashdir, dashstr);
			plr.vel.xy += addvel;
			plr.fricmod = 1.0;
			plr.enable_bhop = true;
			plr.bNOGRAVITY = true;		
		}
	}
}

class SlideAnimator : Inventory
{
	double timeout;
	double animtimer;
	double def_fov; 
	
	Default
	{
		+INVENTORY.UNDROPPABLE;
		inventory.maxamount 1;
	}
	
	static SlideAnimator DoSlide(Actor src, double dur = 35)
	{
		if(src.FindInventory("SlideAnimator")) return null;
		let slide = SlideAnimator(Actor.Spawn("SlideAnimator"));
		if(slide)
		{
			slide.timeout = dur;
			src.AddInventory(slide);
			src.vel.xy *= 3.0;
		}
		return slide;
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		let plr = HaloPlayer(Owner);
		if(!plr) return;
		
		if(!def_fov) def_fov = plr.player.DesiredFOV;
		
		vector2 addvel = AngleToVector(atan2(plr.vel.y, plr.vel.x), 1.5);
		
		if(plr.player.onground) plr.vel.xy += addvel;
		plr.fricmod = 1.0;
		plr.block_moveinput = true;
		
		if(animtimer < 10)
		{
			plr.viewoffs -= 2.5;
			plr.A_SetPitch(plr.pitch - 0.45, SPF_INTERPOLATE);
			plr.player.DesiredFOV = HaloMath.Lerp(plr.player.DesiredFOV, def_fov*1.15, 0.10);
		} 
		else if(animtimer > timeout-10)
		{
			plr.A_SetPitch(plr.pitch + 0.45, SPF_INTERPOLATE);
			plr.viewoffs = HaloMath.Lerp(plr.viewoffs, 0, 0.1);
			plr.player.DesiredFOV = HaloMath.Lerp(plr.player.DesiredFOV, def_fov, 0.10);
		}
		animtimer++;
		
		if(GetAge() > timeout) 
		{
			plr.RemoveInventory(self);
			plr.fricmod = 0;
			plr.viewoffs = 0;
			plr.block_moveinput = false;
			plr.player.DesiredFOV = def_fov;
			GoAwayAndDie();
		}
	}
}