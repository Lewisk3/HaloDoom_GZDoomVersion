class Warthog_Tire : VehPart
{
	bool invert;
	double trueturn;
	double turnvel;
	Property BackTires : invert;
	
	Default
	{
		Radius 16;
		Height 16;
	}
	
	double GetSteerAngle(bool behind = false)
	{
		double fromangle = behind ? base.driver.angle-180 : base.driver.angle;
		return -HaloMath.AngleDiff(fromangle, base.angle);
	}
	
	override void UpdatePos(bool moving)
	{
		if(!base) return;
		vector3 finalpos = GetOffsetPos(base);
		SetOrigin(finalpos, moving);
		
		if(!base.driver) return;
		
		let warthog = HaloWarthog(base);
		
		// Determine angle based on aim.
		// Get angle of "steering wheel"
		double steerangle = GetSteerAngle();
		bool behind = steerangle < -(180 - 50) || steerangle > (180 - 50);
		if(behind) steerangle = GetSteerAngle(true);
		if(invert) steerangle = -steerangle;
		
		trueturn = base.angle + clamp(steerangle, -50, 50);
		steerangle = clamp(steerangle, -30, 30);
		
		A_SetAngle(steerangle + base.angle, SPF_INTERPOLATE);
		A_SetPitch(pitch + warthog.accel, SPF_INTERPOLATE);
		A_SetRoll(base.roll, SPF_INTERPOLATE);
	}
		
	States
	{
		Spawn:
			MODL A 1;
		loop;
	}
}
class Warthog_BackTire : Warthog_Tire 
{
	Default
	{
		WartHog_Tire.BackTires true;
	}
}

// Turret
class Warthog_TurretBase : VehPart
{
	Warthog_TurretGun gun;
	uint cooldown_primary;
	double spinspeed;
	double turretspin;
	
	Default
	{
		Radius 28;
		Height 32;
		+SOLID;
	}
	
	override void OnAttach()
	{
		gun = Warthog_TurretGun(VehPart.Attach("Warthog_TurretGun", base, (32,0,38), (0,0))); 
		if(gun) gun.gunbody = self;
	}
	
	override void UpdatePos(bool moving)
	{
		super.UpdatePos(moving);
		
		let warthog = HaloWarthog(base);
		if(!warthog) return;
		
		vector2 gunnerangles = (
			warthog.gunner ? warthog.gunner.angle : angle,
			warthog.gunner ? warthog.gunner.pitch : pitch
		);
		A_SetAngle(gunnerangles.x + warthog.angvel, SPF_INTERPOLATE);
		A_SetPitch(base.pitch, SPF_INTERPOLATE);
		
		gun.A_SetPitch(gunnerangles.y, SPF_INTERPOLATE);
	}
	
	override void DoUpdate(VehCamera cam)
	{
		let warthog = HaloWarthog(base);
		if(!warthog) return;

		bool shooting = warthog.gunner ? CheckButtons(BT_ATTACK, VehButtons.BTS_DOWN, warthog.gunner) : false;
		
		spinspeed = clamp(spinspeed, 0, 1.0);	
		if(!shooting && spinspeed > 0.01)
		{
			spinspeed *= 0.95;
			if(spinspeed <= 0.1) spinspeed = 0;
			if(spinspeed <= 0) A_StartSound("Halo/Warthog/Chaingun/Stop", flags:CHANF_OVERLAP);
		}
		if(spinspeed > 0.3) 
		{
			A_StartSound("Halo/Warthog/Chaingun/Loop", CHAN_6, flags:CHANF_LOOPING);
		}
		else
		{
			A_StopSound(CHAN_6);
		}

		if(!warthog.gunner) return;
		let gcam = VehCamera(warthog.gunner.player.camera);
		if(!gcam) return;
		
		if(cooldown_primary) 
		{
			gcam.SetZoom(1.0, 0.1);
			cooldown_primary--;
		}
		
		if( shooting && !cooldown_primary )
		{
			cooldown_primary = 10 - (7 * spinspeed);
			double rmax = 1.5;
			double arecoil = frandom(-rmax, rmax);
			double precoil = frandom(-rmax, rmax);
			let proj = HaloMath.FireProjectile("HaloMagnum_Bullet", gun.pos, gun.angle + arecoil, gun.pitch + precoil, 0, 40, 0, 1, 200);
			
			// We need to set the target of this projectile if we want hitmarkers to show up
			// for the person who shoots it.
			proj.target = warthog.gunner;
			
			A_StartSound("Halo/Warthog/Chaingun/Fire", CHAN_BODY, CHANF_OVERLAP);
			gcam.Quake(1.25,1.25,0,0.5);
			spinspeed += 0.1;
			if(spinspeed <= 0.2) A_StartSound("Halo/Warthog/Chaingun/Start", flags:CHANF_OVERLAP|CHANF_NOSTOP);
			turretspin += 45;
		}
	}
			
	States
	{
		Spawn:
			MODL A 1;
		loop;
	}
}

class Warthog_TurretGun : VehPart
{
	VehPart gunbody;
	Warthog_TurretBarrel gunbarrel;
	
	Default
	{
		Radius 16;
		Height 16;
	}
	
	override void OnAttach()
	{
		gunbarrel = Warthog_TurretBarrel(VehPart.Attach("Warthog_TurretBarrel", base, (15,0,0), (0,0))); 
		if(gunbarrel) gunbarrel.gun = self;
	}
	
	override void UpdatePos(bool moving)
	{
		if(!gunbody) return;
		vector3 finalpos = GetOffsetPos(gunbody);
		SetOrigin(finalpos, moving);
		
		AlignWithTerrain();
		A_SetAngle(gunbody.angle, SPF_INTERPOLATE);	
	}
	
	States
	{
		Spawn:
			MODL A 1;
		loop;
	}
}

class Warthog_TurretBarrel : VehPart
{
	VehPart gun;
	Default
	{
		Radius 16;
		Height 16;
	}
	
	override void UpdatePos(bool moving)
	{
		let warthog = HaloWarthog(base);
		if(!gun || !warthog) return;
		
		vector3 finalpos = GetOffsetPos(gun);
		SetOrigin(finalpos, moving);
		
		double proll = roll;
		AlignWithTerrain();
		
		roll = HaloMath.Lerp(proll, warthog.turret.turretspin, 0.15);
		A_SetAngle(gun.angle, SPF_INTERPOLATE);
		A_SetPitch(gun.pitch, SPF_INTERPOLATE);
	}
	
	States
	{
		Spawn:
			MODL A 1;
		loop;
	}
}

class HaloWarthog : HaloVehicle
{
	Warthog_Tire Tire_TL, Tire_TR, Tire_BL, Tire_BR;
	Warthog_TurretBase turret;
	
	double throttle; // Throttle
	double accel;    // Tire rotation velocity.
	double angvel;   // Angular velocity.
	uint horntics;   // How long the horn is being held for.
	
	// Passenger(s)
	PlayerPawn gunner;
	PlayerPawn passenger;
	
	Default
	{
		Radius 65;
		Height 55;
		Scale 45.0;
		
		Mass 800;
		Health 1000;
		HaloVehicle.Armored 500;
		MaxSlopeSteepness 0.2;
	}
	
	// Setup tires and turret.
	override void BeginPlay()
	{
		Tire_TL = Warthog_Tire(VehPart.Attach("Warthog_Tire", self, (65,-35,16), (0,0)));
		Tire_TR = Warthog_Tire(VehPart.Attach("Warthog_Tire", self, (65, 35,16), (0,0)));
		Tire_BL = Warthog_Tire(VehPart.Attach("Warthog_BackTire", self, (-65,-35,16), (0,0)));
		Tire_BR = Warthog_Tire(VehPart.Attach("Warthog_BackTire", self, (-65, 35,16), (0,0)));
		turret = Warthog_TurretBase(VehPart.Attach("Warthog_TurretBase", self, (-47, 0, 35), (0,0)));
		
		// Lights
		VehLight.Attach(self, offset:(50,-50,16));
		VehLight.Attach(self, offset:(50, 50,16));
		
		super.BeginPlay();
	}
	
	// Prevent collision with passengers and driver
	override bool CanCollideWith(Actor other, bool passive)
	{
		if(other == driver || other == passenger || other == gunner) return false;
		return super.CanCollideWith(other, passive);
	}
	
	override void OnDestroy()
	{
		turret.Destroy();
	}
	
	override vector3 GetExitPos(PlayerPawn user)
	{
		if(user == gunner) return (-120,0,20);
		if(user == passenger) return (0,80,10);
		return (0,-80,10);
	}
	
	override bool RemoveDriver(PlayerPawn user, vector3 exitpos)
	{
		bool success = super.RemoveDriver(user, exitpos);
		if(user == driver && success) A_StartSound("Halo/Warthog/EngineOff", flags:CHANF_OVERLAP);
		return success;
	}
	
	override bool OnDriveAttempt(PlayerPawn user)
	{
		double gundist = user.Distance2D(turret);
		if(user == driver) return true;
				
		// Take gunner.
		if(!gunner && gundist <= 70 && user != passenger) 
		{
			// Ready gunner seat.
			let cam = SetupHaloCamera(dist:-120,50, 30,100, setcam:false);
			cam.source = user;
			cam.followact = turret;
			AssignCamera(cam, user);
			ReadyDriver(user);
			SetCrosshair(user, "TurRET", (0.40,0.40));
			
			gunner = user;
			return false;
		}
		else if(gunner && gunner == user)
		{
			if(RemoveDriver(gunner, GetExitPos(gunner))) gunner = null;
			return false;
		}
	
		// Determine use angle
		vector2 usedir = Vec2To(user);
		double useangle = HaloMath.AngleDiff(atan2(usedir.y, usedir.x), angle);
		
		// Take passenger
		if(!passenger && useangle > 0)
		{
			ReadyDriver(user, false);
			passenger = user;
			return false;
		}	
		else if(passenger && passenger == user)
		{
			if(RemoveDriver(passenger, GetExitPos(passenger))) passenger = null;
			return false;
		}
		
		// Do take driver.
		return true;
	}
	
	override void TakeDriver(PlayerPawn user)
	{
		if(health <= 0) return;
		driver = user;
		
		SetupHaloCamera(dist:-140,60, 30,100);
		ReadyDriver(user);
		SetCrosshair(user, "UnarmRET", (1.0,1.0));
		A_StartSound("Halo/Warthog/EngineOn", flags:CHANF_OVERLAP);
	}
	
	override vector3 GetDrivePos()
	{	
		// Driver seat pos
		return (4,-18,15);
	}
	
	override void UpdatePassengers()
	{
		if(gunner) UpdateActorPos(gunner, (-60,0,35));
		if(passenger) UpdateActorPos(passenger, (4,15,15));
	}
	
	vector2 GetTireDir(Warthog_Tire tire)
	{
		return (cos(tire.trueturn), sin(tire.trueturn));
	}
	double GetTireVel(Warthog_Tire tire, double velmod = 0.025)
	{
		velmod *= HaloMath.fmap(vel.xy.length(), 0, 30, 0.05, 1.0); 
		return HaloMath.AngleDiff(angle, Tire_TL.angle) * velmod;
	}
	
	override void DoMove(float forwardmove, float sidemove)
	{	
		if(onground) 
		{
			vel.xy *= 0.85;
			accel *= 0.92;
		}
		else
		{
			vel.xy *= 0.98;
			accel *= 0.98;
		}
		
		if(pos.z+vel.z <= floorz && vel.z <= -3) 
		{
			A_StartSound("Halo/Warthog/Suspension", flags:CHANF_OVERLAP | CHANF_NOSTOP);
			double movepenalty = HaloMath.fmap(abs(vel.z), 0, 20, 0.98, 0.85);
			double bounce = clamp(abs(vel.z)/20.0, 0, 1.0) * 20;

			vel.xy *= movepenalty;
			vel.z += bounce;
			pitch -= bounce;
			throttle *= movepenalty;
		}
		
		// SFX
		if(!driver)
		{
			A_StopSound(CHAN_7); // Engine
			A_StopSound(CHAN_VOICE); // Horn
		}
		else
			A_StartSound("Halo/Warthog/EngineHum", CHAN_7, CHANF_LOOPING);
		
		if(abs(accel) > 0.001)
		{
			double topitch = HaloMath.fmap(accel, -10.0, 10.0, 0.5, 1.0); 
			A_StartSound("Halo/Warthog/DriveHum1", CHAN_5, CHANF_LOOPING);
			A_StartSound("Halo/Warthog/DriveHum2", CHAN_6, CHANF_LOOPING);
			A_SoundPitch(CHAN_5, topitch);
			A_SoundPitch(CHAN_6, topitch);
		}
		else
		{
			A_StopSound(CHAN_5); // Drive 1
			A_StopSound(CHAN_6); // Drive 2
		}
				
		// Physics
		DoCollisionDamage(hitsound:"Halo/Warthog/Collision");
				
		// Apply angular velocity
		A_SetAngle(angle + angvel, SPF_INTERPOLATE);
		if(gunner) gunner.A_SetAngle(gunner.angle + angvel, SPF_INTERPOLATE);
		if(passenger) passenger.A_SetAngle(passenger.angle + angvel, SPF_INTERPOLATE);
		angvel *= 0.92;
		
		// Validity check.
		if(health <= 0)  
		{
			if(driver) RemoveDriver(driver, GetExitPos(driver));
			if(gunner) RemoveDriver(gunner, GetExitPos(gunner));
			Destroy();
			return;
		}
		if(!driver) return;
		
		// Honk honk
		bool horn = CheckButtons(BT_ATTACK, VehButtons.BTS_DOWN);
		if(horn)
		{
			if(horntics < 5) 
				A_StartSound("Halo/Warthog/Horn", flags:CHANF_OVERLAP | CHANF_NOSTOP);
			else
				A_StartSound("Halo/Warthog/HornLoop", CHAN_VOICE, CHANF_LOOPING);
			
			driver.A_AlertMonsters(1024, AMF_TARGETEMITTER); 
			horntics++;
		}
		else
		{
			horntics = 0;
			A_StopSound(CHAN_VOICE);
		}
		
		if(!onground) return;
		
		// Add tire forces for velocities
		double tireforce = 0.80 * throttle;
		if(abs(throttle) > 0)
		{
			vel.xy += GetTireDir(Tire_TL) * tireforce;
			vel.xy += GetTireDir(Tire_TR) * tireforce;
			vel.xy += GetTireDir(Tire_BL) * tireforce;
			vel.xy += GetTireDir(Tire_BR) * tireforce;
			angvel += GetTireVel(Tire_TL) * abs(tireforce);
			angvel += GetTireVel(Tire_BR) * abs(tireforce);
		}
		
		// Handle throttle and acceleration.
		if(forwardmove)
		{
			vector2 movedir = (forwardmove, -sidemove).Unit();
			throttle += movedir.x * 0.015;
			accel += throttle * 1.5;
		}
		else
		{
			throttle *= 0.80;
		}		
		throttle = clamp(throttle, -0.5, 1.0);
		accel = clamp(accel, -10, 10);
							
		// Slow down while turning
		vector2 dir = AngleToVector(angle);
		vector2 aimdir = AngleToVector(viewcam.angle);
		double cosAng = (dir dot aimdir); // -1 to 1;
		vector2 slowforce = AngleToVector(angle, tireforce) * (1.0-cosAng);
		vel.xy -= slowforce;
	}
	
	States
	{
		Spawn:
			MODL A 1;
		loop;
		
		Death:
			MODL A -1;
		stop;
	}
}