class Overshield_LV1 : HaloOvershield
{	
	Default
	{
		HaloOvershield.ShieldBoost 2.0;
		HaloOvershield.UIFlashEffect "Graphics/Powerups/OS1.png";
		Inventory.PickupSound "Halo/Powerup/Shield1";
		Inventory.PickupMessage "Picked up an Overshield (LV1)";
	}
	
	States
	{
		Spawn:
			PWSH B 1 
			{
				HaloPowerup.DoParticles(self, 0xFFffa9ff);
			}
		loop;
	}
}

class Overshield_LV2 : HaloOvershield
{
	Default
	{
		HaloOvershield.ShieldBoost 3.0;
		HaloOvershield.UIFlashEffect "Graphics/Powerups/OS2.png";
		Inventory.PickupSound "Halo/Powerup/Shield2";
		Inventory.PickupMessage "Picked up an Overshield (LV2)";
	}
	
	States
	{
		Spawn:
			PWSH A 1 
			{
				HaloPowerup.DoParticles(self);
			//	DoLightning(10);
			}
		loop;
	}
}

class HasteTrail : HaloScatterShot_Trail
{
	Default
	{
		Scale 0.05;
		+INTERPOLATEANGLES;
	}
	
	override void Tick()
	{
		super.Tick();
		SetOrigin(level.vec3offset(pos, vel), true);
	}
}

class HaloPower_Haste : PowerupGiver replaces SoulSphere
{
	double bobz;
	vector3 spawnPos;

	Default
	{
		+BRIGHT;
		Scale 0.65;
		Height 8;
		Radius 8;
		+ROLLSPRITE;
		+INTERPOLATEANGLES;
		
		Powerup.Duration -30;
		Powerup.Type "PowerHaloSpeedBoost";
		+INVENTORY.AUTOACTIVATE;
	}
	
	override void BeginPlay()
	{
		super.beginPlay();
		spawnPos = pos;
	}
	
	override void AttachToOwner(Actor other)
	{
		super.AttachToOwner(other);
		OnPickup(other);
	}
	
	void DoLightning(double amt = 20, double dist = 5, class<LightningActor> type = "UnmakerLightning")
	{
		vector2 view = (0, 0);
		int adjustedAmt = max(amt * halodoom_effectintensity, 0);

		for(int i = 0; i < adjustedAmt; i++)
		{
			vector2 localPos = (frandom(-1,1) * dist, frandom(-1,1) * (dist / 1.2));
			vector3 lightningPos = HaloMath.RelativeV3Offset(self, 0,0,0, 0, localPos.x, localPos.y + bobz, 1.);				
			view.x = frandom(0,360);
			view.y = frandom(-60,60);

			BiriBiri.DoLightning(lightningPos, view, type:type, dist:30, 1.0, 12.0, 10.0, 30, 0);
		}
	}
	
	virtual void OnPickup(Actor other)
	{
		other.GiveInventory("Overshield_LV2", 1);
	}
	
	virtual void VisualFX()
	{
		roll += 2;
		bobz = sin(level.maptime*10) * 5;
		SetOrigin((pos.xy, spawnPos.z + 16 + bobz), true);
		HaloPowerup.DoParticles(self, pcol:"Gold", zoffset:0);
		
		vector2 localPos = (frandom(-1,1) * 6, frandom(-1,1) * (6 / 1.2));
		vector3 trailPos = HaloMath.RelativeV3Offset(self, frandom(0,360),0,0, 0, localPos.x, localPos.y + bobz, 1.);			
		let trail = Spawn("HasteTrail", trailPos);
		trail.vel.z = bobz*0.25;
		
		if( !(getAge()%10) ) DoLightning(10);
	}
	
	override void Tick()
	{
		super.Tick();
		if(!bSPECIAL)
		{
			GoAwayAndDie();
			return;
		}
		VisualFX();
	}
	
	States
	{
		Spawn:
			PWHA A -1;
		stop;
	}
}

class PowerStrengthSmoke : FragGrenade_Smoke
{
	Default
	{
		Scale 0.24;
	}
}
class PowerStrengthLightning : LightningBiri
{
	Default
	{
		Scale 0.35;
		Translation "0:255=@64[255,16,16]";
	}
}

class HaloPower_Strength : HaloPower_Haste replaces Megasphere
{
	Default
	{
		Scale 0.1;
		
		Powerup.Duration -30;
		Powerup.Type "PowerHaloDamageBoost";
		+INVENTORY.AUTOACTIVATE;
	}
	
	override void OnPickup(Actor other)
	{
		other.GiveBody(200,200);
	}
	
	override void VisualFX()
	{
		double phase = cos( 40 * sin(level.maptime*14) + 16 ) + 0.25;

		double scaleBob = phase;
		A_SetScale(default.Scale.X * scaleBob);

		bobz = sin(level.maptime*10) * 5;
		SetOrigin((pos.xy, spawnPos.z + 16 + bobz), true);
		HaloPowerup.DoParticles(self, pcol:"Red", amt: 20, zoffset:0);
		
		vector3 fxpos = (pos.xy, pos.z);
		if(!(GetAge()%4))
			HaloMath.SpawnFX("PowerStrengthSmoke", fxpos, angle, 1, 20, 20, rstyle:STYLE_Subtract);
			
		if( !(getAge()%20) ) DoLightning(random(4,8), type:"PowerStrengthLightning");
	}
	
	States
	{
		Spawn:
			PWDM A -1;
		stop;
	}
}