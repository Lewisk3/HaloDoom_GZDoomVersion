class HaloDoom_TitleMenu : HaloMenuBase
{
	double fadein;
	override void Init(Menu parent, ListMenuDescriptor desc)
	{
		Super.Init(parent, desc);
		
		MainBG = MenuTexture.Create("Graphics/UI/Title/TitleLogo.png",(0,-200),(0.5,0.5));
		mMouse = Mouse.Create("", 0,0,32,32);
		
		TextElement btntxt = newBtnText("CAMPAIGN", "HaloFont");
		buttons.push(Button.Create("BTN_OFF" ,  "BTN_ON" , 960, 680, 0.8, 0.25, btntxt));
		
		btntxt = newBtnText("LOAD GAME", "HaloFont");
		buttons.push(Button.Create("BTN_OFF" ,  "BTN_ON" , 960, 760, 0.8, 0.25, btntxt));
		
		btntxt = newBtnText("OPTIONS", "HaloFont");
		buttons.push(Button.Create("BTN_OFF" ,  "BTN_ON" , 960, 840, 0.8, 0.25, btntxt));
		
		btntxt = newBtnText("QUIT", "HaloFont");
		buttons.push(Button.Create("BTN_OFF" ,  "BTN_ON" , 960, 920, 0.8, 0.25, btntxt));
		
		for(int i = 0; i < buttons.Size(); i++)
			buttons[i].ScaleCollider(0.9, 0.7);
		
		bool title = (gamestate == GS_TITLELEVEL);
		if(title) S_ChangeMusic("Music/TITLEMUS" .. random[TMUS](1,5) .. ".ogg", force:true);
		
		DontDim = true;
		mMouse.SetPos(buttons[0].btnrect.pos);
		btn_selected = 0;
		fadein = title ? 1.0 : 0.15;
	}
	
	override bool MenuEvent(int mkey, bool fromcontroller)
	{
		if(!mMouse) return false;
		
		switch (mkey)
		{
		case MKEY_Up:
			btn_selected--;
			if(btn_selected < 0) btn_selected = buttons.Size()-1;
			mMouse.SetPos(buttons[btn_selected].btnrect.pos);
			return true;
		case MKEY_Down:
			btn_selected++;
			if(btn_selected > buttons.Size()-1) btn_selected = 0;
			mMouse.SetPos(buttons[btn_selected].btnrect.pos);
			return true;
		case MKEY_Enter:
			if(btn_selected >= 0 && btn_selected <= buttons.Size()-1) 
				buttons[btn_selected].clicked = true;
			return true;
		case MKEY_Back:
			if(gamestate != GS_TITLELEVEL) Close();
			return true;
		}
		return false;
	}
	
	void DStr(String str, vector2 pos, Color col = Font.CR_WHITE, vector2 scale=(1.15,1.15))
	{
		HLSBS.DrawString("BigFont",str,pos,0,col,1.0,scale);
	}
	
	override void Drawer()
	{
		super.Drawer();
		mMouse.Draw();
		
		vector2 ht_pos = (75,1000);
		for(int i = 0; i < buttons.Size(); i++)
		{
			if(buttons[i].btntext)
			{
				if(buttons[i].pressed)
					buttons[i].btntext.font_color = Font.CR_BLACK;
				else if(buttons[i].hovered)
					buttons[i].btntext.font_color = Font.CR_BLUE;
				else
					buttons[i].btntext.font_color = Font.CR_WHITE;
			}
		}
		
		if(gamestate == GS_LEVEL) 
		{
			fadein = 0.15;
		}
		
		if(fadein > 0.15) fadein -= 0.02 * deltatime;
		HLSBS.Dim(Color(255,0,0,0), fadein);
	}
	
	override void Ticker()
	{
		if(btn_selected != -1) mMouse.left_pressed = true;
	
		for(int i = 0; i < buttons.Size(); i++)
		{
			buttons[i].Update(mMouse, btn_selected == -1);
				
			if(buttons[i].clicked)
			{
				MenuSound("UI/PressButton");
				buttons[i].clicked = false;
				
				Switch(i)
				{
					case 0:	 	// New Game
						Menu.SetMenu('HaloPlayerClassMenu');
					break;
					case 1:		// Load Game
						Menu.SetMenu('LoadGameMenu');
					break;
					case 2:     // Options
						Menu.SetMenu('OptionsMenu');
					break;
					case 3:		// Exit
						Menu.SetMenu("QuitMenu");
					break;
					default:
					break;
				}
			}
		}
	}
}

// TitleMap Objects
class Halo_Ring : Actor
{
	Default
	{
		+NOINTERACTION;
		+INTERPOLATEANGLES;
	}
	
	States
	{
		Spawn:
			RING A 1
			{
				pitch += 0.125;
			}
		loop;
	}
}

class Halo_RingPlanet : Actor
{
	Default
	{
		+NOINTERACTION;
		+WALLSPRITE;
		Scale 0.75;
	}
	
	States
	{
		Spawn:
			PLNT A -1;
		stop;
	}	
}

class Halo_Sky : Actor
{
	Default
	{
		+NOINTERACTION;
	}
	
	States
	{
		Spawn:
			RING A -1 Bright;
		stop;
	}
}