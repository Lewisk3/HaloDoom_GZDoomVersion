class Halo_Spiker_Clip : Ammo 
{
	Default 
	{
		Inventory.MaxAmount 40;
	}
}

class HaloSpiker_Bullet : HaloSlowProjectile
{
	Default
	{
		HaloSlowProjectile.SetRanges SPIKER_RANGE1, SPIKER_RANGE2;
		HaloSlowProjectile.MinRangeDamage SPIKER_MINDMG; 
		HaloSlowProjectile.BaseDamage SPIKER_DMG;
		HaloSlowProjectile.Speed_MPS SPIKER_BMPS;
		HaloSlowProjectile.HeadshotDMGFactor SPIKER_HEDMOD;
		HaloSlowProjectile.SetupWallpinning 256, 35*3; // Can pin dead enemies 128 units away, for 3 seconds.
		
		Translation "0:255=@65[245, 164, 66]";
		DamageType "Nailgun";
		Scale 0.5;
		Gravity 0.25;
	}
	
	override void Tick()
	{
		super.Tick();
		bBRIGHT = true;
		
		// If we've somehow died in mid-air, then fall.
		if(!bMISSILE && !pinnedActor && !BlockingLine && pos.z > floorz)
		{
			bNOGRAVITY = false;
			pitch++;
			if(blockingMobJ && bNOGRAVITY) 
				SetState(FindState("Death.Explode"));
		}
	}
	
	States
	{
		Spawn:	
			TRAC A 2 Bright 
			{
				SmokeTrail.Start("Yellow", pos, pos - (vel * 5), (-angle, -pitch), 0.25);
			}
		loop;
		
		Death.Explode:
			TRAC A 1;
		stop;
		
		Death:
			TRAC A 35 A_StartSound("Halo/Weapons/Spiker/Impact", CHAN_AUTO, CHANF_OVERLAP);
			TRAC A 1 A_FadeOut(0.05);
		wait;
	}
}

class HaloSpiker_Shrapnel : HaloSlowProjectile
{
	Default
	{
		HaloSlowProjectile.SetRanges 0, 0;
		HaloSlowProjectile.MinRangeDamage 1.0; 
		HaloSlowProjectile.BaseDamage SPIKER_DMG;
		HaloSlowProjectile.Speed_MPS 150;
		HaloSlowProjectile.HeadshotDMGFactor 1.0;
		HaloSlowProjectile.SetupWallpinning 0, 0; 
	
		DamageType "Fire";
		Scale 0.5;
		Gravity 0.75;
		-NOGRAVITY;
	}
	
	double heat;
	
	override void Tick()
	{
		super.Tick();
		bBRIGHT = (heat > 0.1);
		
		frame = ceil(HaloMath.FMap(heat, 0.1, 1.0, 0, 4));
		
		if(heat <= 0.1) frame = 0;
		if(heat <= 0.01) 
			heat = 0;
		else
			heat *= 0.99;
		
		if(heat > 0.1)
		{
			// Spawn smoke.
			int period = 5 - floor(HaloMath.FMap(heat, 0, 1.0, 1, 4));
			if( !(GetAge() % period) )
			{
				Spawn("MuzzleSmoke", pos);
			}
		}
	}
	
	override void PostBeginPlay()
	{
		heat = 1.0;
		sprite = GetSpriteIndex(String.Format("SPK%d", random(1,3)));		
		super.PostBeginPlay();
	}
	
	States
	{
		Spawn:	
			"####" "#" 1;
		loop;
		
		Death:
			"####" "#" 0 A_StartSound("Halo/Weapon/Spiker/Shrapnel/Impact");
		DeathLoop:
			"####" "#" 5 
			{
				if(heat > 0.1) 
					HaloMath.ExplodeEx(self, 3, 16, damageType:"Fire", selfDamage:1.0);
			}
			"####" "#" 0 A_JumpIf(heat <= 0.01, "TrueDeath");
		loop;
		
		TrueDeath:
			"####" "#" 1 A_FadeOut(0.01);
		wait;
		
		// Tell GZDoom to preload these sprites.
		Cache:
			SPK1 "#" 0;
			SPK2 "#" 0;
			SPK3 "#" 0;
	}
}


class Halo_Spiker_Reserves : Ammo
{
	mixin WorldAmmoBehavior;
	Default 
	{
		Radius 8;
		Height 6;
		Scale 0.65;

		Inventory.Amount 30;
		Inventory.MaxAmount 160;
		Inventory.Icon "HLAMN0";
		Inventory.PickupSound "Halo/Pickup/Ammo";
		Inventory.PickupMessage "Picked up 30 Rounds for the Spiker";
		
		Ammo.BackpackMaxAmount 240;
	}
	
	States
	{
		Spawn:
			HLAM N -1 Bright;
		stop;
	}
}

class Halo_Spiker : HaloGun
{
	Default
	{
		Weapon.SlotNumber 2;
		Obituary "%o was skewered by %k's Spiker.";
		Inventory.PickupMessage	"Picked up the Type-25 Spiker.";
		Tag "Spiker";
		Weapon.BobSpeed 1.86;
		Weapon.BobRangeX 0.30;
		Weapon.AmmoType1 "Halo_Spiker_Reserves";
		Weapon.AmmoType2 "Halo_Spiker_Clip";
		Weapon.AmmoGive 40;
				
		// Recoil
		HaloGun.Bloom 0.8;
		HaloGun.BloomSpeed 0.35;
		HaloGun.BaseRecoil SPIKER_RECOIL;
		HaloGun.Inaccuracy SPIKER_ACCMOD;
		HaloGun.Crosshair "SpikrRet", 0.4;
		HaloGun.UIGraphic "WI_SPIKR", 0.75, 0.60;
		
		HaloGun.Availability "HL3";
		HaloGun.PowerTier P_MID;
		HaloGun.Rarity 80;
		
		RecoilWeapon.YOffsetRange -10, 40;
	}
	
	override void MarkPrecacheSounds()
	{
		// Prevent stutters by precaching weapon sounds.
		MarkSound("Halo/Weapons/Spiker/Fire");
		MarkSound("Halo/Weapons/Spiker/Fire/Bass");
		MarkSound("Halo/Weapons/Spiker/AltFire");
		MarkSound("Halo/Weapons/Spiker/Melee");
		MarkSound("Halo/Weapons/Spiker/Mech");
		MarkSound("Halo/Weapons/Spiker/Reload");
		MarkSound("Halo/Weapons/Spiker/Reload/Empty");
		MarkSound("Halo/Weapons/Spiker/Draw");
	}
	
	action void A_FireSpiker()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/Spiker/Fire", 0, true);
		A_WeapSound("Halo/Weapons/Spiker/Fire/Bass", CHANF_OVERLAP, true);
		A_ShootProjectile("HaloSpiker_Bullet", 5, 5);
		A_SetSFXPos(10, -6, -4);
		A_SpawnSFX("MuzzleSmoke", 0,0);
		A_TakeAmmo();
		A_UpdateSpread();
	}
	
	action void A_FireShrapnel()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/Spiker/AltFire", 0, true);
		A_WeapSound("Halo/Weapons/Spiker/Fire/Bass", CHANF_OVERLAP, true);
		for(int i = 0; i < 20; i++)
		{
			A_ShootProjectile("HaloSpiker_Shrapnel", 5, 5, 0.05);
			A_SetSFXPos(10, -6, -4);
			A_SpawnSFX("MuzzleSmoke", 0,0);
			A_TakeAmmo(1);
			A_UpdateSpread();
		}
	}
	

	States
	{
		Ready:
			TNT1 A 0 A_WeapSound("Halo/Weapons/Spiker/Draw");
			WGD1 ABCDE 1 A_DoSelectAnim(-5);
		Ready.Active:
			WGG1 A 1 A_HaloWeaponReady(WRF_ALLOWRELOAD, usescope:false);
		loop;
		
		Ready.Dryfire:
			WGG1 A 12 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 0 A_GunRaise();
		wait;

		Deselect:
			WGG1 A 1 A_GunLower();
		wait;
		
		Fire:
			TNT1 A 0 A_GunCheckReload();
			TNT1 A 0 
			{
				frame = random(0,1);
			}
			WGF1 "#" 1 Bright A_RecoilOffset(frandom(5,10),frandom(12,18),1);
			TNT1 A 0 
			{		
				A_FireSpiker();
				A_SetRecoil(1.0, 0.2, 2);
				A_RollQuake(1,10,1, true);
				A_SetZoom(0.99, 1);
			}
			WGF1 C 1 A_RecoilOffset(0,0,4);
			WGF1 D 1 
			{
				A_Light(0);
				A_SetZoom(1.0, 3);
				A_SetRecoil(-0.18, 0, 8);
			}
			TNT1 A 0 A_WeapSound("Halo/Weapons/Spiker/Mech", CHANF_OVERLAP);
			WGG1 A 1 A_RecoilOffset(0,0,4);
		goto Ready.Active;
		
		AltFire:
			TNT1 A 0 A_JumpIf(CountLoadedAmmo() < 20, "Reload");
			TNT1 A 0 A_WeapSound("Halo/Weapons/Spiker/AltFire/Prep", CHANF_OVERLAP);
			WGG1 AAAAAAAAAAAAAAAAA 2 
			{
				A_RecoilOffset(frandom(-4,4), frandom(-2,2), 2);
			}
 			WGF1 E 1 Bright A_RecoilOffset(frandom(5,10),frandom(12,18),1);
			WGF1 F 1 Bright 
			{		
				A_SetZoom(0.88, 1);
				A_SetRecoil(8.78, 0, 2);
				A_FireShrapnel();
				A_RollQuake(4,10,2);
				A_RecoilOffset(15, 15, 4);
			}
			WGF1 C 1 A_RecoilOffset(0,0,4);
			WGF1 D 1 
			{
				A_Light(0);
				A_RQuakeEx(3,3,3,6,0,20,"",QF_RELATIVE|QF_WAVE);
				A_SetZoom(1.0, 3);
				A_SetRecoil(-0.18, 0, 8);
			}
			TNT1 A 0 A_WeapSound("Halo/Weapons/Spiker/Mech", CHANF_OVERLAP);
			WGG1 A 8 A_RecoilOffset(0,0,4);
		goto Ready.Active;
		
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			TNT1 A 0 A_ScopeDisable();
			TNT1 A 0 A_StartSound("Halo/Weapons/Spiker/Reload", CHAN_RELOADING, CHANF_OVERLAP);
			WGR1 ABCDEFGHIJKLMNO 1; 
			WGR1 P 1
			{
				A_SetRecoil(-0.75, 1.25, 4);
			}
			WGR1 QRSTUVWXYZ 1;
			WGR2 ABCDEFGHIJKLMNO 1; 
			WGR2 P 1
			{
				A_SetRecoil(1.45, 0.4, 4);
				A_ReloadClip();
			}
			WGR2 QRSTUVWXYZ 1;
		goto Ready.Active;
		
		DoMelee:
			TNT1 A 1 
			{
				let meleeSwing = ResolveState("SpikerMelee.Swing");
				let meleeBash = ResolveState("SpikerMelee.Bash");
				State meleeFinal = meleeSwing;
				let psp = player.GetPSprite(PSP_WEAPON);
				if(psp) psp.SetState(meleeFinal);
			}
		stop;

		SpikerMelee.Bash:
			"####" "#" 1; // This is needed to ensure this state is actually completely set.
			TNT1 A 0 
			{
				invoker.handsbusy = true;
				A_StartSound("Halo/Weapons/Spiker/Melee", CHAN_AUTO, CHANF_OVERLAP);
			}
			TNT1 A 0 A_SetRecoil(0.5, 0, 8);
			WGM1 HGF 2;
			WGM1 E 2;
			TNT1 A 0 
			{
				A_SetRecoil(-2.5, -0.75, 8);
				A_RecoilOffset(0,20,6);
			}
			WGM1 FGHH 1
			{
				A_HaloMelee(50, 32, hitsnd:"Halo/Melee/Punch", "");
			}
			WGM1 I 10 A_RecoilOffset(0,40,1);
			WGM1 I 2 A_RecoilOffset(0,20,2);
			WGM1 H 2 A_RecoilOffset(0,0,4);
			WGG1 A 1 
			{
				invoker.handsbusy = false;
			}
		goto Ready.Active;
		
		SpikerMelee.Swing:
			"####" "#" 1;
			TNT1 A 0 
			{
				A_BeginMelee(false);
				A_StartSound("Halo/Weapons/Spiker/Melee", CHAN_AUTO, CHANF_OVERLAP);
			}
			WGM1 ABCDD 1;
			WGM1 J 1 
			{
				A_SetRecoil(0, 2.0, 8);
			}
			WGM1 KLM 1 
			{
				A_HaloMelee(25, 64, hitsnd:"Halo/Melee/Punch", "Halo/Melee/Ouch");
			}
			WGM1 NO 2;
			WGM1 O 16;
			WGM1 DCBA 1;
			WGG1 A 1 A_FinishMelee();
		goto Ready.Active;
		
		Spawn:
			WGP1 A -1;
		stop;
	}
}
