class Halo_BAR_Clip : Ammo 
{
	Default 
	{
		Inventory.MaxAmount 36;
	}
}
class Halo_BAR_Reserves : Ammo
{
	mixin WorldAmmoBehavior;
	Default 
	{
		Radius 8;
		Height 6;
		Scale 0.65;

		Inventory.Amount 36;
		Inventory.MaxAmount 144;
		Inventory.Icon "HLAMB0";
		Inventory.PickupSound "Halo/Pickup/Ammo";
		Inventory.PickupMessage "Picked up 36 Rounds for the Battle Rifle";
		
		Ammo.BackpackMaxAmount 216;
	}
	
	States
	{
		Spawn:
			HLAM B -1 Bright;
		stop;
	}
}

class HaloBattleRifle_Bullet : HaloBullet 
{
	Default
	{
		HaloProjectile.SetRanges BARIFLE_RANGE1, BARIFLE_RANGE2;
		HaloProjectile.MinRangeDamage BARIFLE_MINDMG; 
		HaloProjectile.BaseDamage BARIFLE_DMG;
		HaloProjectile.Speed_MPS BARIFLE_BMPS;
		HaloProjectile.RipperDamageFalloff 0.25;
		HaloProjectile.RipperCount 3;
		HaloProjectile.HeadshotDMGFactor BARIFLE_HEDMOD;
		
		// Do normal damage to body (1.0), don't deal any headshot bonus to head.
		HaloProjectile.ShieldDamageFactors 1.0, (1.0 / BARIFLE_HEDMOD);
		Scale 0.5;
	}
}

class Halo_BattleRifle : HaloGun
{	
	Default
	{
		Weapon.SlotNumber 1;
		Obituary "%o got a taste of competitive halo by %k's Battle Rifle.";
		Inventory.PickupMessage	"Picked up the Battle Rifle.";
		Tag "Battle Rifle";
		Weapon.BobSpeed 1.86;
		Weapon.BobRangeX 0.30;
		Weapon.AmmoType1 "Halo_BAR_Reserves";
		Weapon.AmmoType2 "Halo_BAR_Clip";
		Weapon.AmmoGive 36;
				
		// Recoil
		HaloGun.Bloom 0.8;
		HaloGun.BloomSpeed 0.30;
		HaloGun.BaseRecoil BARIFLE_RECOIL;
		HaloGun.Inaccuracy BARIFLE_ACCMOD;
		HaloGun.Crosshair "BARRet", 1.0;
		HaloGun.UIGraphic "WI_BTR", 1.0;
		HaloGun.UISight "SI_BR", 0.3, 0.7;
		HaloGun.SetupScope 2.0, 0.25;
		HaloGun.ZoomSounds "Halo/Weapons/Zoom/IN", "Halo/Weapons/Zoom/OUT";
		HaloGun.PowerTier P_MID;
		
		RecoilWeapon.YOffsetRange -10, 40;
	}
	
	override void MarkPrecacheSounds()
	{
		// Prevent stutters by precaching weapon sounds.
		MarkSound("Halo/Weapons/BattleRifle/Fire");
		MarkSound("Halo/Weapons/BattleRifle/Fire/Bass");
		MarkSound("Halo/Weapons/BattleRifle/Mech");
		MarkSound("Halo/Weapons/BattleRifle/Reload/Full");
		MarkSound("Halo/Weapons/BattleRifle/Reload/Empty");
		MarkSound("Halo/Weapons/BattleRifle/Select");
	}
	
	action void A_FireRifle()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/BattleRifle/Fire", 0, true);
		A_WeapSound("Halo/Weapons/BattleRifle/Fire/Bass", CHANF_OVERLAP, true);
		A_ShootProjectile("HaloBattleRifle_Bullet", 6);	
		A_SetSFXPos(20,-22,-15);
		A_SpawnSFX("BulletCasing", -90,-20);
		A_SetSFXPos(25,-10,-10);
		A_SpawnSFX("MuzzleSmoke", 0,0);
		A_TakeAmmo();
	}
	
	action void A_ReloadSound()
	{
		Sound reload_snd = "Halo/Weapons/BattleRifle/Reload/Full";
		if(!CountLoadedAmmo()) reload_snd = "Halo/Weapons/BattleRifle/Reload/Empty";
		A_StartSound(reload_snd, CHAN_RELOADING, CHANF_OVERLAP);
	}
	
	States
	{
		
		Ready:
			TNT1 A 0 
			{
				A_WeapSound("Halo/Weapons/BattleRifle/Select");
				return A_JumpIf(A_DoFullSelectAnimation(), "Ready.Chamber");
			}
			BTRD IHGFDCBA 1 A_DoSelectAnim();
		goto Ready.Active;
		Ready.Chamber:
			BTRS ABCDEFGHIJKLMNOPQRSTUVWXYZ[] 1 A_DoSelectAnim(0, true);
		Ready.Active:
			BTRI A 1 A_HaloWeaponReady(WRF_ALLOWRELOAD, true);
		loop;
		
		Ready.Dryfire:
			BTRI A 4 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 1 A_GunRaise();
		wait;

		Deselect:
			BTRD ABCDEFGH 1;
			BTRD I 1 A_GunLower();
		wait;
		
		
		//  - 900RPM -
		// 900/60 = ~15
		// 35/15 = ~2 frames.
		Fire:
			TNT1 A 0 A_GunCheckReload();
			BRFI A 1 Bright
			{
				A_RecoilOffset(0,8,1);
				A_ModifyRecoil(A_CheckInScope() ? 0.25 : 1.0);
			}
			BRFI B 2 
			{
				A_FireRifle();
				A_SetRecoil(0.3, 0, 4);
				A_RollQuake(2,6,1);
				A_RQuakeEx(1,2,2,4,0,20,"",QF_RELATIVE);
				A_RecoilOffset(8,10,2);
				A_UpdateSpread();
			}
			BRFI C 1 Bright
			{
				A_RecoilOffset(0,8,2);
				A_MechanismSound("Halo/Weapons/BattleRifle/Mech");
				A_GunCheckReload();
				A_Light(0);
			}
			BRFI D 2 
			{
				A_FireRifle();
				A_SetRecoil(0.3, 0, 4);
				A_RQuakeEx(1,2,2,3,0,20,"",QF_RELATIVE);
			}
			BRFI E 1 Bright
			{
				A_RollQuake(-2,8,2);
				A_RecoilOffset(0,8,1);
				A_MechanismSound("Halo/Weapons/BattleRifle/Mech");
				A_GunCheckReload();
				A_Light(0);
			}
			BRFI F 2 
			{
				A_FireRifle();
				A_SetRecoil(-0.15, 0, 8);
				//A_RQuakeEx(2,0,2,4,0,20,"",QF_RELATIVE);
				A_RQuakeEx(2,3,2,3,0,20,"",QF_RELATIVE|QF_WAVE);
				A_RecoilOffset(8,10,2);
			}
			BRFI G 1 A_MechanismSound("Halo/Weapons/BattleRifle/Mech");
			BRFI HI 1 
			{
				A_SetRecoil(-0.3, 0, 4);
				A_Light(0);
			}
			BRFI JKKKKK 1
			{
				A_SetRecoil(0.2, 0, 2);
				A_RecoilOffset(0,0,4);
			}
			BRFI K 2;
		goto Ready.Active;
		
		Reload: // 44
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			TNT1 A 0 A_ScopeDisable();
			TNT1 A 0 A_ReloadSound();
			BRR1 ABCDEFGHIJK 1;
			BRR1 L 1 A_SetRecoil(0, -0.4, 3);
			BRR1 MNO 1;
			BRR1 PQRSTUV 2;
			BRR1 W 1;
			BRR1 X 1 A_SetRecoil(0, 0.4, 3);
			TNT1 A 0 A_ReloadClip();
			BRR1 YZ 1;
			BRR2 ABCDEFGHHHH 1;
			TNT1 A 0 A_JumpIf(!CountReloadAmmo(), "Reload.Rechamber");
		Reload.Finish: // 7
			BRR3 ABCDEFG 1;
		goto Ready.Active;
		Reload.Rechamber: // 25
			BRR4 ABCDEF 1;
			BRR4 G 1 A_SetRecoil(0.4, 0, 3);
			BRR4 HIJK 1 A_SetRecoil(-0.4, 0, 4);
			BRR4 LMNOPQRSTUVWXY 1;
		goto Ready.Active;
		
		Spawn:
			BRFL A -1;
		stop;
	}
}
