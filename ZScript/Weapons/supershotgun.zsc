// SNDBase: Halo/Weapons/SuperShotgun/Fire,Select,Reload
/*
	Double Barrel:
	Role: Heavy Close Quarters Combat
	Ammo:: 2 per Mag ; 28 total
	RPM: 
	Double: 30 RPM
	Single: 50 RPM
	Velocity: 1463 meters per second
	Range: 75 Meters
	Damage Output: 
	Buckshot: 25 per pellet (15 per shell)
*/
class Halo_DBLShotgun_Shells : Ammo 
{
	Default{
		Inventory.MaxAmount 2;
	}
}

class HaloDBLShotgun_Bullet : HaloBullet 
{
	Default
	{
		HaloProjectile.SetRanges DBLSHOTGUN_RANGE1, DBLSHOTGUN_RANGE2;
		HaloProjectile.MinRangeDamage DBLSHOTGUN_MINDMG/10.; 
		HaloProjectile.BaseDamage DBLSHOTGUN_DMG;
		HaloProjectile.Speed_MPS DBLSHOTGUN_BMPS;
	}
}

class Halo_DBLShotgun : HaloGun replaces SuperShotgun
{
	Default
	{
		Weapon.SlotNumber 2;
		Obituary "%o was took down by %k's Double Barreled Shotgun.";
		Inventory.PickupMessage	"DBL Shotgun Acquired.";
		Tag "DBLShotgun";
		Weapon.BobSpeed 2.12;
		Weapon.BobRangeX 0.63;
		// [Lewisk] had to switch ammotype's 1 and 2 because thats how my
		// reload function works.
		Weapon.AmmoType1 "Shell";
		Weapon.AmmoType2 "Halo_DBLShotgun_Shells";
				
		// Recoil
		HaloGun.Bloom 0.8;
		HaloGun.BloomSpeed 0.08;
		HaloGun.BaseRecoil DBLSHOTGUN_RECOIL;
		HaloGun.Inaccuracy DBLSHOTGUN_ACCMOD;
		HaloGun.Crosshair "DBLSRet1";
		HaloGun.UIGraphic "WI_DBL";
	}
	
	action void A_FireDouble()
	{
		A_Light(4);
		A_WeapSound("Halo/Weapons/SuperShotgun/Fire");
		for(int i = 0; i < 15; i++)
			A_FireProjectile("HaloDBLShotgun_Bullet", A_RecoilSpread()+1, false, 8, 0, 0, A_RecoilSpread()-1.35);		
		A_TakeAmmo(2);
		A_UpdateSpread();
	}
	action void A_FireSingle()
	{
		A_Light(4);
		A_WeapSound("Halo/Weapons/SuperShotgun/Fire");
		for(int i = 0; i < 7; i++)
			A_FireProjectile("HaloDBLShotgun_Bullet", A_RecoilSpread()+1, false, 8, 0, 0, A_RecoilSpread()-1.35);		
		A_TakeAmmo(1);
		A_UpdateSpread();
	}
	
	States
	{
		
		Ready:
			TNT1 A 0 
			{
				A_WeapSound("Halo/Weapons/SuperShotgun/Select");
				A_WeaponOffset(0,50);
			}
			DBSE ABCDE 1;
		Ready.Active:
			DBSH A 1 
			{
				A_RWeaponReady(WRF_ALLOWRELOAD);
			}
		loop;
		
		Ready.Dryfire:
			DBSH A 12 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 1 A_RRaise(12);
		wait;

		Deselect:
			DBSE EDCBA 1;
			TNT1 A 0 A_RLower(12);
		wait;
		
		// DBL - 30RPM -
		// 30/60.  = 0.5
		// 35/0.5  = 70 frames
		// 26 frames for shot, leaves 44 for reload.
		Fire:
			TNT1 A 0 A_JumpIf(countinv(invoker.ammotype2)<2, "AltFire");
			TNT1 A 0 A_GunCheckReload();
			TNT1 A 0 A_SetZoom(0.71, 2);
			DBSF A 1 Bright
			{
				A_RecoilOffset(random(6,12),24,4);
				A_SetRecoil(7.69, 7.42, 1);
				A_FireDouble();
				A_QuakeEx(4,4,4,4,0,20,"",QF_RELATIVE|QF_WAVE);
				
			}
			DBSF B 2 Bright;
			TNT1 A 0 A_Light(0);
			DBSF CD 1 
			{
				A_QuakeEx(2,2,2,6,0,20,"",QF_RELATIVE ,2.,3.,2.);
				A_SetZoom(0.994, 2);
				A_SetRecoil(-0.26, -0.13, 8);
			}
			DBSF E 11 A_RecoilOffset(0,10,24);
			DBSF FGHI 2 A_RecoilOffset(0,0,8);
			DBSF J 2;
			TNT1 A 0 A_SetZoom(1.0, 2);
		goto Reload;
		
		// SGL - 50RPM -
		// 50/60.  = ~0.83
		// 35/0.83 = ~42 frames
		AltFire:
			TNT1 A 0 A_GunCheckReload();
			TNT1 A 0 A_SetZoom(0.86, 3);
			TNT1 A 0 
			{
				Name fspr = countinv(invoker.ammotype2) < 2 ? "DBFA" : "DBFB";
				A_SetSprite(fspr);
			}
			"####" A 1 Bright
			{
				A_RecoilOffset(random(2,7),12,4);
				A_Quake(3,4,0,20,"");
				A_FireSingle();
			}
			"####" B 2 Bright A_SetRecoil(3.44, 2.26, 1);
			"####" C 1 A_SetZoom(0.994, 2);
			"####" A 0 A_Light(0);
			"####" D 1 A_SetRecoil(-0.27, -0.16, 4);
			"####" E 12 A_RecoilOffset(0,0,10);
			DBSH A 5;
			TNT1 A 0 A_SetZoom(1.0, 2);
			TNT1 A 0 A_GunCheckReload();
		goto Ready.Active;
		
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			DBSR A 1 A_StartSound("Halo/Weapons/SuperShotgun/Reload",CHAN_WEAPON);
			DBSR BCD 1;
			TNT1 A 0 A_SetRecoil(0.56, -0.18, 4);
			DBSR EFGH 1;
			DBSR I 6;
			DBSR JKLM 1;
			DBSR NOP 3 
			{
				A_SetRecoil(-0.56, 0.18, 4);
				A_RecoilOffset(7,24,4);	
				A_ReloadClip();
			}
			DBSR QRSWXYZ 1 A_RecoilOffset(0,0,8);	
			DBRA ABCDE 2;
		goto Ready.Active;
		
		Spawn:
			HMAG A -1;
		stop;
		
		// Used for internal data reasons.
		SprCache:
			DBFA "#" 1;
			DBFB "#" 1;
		stop;
	}
}
