class HaloProjectile : FastProjectile
{
	double truedamage;
	double range_falloff;
	double range_max;
	double falloff_minimum;
	vector3 spawnpos;
	
	Property SetRanges : range_falloff, range_max;
	Property MinRangeDamage : falloff_minimum;
	Property BaseDamage : truedamage;
	
	Default
	{
		Radius 5;
		Height 5;
		Speed 60;
		Projectile;
		
		HaloProjectile.SetRanges 61, 123;
		HaloProjectile.MinRangeDamage 0.4; 
		HaloProjectile.BaseDamage 145;
	}
	
	override int SpecialMissileHit(Actor victim)
	{	
		if(victim == target) return 1;
		AdjustRangeDamage();
		//console.printf("Damage output: %f, Dist: %f", truedamage, getTravelDistance());
		victim.DamageMobJ(target,self,truedamage,DamageType,0,angle);
		class<Actor> blud = victim.GetBloodType(0);
		if(blud && bBLOODSPLATTER && victim.bISMONSTER) victim.Spawn(blud,pos);
		return -1;
	}
	
	double getTravelDistance(bool tometers = true)
	{
		double convert = tometers ? UNIT_METER : 1.0;
		return clamp(HaloMath.Distance3D(pos, spawnpos),1,int.max) / convert;
	}	
	virtual void AdjustRangeDamage()
	{
		// Do not calculate damage if we have no ranged damage.
		if(range_max <= 0) return;
		// Interpolate to minimum damage based on range maximum.
		double dist = getTravelDistance();
		// Negate damage at max range.
		if(dist >= range_max)
		{
			truedamage = 0;
			return;
		}
		// Dampen damage until minimum.
		if(dist >= range_falloff)
		{
			double distmax = dist/range_max;
			double dmgmod = (-1.5 * (distmax**2)) + 1;
			dmgmod = clamp(dmgmod, falloff_minimum, 1.0);
			truedamage = default.truedamage*dmgmod;
		}
	}
	virtual void Init()
	{
		spawnpos = pos;
		SetDamage(0);
	}
	
	override void PostBeginPlay()
	{
		Init();
		super.PostBeginPlay();
	}
	override void Tick()
	{
		super.Tick();
		if(target) SetOrigin(pos+target.vel, true);
	}
}

class NeedleCounter : Inventory
{
	Array<HaloNeedleProjectile> needles;
	default
	{
		inventory.maxamount 1;
	}
	int CountNeedles()
	{
		return needles.Size();
	}
	void AddNeedle(HaloNeedleProjectile needle)
	{
		needles.Push(needle);
	}
	void RemoveNeedle(HaloNeedleProjectile needle)
	{
		for(int i = 0; i < needles.Size(); i++)
		{
			if(needles[i] == needle) 
			{
				needles.Delete(i);
				break;
			}
		}
	}
	void ExplodeNeedles()
	{
		for(int i = 0; i < needles.Size(); i++)	needles[i].doexplode = true;
	}
}
class HaloNeedleProjectile : Actor
{
	bool doexplode;
	Actor hitactor;
	vector3 hitoffs;
	double hitz;
	double hitangle;
	double hitpitch;
	int explodetimer, explodedelay, explodecount;
	NeedleCounter needlecount;
	Property ExplodeDelay : explodedelay;
	Property ExplodeCount : explodecount;

	Default
	{
		Radius 1;
		Height 5;
		Projectile;
		Speed 45;
		Damage 1;
		BounceType "Doom";
		BounceCount 4;
		RenderStyle "Add";
		+INTERPOLATEANGLES;
		+BOUNCEONWALLS;
		+BOUNCEONFLOORS;
		+BOUNCEONCEILINGS;
		-BOUNCEONACTORS;
		Scale 0.25;
		HaloNeedleProjectile.ExplodeDelay 100;
		HaloNeedleProjectile.ExplodeCount 10;
	}
	
	override int SpecialMissileHit(Actor victim)
	{
		if(hitactor || victim == target) return 1;
		hitactor = victim;
		bSOLID = false;
		bMISSILE = false;
		vel *= 0;
		hitoffs = Level.SphericalCoords(pos, victim.pos, (angle, pitch));
		hitangle = angle-victim.angle;
		hitpitch = pitch-victim.pitch;
		hitz = pos.z-victim.pos.z;
		explodetimer = explodedelay;
		needlecount = NeedleCounter(victim.FindInventory("NeedleCounter"));
		if(!needlecount) 
		{
			victim.GiveInventory("NeedleCounter", 1);
			needlecount = NeedleCounter(victim.FindInventory("NeedleCounter"));
		}
		needlecount.AddNeedle(self);
		SetState(ResolveState("Spawn.HitActor"));
		return -1;
	}
	
	override void Tick()
	{
		super.Tick();
		if(!hitactor) return;
		double hitdist = hitoffs.z*0.25;
		double realang = hitactor.angle+hitoffs.x+hitangle;
		vector3 newpos = hitactor.Vec3Offset(cos(realang)*-hitdist, sin(realang)*-hitdist, hitz);
		angle = realang;
		pitch = hitactor.pitch+hitpitch;
		SetOrigin(newpos, true);
	}
	
	States
	{
		Spawn:	
			TRAC A 1 Bright;
		loop;
		Spawn.HitActor:
			TRAC A 1 Bright 
			{
				StateLabel finalst = Null;
				explodetimer--;
				if(hitactor && hitactor.health <= 0 && needlecount) 
					hitactor.RemoveInventory(needlecount); 
				
				if(doexplode) finalst = "Death.Explode";
				if(!bMISSILE && (!hitactor || hitactor.health <= 0) || explodetimer <= 0) 
					finalst = "Death.Explode";
				return ResolveState(finalst);
			}
		loop;
		Death.Explode:
			TRAC A 1 
			{
				if((needlecount && needlecount.CountNeedles() >= explodecount) && !doexplode) 
				{
					needlecount.ExplodeNeedles();
					hitactor.DamageMobJ(self, self.target, 60, "Explosive", 0, angle);
				}
				if(hitactor) hitactor.DamageMobJ(self, self.target, 20, "Explosive", 0, angle);
				if(needlecount) needlecount.RemoveNeedle(self);
				for(int i = 0; i < 10; i++) 
				{
					A_SpawnParticle(
						"Purple", SPF_FULLBRIGHT|SPF_RELVEL,
						 35,5,
						 angle+frandom(-90,90), 
						 frandom(-20,20),frandom(-20,20),frandom(-10,10),
						 10,10,frandom(-8,8),0,0,-0.1,1.0,0.14,0.15);
				}
			}
		stop;
	}
	
}

class HaloBullet : HaloProjectile
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 100;
		RenderStyle "Add";
		+INTERPOLATEANGLES;
		+BLOODSPLATTER
		
		Decal "BulletChip";
		Scale 0.5;
	}
	
	States
	{
		Spawn:	
			TRAC A 1 Bright;
		loop;
	}
}