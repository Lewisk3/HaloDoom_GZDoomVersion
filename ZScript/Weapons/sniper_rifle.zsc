
class Halo_SniperClip : Ammo 
{
	Default
	{
		Inventory.MaxAmount 4;
	}
}

class HaloSniper_Bullet : HaloBullet 
{
	Default
	{
		DamageType "SSG";
		HaloProjectile.SetRanges SNIPER_RANGE1, SNIPER_RANGE2;
		HaloProjectile.MinRangeDamage SNIPER_MINDMG; 
		HaloProjectile.BaseDamage SNIPER_DMG;
		HaloProjectile.Speed_MPS SNIPER_BMPS;
		HaloProjectile.RipperCount 2;
		HaloProjectile.HeadshotDMGFactor 2.0;
	}
}

class Halo_SniperRifle : HaloGun replaces RocketLauncher
{
	Default
	{
		Weapon.SlotNumber 2;
		Obituary "%o was took down by %k's Sniper Rifle.";
		Inventory.PickupMessage	"Sniper Rifle Acquired.";
		Tag "Sniper Rifle";
		Weapon.BobSpeed 2.12;
		Weapon.BobRangeX 0.63;
		// [Lewisk] had to switch ammotype's 1 and 2 because thats how my
		// reload function works.
		Weapon.AmmoType1 "RocketAmmo";
		Weapon.AmmoGive 4;
		Weapon.AmmoType2 "Halo_SniperClip";
				
		// Recoil
		HaloGun.Bloom 1.25;
		HaloGun.BloomSpeed 0.08;
		HaloGun.BaseRecoil SNIPER_RECOIL;
		HaloGun.Inaccuracy SNIPER_ACCMOD;
		HaloGun.Crosshair "SNPRet";
		HaloGun.UIGraphic "WI_SNPR";
		HaloGun.UISight "SI_SNPR", 1.0;
		HaloGun.SetupScope 4.0, 0.25;
		HaloGun.ZoomSounds "Halo/Weapons/Sniper/Zoom/IN", "Halo/Weapons/Sniper/Zoom/OUT";
		
		RecoilWeapon.YOffsetRange -10, 40;
	}
	
	action void A_FireSniper()
	{
		A_Light(4);
		A_WeapSound("Halo/Weapons/Sniper/Fire", CHANF_OVERLAP, true);
		A_WeapSound("Halo/Weapons/Sniper/Fire/Bass", CHANF_OVERLAP, true);
		vector3 shootpos = A_ShootProjectile("HaloSniper_Bullet", 1, 8);
		A_SetSFXPos(25,-5,-3);
		A_TrailSFX(shootpos);
		A_SetSFXPos(25,-5,-7);
		for(int i = 0; i < 15; i++)
			A_SpawnSFX("MuzzleSmoke", 0,0);
			
		A_TakeAmmo(1);
		A_UpdateSpread();
	}

	States
	{
		
		Ready:
			TNT1 A 0 A_WeapSound("Halo/Weapons/Sniper/Ready");
			HSND ABCDEF 1 A_DoSelectAnim();
		Ready.Active:
			HSNI A 1 
			{
				A_HaloWeaponReady(WRF_ALLOWRELOAD, true);
			}
		loop;
		
		Ready.Dryfire:
			HSNI A 4 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			HSND A 1 A_GunRaise();
		wait;

		Deselect:
			HSND FEDCB 1;
			HSND A 1 A_GunLower();
		wait;
		
		// DBL - 30RPM -
		// 30/60.  = 0.5
		// 35/0.5  = 70 frames
		// 26 frames for shot, leaves 44 for reload.
		Fire:
			TNT1 A 0 A_GunCheckReload();
			HSNF A 1 Bright
			{
				A_RecoilOffset(random(-12,12),24,3);
				A_SetRecoil(4.42, 3.42, 1);
				A_SetZoom(0.75, 2);
				A_FireSniper();
				A_RQuakeEx(2,2,2,14,0,20,"",QF_RELATIVE);
				A_RQuakeEx(4,10,4,6,0,20,"",QF_RELATIVE | QF_WAVE);
			}
			HSNF BC 1 Bright;
			TNT1 A 0 A_Light(0);
			HSNF DEFGGHIJKL 1 A_SetZoom(1.0, 7);
			HSNF L 4;
		goto Bolt;
		
		// 1,280ms @ 40ms per frame rendered.
		// ~45 frames
		Bolt:
			TNT1 A 0 A_ScopeDisable();
			TNT1 A 0 A_StartSound("Halo/Weapons/Sniper/Bolt",CHAN_WEAPON,CHANF_OVERLAP);
			HSNB ABCDEFGH 1;
			HSNB IJKLMNO 1 A_SetRecoil(-0.34, 0.67, 1);
			TNT1 A 0 
			{
				A_SetSFXPos(25,-20,-20);
				A_SpawnSFX("SmallCasing", -90,-20);
			}
			HSNB PPO 2;
			HSNB ONNMLKJ 1; 
			HSNB IHGFE 1 A_SetRecoil(0.34, -0.67, 1);
			HSNB DCBAA 1;
		goto Ready.Active;
				
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			TNT1 A 0 A_ScopeDisable();
			TNT1 A 0 A_StartSound("Halo/Weapons/Sniper/Reload",CHAN_WEAPON,CHANF_OVERLAP);
			HSR1 ABCDEFG 2;
			HSR1 HIJKLM 1;
			HSR1 NN 1 A_SetRecoil(-2.0, 0, 3);
			HSR1 OPQRS 1;
			HSR1 T 4;
			HSR1 UVWXYZ 1;
			HSR2 AB 2;
			HSR2 CC 1 A_SetRecoil(2.0, 0, 3);
		// This is... less than ideal but, I don't want to create a varible for this.
			TNT1 A 0 A_JumpIf(!CountLoadedAmmo(), "Reload.Empty");
		Reload.Full:
			TNT1 A 0 A_ReloadClip();
			HSR2 CDEFGHIJ 1;
			HSR2 KLMNOPQRST 2;
		goto Ready.Active;
		Reload.Empty:
			TNT1 A 0 A_ReloadClip();
			HSR2 CDEFGHIJ 1;
			HSR2 KLMNOPQRST 2;
		goto Bolt;

		Scope.ON:
			TNT1 A 1 A_ScopeON();
			TNT1 A 5 A_SetZoom(invoker.scopezoom * 0.5, 3, true);
			TNT1 A 0 
			{
				bool holding = A_ButtonStatus(BT_ALTATTACK, BTS_HELD) && !A_ButtonStatus(BT_ALTATTACK, BTS_PRESSED);
				invoker.zoom_btnstate = holding ? 0 : BTS_PRESSED;
				if(holding) return ResolveState("Scope.Ready");
				return ResolveState("Scope.Wait");
			}
		goto Scope.Wait;
		
		Scope.Two:
			TNT1 A 1 A_ScopeON();
			TNT1 A 5 A_SetZoom(invoker.scopezoom * 2.0, 3, true);
		goto Scope.Ready;
		
		Scope.Wait:
			TNT1 A 1 
			{
				State ret = ResolveState(null);
				if(!invoker.enablezoom) ret = ResolveState("Scope.OFF");
				if(A_ButtonStatus(BT_ALTATTACK, BTS_PRESSED)) ret = ResolveState("Scope.Two");
				return ret;
			}
		loop;

		Spawn:
			SNPF A -1;
		stop;
	}
}
