class Halo_AR_Clip : Ammo 
{
	Default{
		Inventory.MaxAmount 32;
	}
}

class HaloRifle_Bullet : HaloBullet 
{
	Default
	{
		HaloProjectile.SetRanges ARIFLE_RANGE1, ARIFLE_RANGE2;
		HaloProjectile.MinRangeDamage ARIFLE_MINDMG/10.; 
		HaloProjectile.BaseDamage ARIFLE_DMG;
		HaloProjectile.Speed_MPS ARIFLE_BMPS;
		Scale 0.5;
	}
}

class Halo_AssaultRifle : HaloGun
{
	const LCD_LLayer = 20;
	const LCD_RLayer = 21;
	int rolldir;
	
	Default
	{
		Weapon.SlotNumber 1;
		Obituary "%o was took down by %k's Magnum.";
		Inventory.PickupMessage	"Magnum Md6 Adquired.";
		Tag "Assault Rifle";
		Weapon.BobSpeed 1.86;
		Weapon.BobRangeX 0.30;
		// [Lewisk] had to switch ammotype's 1 and 2 because thats how my
		// reload function works.
		Weapon.AmmoType1 "Halo_Clip";
		Weapon.AmmoType2 "Halo_AR_Clip";
				
		// Recoil
		HaloGun.Bloom 0.6;
		HaloGun.BloomSpeed 0.44;
		HaloGun.BaseRecoil ARIFLE_RECOIL;
		HaloGun.Inaccuracy ARIFLE_ACCMOD;
		HaloGun.Crosshair "RifleRet";
		HaloGun.UIGraphic "WI_AR";
	}
	
	action void A_FireRifle()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/Rifle/Fire");
		A_FireProjectile("HaloRifle_Bullet", A_RecoilSpread()+1, false, 6, 0, 0, A_RecoilSpread()-1.35);		
		A_TakeAmmo();
		A_UpdateSpread();
	}
	
	States
	{
		
		Ready:
			TNT1 A 0 A_WeapSound("Halo/Weapons/Mag_MD6/Select");
			HARI AAAAA 1;
		Ready.Active:
			HARI A 1 
			{
				A_RWeaponReady(WRF_ALLOWRELOAD);
				A_Overlay(LCD_LLayer, "LCD_AmmoCount.Left");
				A_Overlay(LCD_RLayer, "LCD_AmmoCount.Right");
				A_OverlayFlags(LCD_LLayer, PSPF_ADDWEAPON|PSPF_ADDBOB, true);
				A_OverlayFlags(LCD_RLayer, PSPF_ADDWEAPON|PSPF_ADDBOB, true);
			}
		loop;
		
		Ready.Dryfire:
			HARI A 12 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 1 A_RRaise(12);
		wait;

		Deselect:
			HARI AAAAA 1;
			TNT1 A 0 A_RLower(12);
		wait;
		
		
		//  - 650RPM -
		// 650/60 = 10.83
		// 35/10.83 = ~3 frames.
		Fire:
			TNT1 A 0 
			{	
				return A_GunCheckReload();
			}
			HARF "#" 1
			{
				invoker.rolldir = randompick(-1, 1);
				A_SetRoll(roll+1*invoker.rolldir, SPF_INTERPOLATE);
				A_SetFrame(random(0,1));
				A_RecoilOffset(0,8,1);
				A_OverlayOffset(LCD_LLayer, 8, 2, WOF_ADD);
				A_OverlayOffset(LCD_RLayer, 8, 2, WOF_ADD);
			}
			TNT1 A 0 
			{
				A_FireRifle();
				A_SetRecoil(0.4, 0, 6);
				A_SetZoom(0.987,2);
				A_QuakeEx(1,2,0,3,0,20,"",QF_RELATIVE|QF_WAVE);
				A_RecoilOffset(8,10,2);
				A_OverlayOffset(LCD_LLayer, 0, 0);
				A_OverlayOffset(LCD_RLayer, 0, 0);
			}
			HARI A 1
			{
				A_SetRoll(roll-1*invoker.rolldir, SPF_INTERPOLATE);
				A_Light(0);
				A_SetZoom(1.0, 2);
				A_RecoilOffset(0,0,2);
				//A_OverlayOffset(LCD_LLayer, 0, 0);
				//A_OverlayOffset(LCD_RLayer, 0, 0);
			}
			HARI A 1 A_SetRecoil(-0.1, 0, 6);
		goto Ready.Active;
		
		// Ammo 10s count
		LCD_AmmoCount.Left:
			LCDL "#" 1 A_SetFrame(floor(countinv(invoker.AmmoType2)/10.), LCD_LLayer);
		loop;
		// Ammo 1s count
		LCD_AmmoCount.Right:
			LCDR "#" 1 A_SetFrame(floor(countinv(invoker.AmmoType2)%10.), LCD_RLayer);
		loop;
		
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			HARI A 10 A_ReloadClip();
		goto Ready.Active;
		
		Spawn:
			HMAG A -1;
		stop;
	}
}