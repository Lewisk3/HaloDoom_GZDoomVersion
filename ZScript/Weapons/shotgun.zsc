class Halo_Shotgun_Shells : Ammo 
{
	Default 
	{
		Inventory.MaxAmount 8;
	}
}

class HaloShotgun_Bullet : HaloBullet 
{
	Default
	{
		DamageType "Shotgun";
		HaloProjectile.SetRanges SHOTGUN_RANGE1, SHOTGUN_RANGE2;
		HaloProjectile.MinRangeDamage SHOTGUN_MINDMG; 
		HaloProjectile.BaseDamage SHOTGUN_DMG;
		HaloProjectile.Speed_MPS SHOTGUN_BMPS;
		HaloProjectile.HeadshotDMGFactor SHOTGUN_HEDMOD;
	}
}

class Halo_Shotgun : HaloGun replaces Shotgun
{
	Default
	{
		Weapon.SlotNumber 1;
		Obituary "%o was took down by %k's Shotgun.";
		Inventory.PickupMessage	"Shotgun Adquired.";
		Tag "Shotgun";
		Weapon.BobSpeed 2.0;
		Weapon.BobRangeX 0.10;
		Weapon.AmmoType1 "Shell";
		Weapon.AmmoGive 4;
		Weapon.AmmoType2 "Halo_Shotgun_Shells";
				
		// Recoil
		HaloGun.Bloom 1.10;
		HaloGun.BloomSpeed 0.08;
		HaloGun.BaseRecoil SHOTGUN_RECOIL;
		HaloGun.Inaccuracy SHOTGUN_ACCMOD;
		HaloGun.Crosshair "SGRet";
		HaloGun.UIGraphic "WI_SG";
	}
	
	override void MarkPrecacheSounds()
	{
		// Prevent stutters by precaching weapon sounds.
		MarkSound("Halo/Weapons/Shotgun/Fire");
		MarkSound("Halo/Weapons/Shotgun/Fire/Bass");
		MarkSound("Halo/Weapons/Shotgun/Pump");
		MarkSound("Halo/Weapons/Shotgun/Reload");
		MarkSound("Halo/Weapons/Shotgun/Ready");
	}
	
	action void A_FireShotgun()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/Shotgun/Fire", CHANF_OVERLAP, true);
		A_WeapSound("Halo/Weapons/Shotgun/Fire/Bass", CHANF_OVERLAP, true);
		A_SetSFXPos(25,-10,-10);
		for(int i = 0; i < 15; i++)
		{
			A_ShootProjectile("HaloShotgun_Bullet", 8);		
			A_SpawnSFX("MuzzleSmoke", 0,0);
		}
		A_TakeAmmo(1);
		A_UpdateSpread();
	}
	
	States
	{
		Ready:
			TNT1 A 0 A_WeapSound("Halo/Weapons/Shotgun/Ready");
			HSGS ABCDE 1 A_DoSelectAnim();
		Ready.Active:
			HSGI A 1 A_HaloWeaponReady(WRF_ALLOWRELOAD);
		loop;
		
		Ready.Dryfire:
			HSGI A 1;
		goto Ready.Active;

		Select:
			TNT1 A 1 A_GunRaise();
		wait;

		Deselect:
			HSGI A 1;
			HSGS EDCB 1;
			HSGS A 1 A_GunLower();
		wait;
		
		Fire:
			TNT1 A 0 A_GunCheckReload();
			HSGF A 1 Bright
			{
				A_SetRecoil(8.78, 0, 1);
				A_FireShotgun();
				A_RQuakeEx(3,3,3,6,0,20,"",QF_RELATIVE);
				A_RollQuake(4,10,2);
				A_RecoilOffset(15, 15, 4);
			}
			HSGF B 1 Bright
			{
				A_RQuakeEx(3,6,3,3,0,20,"",QF_RELATIVE|QF_WAVE);
			}
			HSGF C 1;
			TNT1 A 0 A_Light(0);
			HSGF D 6 A_RecoilOffset(15, 20, 4);
			HSGF EFGH 1 
			{
				A_RecoilOffset(8,10,6);
				A_SetRecoil(-0.36, -0.13, 10);
			}
			HSGF IJKK 1 A_RecoilOffset(0,0,8);
		goto Pump;
		
		Pump:
			TNT1 A 0 A_GunCheckReload();
			HSGP ABCDEFG 1;
			TNT1 A 0 A_WeapSound("Halo/Weapons/Shotgun/Pump", CHANF_OVERLAP);
			HSGP HI 1;
			HSGP J 1 A_SetRecoil(-0.25, -0.09, 6);
			HSGP KLLLLK 1 A_RecoilOffset(0,10,6);
			HSGP J 1 
			{
				A_RecoilOffset(0,0,14);
				A_SetRecoil(0.25, 0.09, 4);
				A_SetSFXPos(25,-25,-15);
				A_SpawnSFX("ShellCasing", -90,20);
			}
			HSGP IHGFEDCBA 1;
			HSGI A 4;
		goto Ready.Active;
		
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
		Reload.Ready:
			SGR1 ABCDEFGHI 1;
		Reload.Loop:
			SGR2 ABCDE 1;
			SGR2 F 1
			{
				A_ReloadSingle();
				A_SetRecoil(-0.12, 0.12, 2);
				A_WeapSound("Halo/Weapons/Shotgun/Reload", CHANF_OVERLAP);
			}
			SGR2 GHIJKLM 2;
			TNT1 A 0 A_JumpIf(CountLoadedAmmo() <= 1, "Reload.Pump");
			TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_ATTACK, "Fire");
			TNT1 A 0 A_ValidateReload("Reload.Finish");
		goto Reload.Loop;
		Reload.Pump:
			SGR3 ABCD 1;
			SGR3 E 1 A_WeapSound("Halo/Weapons/Shotgun/Reload/Pump", CHANF_OVERLAP);
			SGR3 FGFEDCBA 1;
		goto Reload.Loop;
		Reload.Finish:
			SGR1 IHGFEDCBA 1;
		goto Ready.Active;
		
		Spawn:
			SGFL A -1;
		stop;
	}
}