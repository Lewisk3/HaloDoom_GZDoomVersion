class Halo_Shotgun_Shells : Ammo 
{
	Default 
	{
		Inventory.MaxAmount 8;
	}
}
class Halo_Shotgun_Reserves : Ammo
{
	mixin WorldAmmoBehavior;
	Default 
	{
		Radius 8;
		Height 6;
		Scale 0.65;
		
		Inventory.Amount 8;
		Inventory.MaxAmount 32;
		Inventory.Icon "HLAMK0";
		Inventory.PickupSound "Halo/Pickup/Ammo";
		Inventory.PickupMessage "Picked up 8 Shells for the Tactical Shotgun";
		
		Ammo.BackpackMaxAmount 50;
	}
	
	States
	{
		Spawn:
			HLAM K -1 Bright;
		stop;
	}
}

class HaloShotgun_Bullet : HaloBullet 
{
	Default
	{
		DamageType "Shotgun";
		HaloProjectile.SetRanges SHOTGUN_RANGE1, SHOTGUN_RANGE2;
		HaloProjectile.MinRangeDamage SHOTGUN_MINDMG; 
		HaloProjectile.BaseDamage SHOTGUN_DMG;
		HaloProjectile.Speed_MPS SHOTGUN_BMPS;
		HaloProjectile.HeadshotDMGFactor SHOTGUN_HEDMOD;
	}
}

class Halo_Shotgun : HaloGun
{
	Default
	{
		Weapon.SlotNumber 3;
		Obituary "%o got an 8-Gauge Breakfast from %k's Tactical Shotgun";
		Inventory.PickupMessage	"Picked up the M35 Tactical Shotgun";
		Tag "Shotgun";
		Weapon.BobSpeed 2.0;
		Weapon.BobRangeX 0.10;
		Weapon.AmmoType1 "Halo_Shotgun_Reserves";
		Weapon.AmmoGive 12;
		Weapon.AmmoType2 "Halo_Shotgun_Shells";
		
		HaloGun.Availability "HL1, HL2, HL3, HL4, MSX";
		HaloGun.PowerTier P_MID;
				
		// Recoil
		HaloGun.Bloom 1.10;
		HaloGun.BloomSpeed 0.08;
		HaloGun.BaseRecoil SHOTGUN_RECOIL;
		HaloGun.Inaccuracy SHOTGUN_ACCMOD;
		HaloGun.Crosshair "SGRet", 0.8;
		HaloGun.UIGraphic "WI_SG", 1.0, 1.0;
	}
	
	override void GetStatsLore(out Array<String> stats, out string lore)
	{
		stats.Push("M35 8-Gauge Pump Action Tactical Shotgun");
		stats.Push("Ammo Type: 8-Gauge Western Judge Type #14-C");
		stats.Push("Frame: Top Loading Pump Action");
		stats.Push("Feed System: 8 Shell Integrated Magazine");	
		stats.Push(String.Format("Base Damage: %d", SHOTGUN_DMG*15));
		
		lore = (
			"The M35 8-Gauge Pump Action Tactical Shotgun was the predecessor "
			"of the M45 Tactical Shotgun. It has a long service record from "
			"before the human covenant war. It's rugged, reliable, and a master "
			"at close quarters combat. Sporting the powerful Misriah Armory Western "
			"Judge 8-Gauge Type #14-C Buckshot Shell, it was a force to be "
			"reckoned with to anyone facing down its barrel, and even when "
			"the Human Covenant War began, it still gave the Covenant a "
			"run for its money."
		);
	}
	
	override void MarkPrecacheSounds()
	{
		// Prevent stutters by precaching weapon sounds.
		MarkSound("Halo/Weapons/Shotgun/Fire");
		MarkSound("Halo/Weapons/Shotgun/Fire/Bass");
		MarkSound("Halo/Weapons/Shotgun/Pump");
		MarkSound("Halo/Weapons/Shotgun/Reload");
		MarkSound("Halo/Weapons/Shotgun/Ready");
	}
	
	action void A_FireShotgun()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/Shotgun/Fire", CHANF_OVERLAP, true);
		A_WeapSound("Halo/Weapons/Shotgun/Fire/Bass", CHANF_OVERLAP, true);
		A_SetSFXPos(25,-10,-10);
		for(int i = 0; i < 15; i++)
		{
			A_ShootProjectile("HaloShotgun_Bullet", 8);		
			A_SpawnSFX("MuzzleSmoke", 0,0);
		}
		A_MuzzleSparks(30, 50, 8,8);
		A_TakeAmmo(1);
		A_UpdateSpread();
	}
	
	States
	{
		Ready:
			TNT1 A 0 A_JumpIf(!A_DoFullSelectAnimation(), "Ready.Chambered");
			TNT1 A 4 A_WeapSound("Halo/Weapons/Shotgun/Ready");
			W3D1 ABCDEFGHIJKLMNOPQRSTUVWX 1 A_DoSelectAnim(-5, true);
		goto Ready.Active;
		Ready.Chambered:
			TNT1 A 0 A_WeapSound("Halo/Weapons/Shotgun/Ready");
			W3S1 FEDCBA 1 A_DoSelectAnim(-5);
		Ready.Active:
			W3I1 A 1 A_HaloWeaponReady(WRF_ALLOWRELOAD, true);
		loop;
	
		Ready.Dryfire:
			W3I1 A 1;
		goto Ready.Active;

		Select:
			TNT1 A 1 A_GunRaise();
		wait;

		Deselect:
			W3S1 ABCDE 1;
			W3S1 F 1 A_GunLower();
		wait;
		
		Fire:
			TNT1 A 0 A_GunCheckReload();
			W3F1 A 1 Bright
			{
				A_SetZoom(0.80, 2);
				A_SetRecoil(8.78, 0, 2);
				A_FireShotgun();
				A_RQuakeEx(4,2,1,3,0,20,"",QF_RELATIVE|QF_WAVE);
				A_RollQuake(6,10,1);
				// A_RecoilOffset(15, 15, 4);
			}
			W3F1 B 1 Bright;
			W3F1 C 1
			{
				A_SetZoom(1.0, 6);
			}
			TNT1 A 0 A_Light(0);
			W3F1 D 6 
			{	
				// A_RecoilOffset(15, 20, 4);
				A_RQuakeEx(2,2,2,4,0,20,"",QF_RELATIVE);
			}
			W3F1 EFGGGH 1 
			{
				// A_RecoilOffset(8,10,6);
				A_SetRecoil(-0.36, -0.13, 10);
			}
			W3F1 IJK 1 A_RecoilOffset(0,0,8);
		goto Pump;
		
		Pump:
			TNT1 A 0 A_GunCheckReload();
			TNT1 A 0 A_JumpIf(!(level.maptime%4), "Pump.Alt");
			W3P1 ABCDEFG 1;
			TNT1 A 0 A_WeapSound("Halo/Weapons/Shotgun/Pump", CHANF_OVERLAP);
			W3P1 H 1;
			W3P1 I 1 A_SetRecoil(-0.25, -0.09, 6);
			W3P1 JKLLL 1 A_RecoilOffset(0,10,6);
			W3P1 K 1 
			{
				A_RecoilOffset(0,0,14);
				A_SetRecoil(0.25, 0.09, 4);
				A_SetSFXPos(25,-25,-15);
				A_SpawnSFX("ShellCasing", -90,20);
			}
			W3P1 JIHGFEDCBA 1;
			W3P1 A 4;
		goto Ready.Active;
		
		Pump.Alt:
			W3P1 MNOPQRS 1;
			TNT1 A 0 A_WeapSound("Halo/Weapons/Shotgun/Pump", CHANF_OVERLAP);
			W3P1 T 1;
			W3P1 U 1 A_SetRecoil(-0.09, -0.25, 6);
			W3P1 VWXXX 1 A_RecoilOffset(0,10,6);
			W3P1 W 1 
			{
				A_RecoilOffset(0,0,14);
				A_SetRecoil(0.09, 0.25, 4);
				A_SetSFXPos(25,-25,-25);
				A_SpawnSFX("ShellCasing", -90,20);
			}
			W3P1 VUTSRQPONM 1;
			W3P1 M 4;
		goto Ready.Active;
		
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
		Reload.Ready:
			W3R1 ABCDEFGHIJ 1;
		Reload.Loop:
			W3R1 JKLMNOPQR 1;
			W3R1 S 1
			{
				A_ReloadSingle();
				A_SetRecoil(-0.12, 0.12, 2);
				A_StartSound("Halo/Weapons/Shotgun/Reload", CHAN_RELOADING, CHANF_OVERLAP);
			}
			W3R1 TUVWXYZ 1;
			W3R2 ABC 1;
			TNT1 A 0 A_ValidateReload("Reload.Finish");
			TNT1 A 0 A_JumpIf(CountLoadedAmmo() <= 1, "Reload.Pump");
			TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_ATTACK, "Fire");
		goto Reload.Loop;
		Reload.Pump:
			W3R1 IHGFEDCBA 1;
			W3P1 ABCDEFG 1;
			W3P1 H 1 
			{
				A_WeapSound("Halo/Weapons/Shotgun/Pump", CHANF_OVERLAP);
				A_RecoilOffset(0,0,14);
				A_SetRecoil(0.25, 0.09, 4);
			}
			W3P1 IJKL 1;
			W3P1 LKJIHGFEDCBA 1;
			W3R1 ABCDEFGHIJ 1;
		goto Reload.Loop;
		Reload.Finish:
			W3R1 IHGFEDCBA 1;
		goto Ready.Active;
		
		Spawn:
			W3G1 A -1;
		stop;
	}
}
