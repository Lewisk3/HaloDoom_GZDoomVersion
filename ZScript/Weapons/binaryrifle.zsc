
class Halo_BinaryRifleBattery : Ammo 
{
	Default
	{
		Inventory.MaxAmount 100;
	}
}

class LSR_BinaryRifle : LSR_UnmakerBeam
{
	Default
	{
		Alpha 2.0;
		Scale 8.0;
		LaserBeam.LaserColor "Yellow"; 
		LaserBeam.TrackWeapon true, PSP_WEAPON;
		LaserBeam.AimFromWeapon false;
		LaserBeam.LaserDecal "UnmakerDecal";
		+BRIGHT;
	}
	
	override void BeamTick()
	{	
		if(GetAge() < 2) 
			aimAtCrosshair();

		alpha -= 0.10;
		A_SetScale(Scale.X * 0.87, Scale.Y);
	
		if(alpha <= 0.1 || Scale.X <= 0.05) Destroy();
	}
	
	override void OnImpact(vector3 hitPos, Actor hitActor)
	{
		super.OnImpact(hitPos, hitActor);
		let puff = UnmakerLaserPuff(Spawn("BinaryRiflePuff", hitPos));
		if(puff) 
		{
			puff.angle = angle + 180;
			puff.pitch = -(pitch-90);
		}
		
		if(hitActor) hitActor.DamageMobJ(self, source, BINARYRIFLE_DMG, "Plasma");
	}
}
class LSR_BinaryRifleCore : LSR_BinaryRifle
{
	Default
	{
		Alpha 2.0;
		Scale 4.0;
		LaserBeam.LaserColor "White"; 
		LaserBeam.TrackWeapon true, PSP_WEAPON;
		LaserBeam.AimFromWeapon false;
		+BRIGHT;
	}
	
	override void OnImpact(vector3 hitPos, Actor hitActor) {}
}
class BinaryRiflePuff : UnmakerLaserPuff
{
	Default
	{
		UnmakerLaserPuff.laserPuffs 6, 100;
	}
}

class Halo_BinaryRifle : HaloGun
{
	Default
	{
		Weapon.SlotNumber 5;
		Obituary "%o was disintegrated by %k's Binary Rifle.";
		Inventory.PickupMessage	"Picked up the Binary Rifle.";
		Tag "Binary Rifle";
		Weapon.BobSpeed 2.12;
		Weapon.BobRangeX 0.63;
		Weapon.AmmoType1 "Halo_BinaryRifleBattery";
		Weapon.AmmoType2 "Halo_BinaryRifleBattery";
		Weapon.AmmoGive 100;
		HaloGun.HasBattery true, 5;
		
		HaloGun.Availability GT_HL4;
		HaloGun.PowerTier P_POWER;
				
		// Recoil
		RecoilWeapon.RecoilSpeed 20;
		HaloGun.Bloom 1.25;
		HaloGun.BloomSpeed 0.08;
		HaloGun.BaseRecoil 1;
		HaloGun.Inaccuracy 1;
		HaloGun.Crosshair "SNPRet", 1.0;
		HaloGun.UIGraphic "MISSNG", 1.0;
		
		HaloGun.UISight "BINRADS", 1.5, 1.0;
		HaloGun.SetupScope 2.0, 0.5;
		+HaloGun.SI_Fullscreen;
		+HaloGun.SI_Rotate;
		+HaloGun.SI_AlwaysHide;
		
		HaloGun.ZoomSounds "Halo/Weapons/BinaryRifle/Zoom/IN", "Halo/Weapons/BinaryRifle/Zoom/OUT";
		RecoilWeapon.YOffsetRange -10, 40;
	}
	
	int smokeTics;
	action void A_DoRecoilSmoke()
	{
		A_SetSFXPos(20,-8,2);
		for(int i = 0; i < 15; i++)
			A_SpawnSFX("MuzzleSmoke", 0,0);
		invoker.smokeTics = 0;
	}
	
	action void A_FireBinaryRifle()
	{
		A_Light(4);
		A_WeapSound("Halo/Weapons/BinaryRifle/Fire", CHANF_OVERLAP, true);
		A_WeapSound("Halo/Weapons/BinaryRifle/Fire/Bass", CHANF_OVERLAP, true);
		
		double lr = A_CheckInScope() ? 0 : 2;
		let beam = LaserBeam.Create(self, 7, lr, -2, type:"LSR_BinaryRifle");
		let beamCore = LaserBeam.Create(self, 7, lr, -2, type:"LSR_BinaryRifleCore");
		
		beam.setEnabled(true);
		beamCore.setEnabled(true);
					
		A_TakeAmmo(5);
		A_UpdateSpread();
	}

	States
	{
		
		Ready:
			TNT1 A 0 A_WeapSound("Halo/Weapons/BinaryRifle/Draw");
			WOD1 HGFEDCBA 1 A_DoSelectAnim();
		Ready.Active:
			WOI1 A 1 
			{
				A_HaloWeaponReady(WRF_ALLOWRELOAD, true);
				if(A_CheckInScope()) A_ResetSightGraphic();
			}
		loop;
		
		Ready.Dryfire:
			WOI1 A 4 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 1 A_GunRaise();
		wait;

		Deselect:
			WOD1 ABCDEFGH 1;
			WOI1 H 1 A_GunLower();
		wait;
		
		Fire:
			TNT1 A 0 A_GunCheckReload();
			WOF1 A 1 Bright;
			WOF1 B 1 Bright
			{
				A_SetZoom(0.90, 2);
				if(A_CheckInScope()) 
					A_SetSightGraphic("BINRADSF");
				
				A_FireBinaryRifle();
			}
			WOF1 C 2 Bright;
			WOF1 D 1 Bright
			{
				A_RecoilOffset(random(-12,12),12,3);
				A_SetRecoil(4.20, 2.40, 2);
				A_RollQuake(4, 10, 2, true);
			}
			TNT1 A 0 
			{
				A_Light(0);
			//	A_RQuakeEx(3,3,3,10,0,20,"",QF_RELATIVE);
			}
			WOF1 EFG 1 A_SetZoom(1.0, 8);
			WOF1 GGGGGGGGGGGG 2 
			{
				if(invoker.smokeTics++ > 1) 
					A_DoRecoilSmoke();
			}
			WOF1 HIJKL 1;
			WOI1 A 4;
		goto Ready.Active;
		
		Spawn:
			MSNG A -1;
		stop;
	}
}
