// [Samu] Man! Setting a good resolution was a true hassle!
// 		  Made it a bit faster, and also, replaced the reloading sound for a faster version.
//		  Gun's centered properly, and now contains a reloading function!
class Halo_Magnum_Clip : Ammo 
{
	Default{
		Inventory.MaxAmount 15;
	}
}

class HaloMagnum_Bullet : HaloBullet 
{
	Default
	{
		HaloProjectile.SetRanges MAGNUM_RANGE1, MAGNUM_RANGE2;
		HaloProjectile.MinRangeDamage MAGNUM_MINDMG/10.; 
		HaloProjectile.BaseDamage MAGNUM_DMG;
	}
}

class Halo_Magnum : HaloGun
{
	Default
	{
		Weapon.SlotNumber 2;
		Obituary "%o was took down by %k's Magnum.";
		Inventory.PickupMessage	"Magnum Md6 Adquired.";
		Tag "Magnum";
		Weapon.BobSpeed 1.86;
		Weapon.BobRangeX 0.30;
		// [Lewisk] had to switch ammotype's 1 and 2 because thats how my
		// reload function works.
		Weapon.AmmoType1 "Clip";
		Weapon.AmmoType2 "Halo_Magnum_Clip";
				
		// Recoil
		HaloGun.Bloom 0.8;
		HaloGun.BloomSpeed 0.15;
		HaloGun.BaseRecoil MAGNUM_RECOIL;
		HaloGun.Inaccuracy MAGNUM_ACCMOD;
		HaloGun.Crosshair "MagRet";
	}
	
	action void A_FireMagnum()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/Mag_MD6/Fire");
		A_FireProjectile("HaloMagnum_Bullet", A_RecoilSpread(), false, 8, -2, 0, A_RecoilSpread());		
		A_TakeAmmo();
		A_UpdateSpread();
	}
	action state A_CheckRapidFire()
	{
		return A_JumpIf(A_FirePressed(), "Fire");
	}
	
	States
	{
		
		Ready:
			TNT1 A 0 A_WeapSound("Halo/Weapons/Mag_MD6/Select");
			MASE ABCDE 1;
		Ready.Active:
			MAR1 A 1 
			{
				A_RWeaponReady(WRF_ALLOWRELOAD);
				A_WeaponOffset(0,40);
			}
		loop;
		
		Ready.Dryfire:
			MAR1 A 12 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 1 A_RRaise(12);
		wait;

		Deselect:
			MASE EDCBA 1;
			TNT1 A 0 A_RLower(12);
		wait;
		
		
		//  - 210RPM -
		// 210/60 = 3.5
		// 35/3.5 = 10 frames.
		Fire:
			MAFI A 1
			{
				A_RecoilOffset(0,8,1);
				return A_GunCheckReload();
			}
			TNT1 A 0 
			{
				A_FireMagnum();
				A_SetRecoil(1.56, 0, 3);
				A_SetZoom(0.984,2);
				A_QuakeEx(0,2,2,2,0,20,"",QF_RELATIVE|QF_WAVE);
			}
			MAFI B 1 bright A_RecoilOffset(16,-37,4);
			MAFI C 1 
			{
				A_Light(0);
				A_SetZoom(1.0, 2);
				A_SetRecoil(-0.18, 0, 8);
			}
			MAFI CDE 2 A_RecoilOffset(0,0,4);
			MAFI FG 1 A_CheckRapidFire();
		Hold:
			MAR1 A 1 A_CheckRapidFire();
			TNT1 A 0 A_ReFire("Hold");
		goto Ready.Active;
		
		// [Lewisk] A_ValidateReload and A_WeapSound are not in the same state because 
		// that'll change the return type of the annon function to a State.
		// This results in the reload sound not being seperated from the 
		// conditional check.
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			TNT1 A 0 A_WeapSound("Halo/Weapons/Mag_MD6/Reload", CHANF_OVERLAP);
			MAR1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			MAR2 A 1 A_ReloadClip();
			MAR2 BCDEFGHIJKL 1;
		goto Ready.Active;
		
		Spawn:
			HMAG A -1;
		stop;
	}
}