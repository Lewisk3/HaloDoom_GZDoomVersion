// [Samu] Man! Setting a good resolution was a true hassle!
// 		  Made it a bit faster, and also, replaced the reloading sound for a faster version.
//		  Gun's centered properly, and now contains a reloading function!
class Halo_Magnum_Clip : Ammo 
{
	Default 
	{
		Inventory.MaxAmount 15;
	}
}

class HaloMagnum_Bullet : HaloBullet 
{
	Default
	{
		HaloProjectile.SetRanges MAGNUM_RANGE1, MAGNUM_RANGE2;
		HaloProjectile.MinRangeDamage MAGNUM_MINDMG; 
		HaloProjectile.BaseDamage MAGNUM_DMG;
		HaloProjectile.Speed_MPS MAGNUM_BMPS;
	}
}

class Halo_Magnum : HaloGun
{
	Default
	{
		Weapon.SlotNumber 2;
		Obituary "%o was took down by %k's Magnum.";
		Inventory.PickupMessage	"Magnum Md6 Acquired.";
		Tag "Magnum";
		Weapon.BobSpeed 1.86;
		Weapon.BobRangeX 0.30;
		// [Lewisk] had to switch ammotype's 1 and 2 because thats how my
		// reload function works.
		Weapon.AmmoType1 "Halo_Clip";
		Weapon.AmmoType2 "Halo_Magnum_Clip";
		Weapon.AmmoGive 8;
				
		// Recoil
		HaloGun.Bloom 0.8;
		HaloGun.BloomSpeed 0.15;
		HaloGun.BaseRecoil MAGNUM_RECOIL;
		HaloGun.Inaccuracy MAGNUM_ACCMOD;
		HaloGun.Crosshair "MagRet";
		HaloGun.UIGraphic "WI_MAGNM";
		HaloGun.SetupScope 1.25, 0.5;
		
		RecoilWeapon.YOffsetRange -10, 40;
	}
	
	action void A_FireMagnum()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/Mag_MD6/Fire", 0, true);
		A_WeapSound("Halo/Weapons/Mag_MD6/Fire/Bass", CHANF_OVERLAP, true);
		double lowoffs = A_CheckInScope() ? 2.0 : 8.0;
		A_ShootProjectile("HaloMagnum_Bullet", 2, -1.8, lowoffs, 12);
		A_SetSFXPos(25,-9,-10);
		A_SpawnSFX("MuzzleSmoke", 0,0);
		A_SetSFXPos(25,-20,-12);
		A_SpawnSFX("SmallCasing", -90,20);
		A_TakeAmmo();
		A_UpdateSpread();
	}
	action state A_CheckRapidFire()
	{
		return A_JumpIf(A_FirePressed(), "Fire");
	}
	
	States
	{
		Ready:
			TNT1 A 0 
 			{
				A_WeapSound("Halo/Weapons/Mag_MD6/Select");
				return A_JumpIf(!A_DoFullSelectAnimation(), "Ready.Chambered");
			}
			MAGS ABCDEFGHIJKLMNOPQRSTUV 1 A_DoSelectAnim(-5, true);
		goto Ready.Active;
		Ready.Chambered:
			MAGD ABCE 1 A_DoSelectAnim(-5);
		Ready.Active:
			MAR1 A 1 A_HaloWeaponReady(WRF_ALLOWRELOAD, true);
		loop;
		
		Ready.Dryfire:
			MAGI A 12 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 0 A_GunRaise();
		wait;

		Deselect:
			MAGD EDCBA 1;
			MAGD A 1 A_GunLower();
		wait;
		
		
		//  - 210RPM -
		// 210/60 = 3.5
		// 35/3.5 = 10 frames.
		Fire:
			MAGF A 1
			{
				A_RecoilOffset(0,8,1);
				return A_GunCheckReload();
			}
			TNT1 A 0 
			{
				A_FireMagnum();
				A_ModifyRecoil(A_CheckInScope() ? 0.25 : 1.0);
				A_SetRecoil(2.20, 0, 2);
				A_SetZoom(0.984,2);
				A_RQuakeEx(0,2,2,2,0,20,"",QF_RELATIVE|QF_WAVE);
			}
			MAGF B 1 bright A_RecoilOffset(16,-20,4);
			MAGF C 1 
			{
				A_Light(0);
				A_SetZoom(1.0, 2);
				A_SetRecoil(-0.18, 0, 8);
			}
			TNT1 A 0 A_MechanismSound("Halo/Weapons/Mag_MD6/Mech");
			MAGF DEF 1 A_RecoilOffset(0,0,4);
			MAGF GH 1 A_CheckRapidFire();
		Hold:
			MAR1 A 1 A_CheckRapidFire();
			TNT1 A 0 A_ReFire("Hold");
		goto Ready.Active;
		
		// [Lewisk] A_ValidateReload and A_WeapSound are not in the same state because 
		// that'll change the return type of the annon function to a State.
		// This results in the reload sound not being seperated from the 
		// conditional check.
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			TNT1 A 0 A_ScopeDisable();
			TNT1 A 0 A_JumpIf(!CountLoadedAmmo(), "Reload.Empty");
			TNT1 A 0 A_WeapSound("Halo/Weapons/Mag_MD6/Reload", CHANF_OVERLAP);
			MAR1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			MAR2 ABCD 1;
			MAR2 E 1 A_ReloadClip();
			MAR2 FGHIJKLMNO 1;
		goto Ready.Active;
		
		Reload.Empty:
			TNT1 A 0 A_WeapSound("Halo/Weapons/Mag_MD6/Reload/Empty", CHANF_OVERLAP);
			MAR3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			MAR4 ABC 1;
			MAR4 D 1 A_ReloadClip();
			MAR4 EFGHIJKLMNOPQRSTUVW 1;
		goto Ready.Active;
		
		Spawn:
			MGFL A -1;
		stop;
	}
}