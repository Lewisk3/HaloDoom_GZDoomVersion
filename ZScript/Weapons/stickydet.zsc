class Halo_StickyDet_Clip : Ammo 
{
	Default 
	{
		Inventory.MaxAmount 1;
	}
}
class Halo_StickyDet_Reserves : Ammo
{
	mixin WorldAmmoBehavior;
	Default 
	{
		Radius 8;
		Height 6;
		Scale 0.15;

		Inventory.Amount 3;
		Inventory.MaxAmount 18;
		Inventory.Icon "STIKA0"; // TODO: Icon for StickyDet ammo(?)
		Inventory.PickupSound "Halo/Pickup/Ammo";
		Inventory.PickupMessage "Picked up 3 Rounds for the Sticky Detonator";
		
		Ammo.BackpackMaxAmount 28;
	}
	
	States
	{
		Spawn:
			STIK A -1 Bright;
		stop;
	}
}

class HaloStickyDetProj : HaloSlowProjectile
{
	FLineTraceData hitlt;
	PlayerPawn viewer;
	vector3 stuckOffs;
	bool detonate;
	double hitangle;
	
	Default
	{
		HaloSlowProjectile.SetRanges 0, 0;
		HaloSlowProjectile.MinRangeDamage 100;
		HaloSlowProjectile.BaseDamage 10;
		HaloSlowProjectile.Speed_MPS 200;
		
		-NOGRAVITY;
		Health 10;
		Gravity 0.55;
		Scale 0.15;
		Radius 3;
		Height 6;
	}
	
	override bool Used(Actor user)
	{
		user.GiveInventory("Halo_StickyDet_Reserves", 1);
		SetState(ResolveState("Pickup"));
		
		return false;
	}
	
	override void Tick()
	{
		super.Tick();	
		if(health <= 0) detonate = true;
		
		if(!viewer || !viewer.player) return;
		
		// Get look inputs
		UserCmd cmd = viewer.player.cmd;
		double scalar = (360 / 65536.0);
		double cmdYaw = cmd.yaw * scalar;
		double cmdPitch = -cmd.pitch * scalar;

		A_SetAngle(angle + cmdYaw, SPF_Interpolate);
		angle = clamp(angle, hitangle-25, hitangle+25);

		A_SetPitch(pitch + cmdPitch, SPF_Interpolate);
		pitch = clamp(pitch, -10, 20);
	}
	
		
	virtual void StickToActor(Actor targ)
	{
		vector3 followPos = HaloMath.V3Offset(targ.angle, targ.pitch, targ.roll, stuckOffs.x * 0.5, stuckOffs.y * 0.5, stuckOffs.z, 1.0);
		followPos = level.Vec3Offset(targ.pos, followPos);
		SetOrigin(followPos, true);
	}
	
	States
	{
		Spawn:	
			STIK A 1;
		loop;
		
		Crash:
		XDeath:
		Death:
			STIK A 1
			{
				A_StopSound(CHAN_BODY);
				A_StartSound("Halo/Weapons/StickyDet/Proj/Impact", CHAN_AUTO, CHANF_OVERLAP);
			}
		goto Stuck;
		
		Stuck:
			STIK A 1
			{
				A_StartSound("Halo/Weapons/StickyDet/Proj/Ping", CHAN_BODY, CHANF_LOOPING);
				bSOLID = true;
				bMISSILE = false;
				bSHOOTABLE = true;
				A_ChangeLinkFlags(false);
						
				// Check for wall collision.
				LineTrace(angle, 16, pitch, data:hitlt);
				
				if(hitlt.HitType == TRACE_HitActor && hitlt.HitActor && hitlt.HitActor.bSHOOTABLE)
				{
					let victim = hitlt.HitActor;
					stuckOffs = HaloMath.DiffToLocal(pos, victim.pos, (victim.angle, victim.pitch));
					hitangle = angle-victim.angle;
					
					return ResolveState("Stuck.Enemy");
				}
				
				if(hitlt.HitType == TRACE_HitWall)
					return ResolveState("Stuck.Wall");
				
				hitangle = angle;
				return ResolveState("Stuck.Fall");
			}
		goto Stuck.Fall;
		
		Stuck.Wall:
			TNT1 A 0 A_JumpIf(detonate, "ExplodeAndDie");
			STIK A 1
			{
				Line ln = hitlt.HitLine;
				if(ln)
				{
					vector3 normal = (-ln.delta.Y, ln.delta.X, 0).Unit();
					if (!hitlt.LineSide) normal *= -1;
				
					double lineAngle = atan2(normal.y, normal.x);
					if(!viewer) angle = lineAngle;
					hitangle = lineAngle;
					
					bNOGRAVITY = true;
					vector3 stuckPos = hitlt.HitLocation + (normal * 4);
					SetOrigin(stuckPos, true);
					
					FLineTraceData wallchecklt;
					LineTrace(angle + 180, 16, pitch, data:wallchecklt);
					if(wallchecklt.HitType != TRACE_HitWall)
					{
						bNOGRAVITY = false;
						return ResolveState("Stuck.Fall");
					}
				}
				return ResolveState(null);
			}
		loop;
		
		Stuck.Enemy:
			TNT1 A 0 A_JumpIf(detonate, "ExplodeAndDie");
			STIK A 1 
			{
				let victim = hitlt.HitActor;
				if(victim)
				{
					StickToActor(victim);
					if(victim.health <= 0) detonate = true;
				}
			}
		loop;
		
		Stuck.Fall:
			TNT1 A 0 A_JumpIf(detonate, "ExplodeAndDie");
			STIK A 1
			{				
				vel.xy *= 0;
				A_SpriteOffset(0,-3);
			}
		loop;
		
		ExplodeAndDie:
			STIK A 1
			{
				A_StopSound(CHAN_BODY);
				A_StartSound("Halo/Grenade/Frag/Explode");
				Spawn("FragGrenadeExplosion", pos);
			}
		stop;
		
		Pickup:
			TNT1 A 1 A_StopSound(CHAN_BODY);
		stop;
	}
}

class Halo_StickyDetonator : HaloGun
{	
	HaloStickyDetProj activeDet;

	Default
	{
		//$Category "HaloDoom/Weapons"
		Weapon.SlotNumber 6;
		Obituary "%o was stuck by %k's Sticky Detonator.";
		Inventory.PickupMessage	"Picked up the Sticky Detonator.";
		Tag "Sticky Detonator";
		Weapon.BobSpeed 1.86;
		Weapon.BobRangeX 0.30;
		Weapon.AmmoType1 "Halo_StickyDet_Reserves";
		Weapon.AmmoType2 "Halo_StickyDet_Clip";
		Weapon.AmmoGive 3;
		HaloGun.Faction "Human";
				
		// Recoil
		HaloGun.Bloom 0.8;
		HaloGun.BloomSpeed 0.30;
		HaloGun.BaseRecoil 0;
		HaloGun.Inaccuracy 0;
		
		HaloGun.Crosshair "NadeRet", 0.75;
		HaloGun.UIGraphic "WI_STD", 1.0, 1.0;
		HaloGun.SetupUIAmmo "WI_AMM2", 24, 16, 0, 5;
		HaloGun.UIAmmo_Scale_Offset 1.5, 0,0;
		
		HaloGun.PowerTier P_MID;
		
		RecoilWeapon.YOffsetRange -10, 40;
	}
	
	override int GetPowerTier()
	{	
		if(HaloPlayer.IsGameType("SpartanOverkill")) return P_NORM | P_MID;
		return super.GetPowerTier();
	}
	
	action void A_FireStickyDet()
	{
		A_Light(2);
		A_WeapSound("Halo/Weapons/StickyDet/Fire", 0, true);
		A_WeapSound("Halo/Weapons/StickyDet/Fire/Bass", CHANF_OVERLAP, true);
		
		vector3 _hitpos; Actor sticky;
		[_hitpos, sticky] = A_ShootProjectile("HaloStickyDetProj", 12, 2);	
		
		invoker.activeDet = HaloStickyDetProj(sticky);
		A_TakeAmmo();
	}
	
	action void A_ReloadSound()
	{
		A_StartSound("Halo/Weapons/StickyDet/Reload", CHAN_RELOADING, CHANF_OVERLAP);
	}
	
	action void A_SetupIdleFrame()
	{
		if(A_CheckInScope())
		{	
			A_SetFrame(CountLoadedAmmo() ? 0 : 6);
			return;
		}
		A_SetFrame(CountLoadedAmmo() ? 0 : 1);
		if(invoker.activeDet) A_SetFrame(2);
	}
	
	override void OverlayUIFX(RenderEvent ev, double deltaTime)
	{
		if( !zoomed || !activeDet ) return;
		if(activeDet && activeDet.bMISSILE) return; // Only allow camera view for stuck sticky dets.

		// Adjust scale for fullscreen.
		vector2 texsize;
		let camGraphic = TexMan.CheckForTexture("Graphics/StickyDet/StickyDetCam.png");
		[texsize.x, texsize.y] = TexMan.GetSize(camGraphic);
		
		vector2 toRes = (1920., 1080.);
		vector2 scaleMod = (
			toRes.x / texsize.x,
			toRes.y / texsize.y
		);
		
		uint flags = HLSBS.SS_SCREEN_CENTER | HLSBS.SS_COORDS_TEXGRID;
		TexMan.SetCameraToTexture(activeDet,"ScopeCamera",90.0);
		
		HLSBS.DrawImage("ScopeCamera", (0,0), flags, 1.0);
		HLSBS.DrawTexture(camGraphic, (0.5,0.5), flags, 1.0, scale:scaleMod);
	}
	
	action void A_DetonateSticky()
	{
		if(invoker.activeDet) 
		{
			invoker.activeDet.detonate = true;
			 A_StartSound("Halo/Weapons/StickyDet/Cam/Close", CHAN_AUTO, CHANF_OVERLAP);
		}
		invoker.activeDet = NULL;
	
		A_SetCustomScope(0);
		A_StopSound(CHAN_PING);
	}
	
	override void DoEffect()
	{
		super.DoEffect();
		
		if(activeDet)
			Owner.A_StartSound("Halo/Weapons/StickyDet/Ping", CHAN_PING, CHANF_LOOPING);
		else
			Owner.A_StopSound(CHAN_PING);
	}
	
	bool sightsHold;
	const CHAN_PING = 33;
	
	States
	{
		Ready:
			TNT1 A 0 
			{
				A_WeapSound("Halo/Weapons/StickyDet/Draw");
				return A_JumpIf(CountLoadedAmmo(), "Ready.Loaded");
			}
			WWR1 FFEEDCBA 1 A_DoSelectAnim();
		goto Ready.Active;
		Ready.Loaded:
			TNT1 A 1 A_RecoilOffset(0,120,1,true);
			WWR1 UUUUUUUVWWXXYYZ 1 A_RecoilOffset(0,0,12);
			WWR2 A 1 A_RecoilOffset(0,0,1);
		Ready.Active:
			WWG1 "#" 1 
			{
				if(invoker.activeDet)
					invoker.activeDet.viewer = NULL;
			
				A_SetupIdleFrame();
				A_HaloWeaponReady(WRF_ALLOWRELOAD, usescope:false);
				return A_JumpIf(A_ShouldEnableScope(), "Sights.Enable");
			}
		loop;
		
		Ready.ADS:
			WWS1 "#" 1 
			{
				if(invoker.activeDet)
					invoker.activeDet.viewer = PlayerPawn(self);
			
				A_SetupIdleFrame();
				A_HaloWeaponReady(WRF_ALLOWRELOAD, usescope:false);
				return A_JumpIf(A_ShouldDisableScope(invoker.sightsHold), "Sights.Disable");
			}
		loop;
		
		Ready.Dryfire:
			TNT1 A 0 A_SetupIdleFrame();
			WWG1 "#" 4 A_WeapSound("Halo/Weapons/DryFire", CHANF_OVERLAP);
		goto Ready.Active;

		Select:
			TNT1 A 1 A_GunRaise();
		wait;

		Deselect:
			// TNT1 A 0 A_DetonateSticky();
			TNT1 A 0 A_JumpIf(!CountLoadedAmmo(), "Deselect.Empty");
			WWR1 ZYXWV 2;
			WWR1 U 1 A_GunLower(10);
		wait;
		
		Deselect.Empty:
			WWR1 AABBCDE 1;
			WWR1 F 1 A_GunLower();
		wait;
					
		Fire:
			TNT1 A 0 A_JumpIf(invoker.activeDet, "Fire.Detonate");
			TNT1 A 0 A_JumpIf(A_CheckInScope(), "Fire.FromSights");
			TNT1 A 0 A_GunCheckReload();
			WWG1 A 1;
			WWF1 A 1 Bright
			{
				A_RecoilOffset(0,8,1);
				A_SetRecoil(2.0, 0, 2);
				A_FireStickyDet();
				A_RQuakeEx(2,3,2,3,0,20,"",QF_RELATIVE|QF_WAVE);
			}
		goto Camera.Open;
		
		Fire.FromSights:
			WWS1 B 1;
			WWS1 C 1 Bright
			{
				A_RecoilOffset(0,4,1);
				A_SetRecoil(0.75, 0, 2);
				A_FireStickyDet();
			}
			WWS1 D 1;
			WWS1 EF 1 A_Light(0);
			TNT1 A 0 A_SetCustomScope(0);
			WWS1 G 4;
		goto Camera.Open;
		
		Camera.Open:
			WWF1 B 1 A_StartSound("Halo/Weapons/StickyDet/Cam/Open", CHAN_AUTO, CHANF_OVERLAP);
			WWF1 CD 1 A_Light(0);
			WWG1 C 4;
		goto Ready.Active;
		
		Fire.Detonate:
			WWD1 A 1;
			WWD1 A 4 A_DetonateSticky();
			WWD1 BCD 2;
		goto Ready.Active;
		
		Reload.FromSights:
			TNT1 A 0 A_SetCustomScope(0);
			WWG1 A 4;
			
		Reload:
			TNT1 A 0 A_ValidateReload("Ready.Dryfire");
			TNT1 A 0 A_JumpIf(A_CheckInScope(), "Reload.FromSights");
			TNT1 A 0 A_DetonateSticky();
			TNT1 A 0 A_ReloadSound();
			WWR1 ABCDE 2;
			WWR1 F 4;
			WWR1 GHIJKLMNO 2;
			WWR1 P 2 A_SetRecoil(-1.0, 0, 2);
			WWR1 QRS 2;
			WWR1 TU 2 A_SetRecoil(-2.0, 0, 2);
			WWR1 V 2 A_ReloadClip();
			WWR1 WXYZ 2 A_SetRecoil(0.75, 0, 2);
			WWR2 A 4;
		goto Ready.Active;
		
		Sights.Enable:
			TNT1 A 0 A_SetupIdleFrame();
			WWS1 "#" 2;
			TNT1 A 0 
			{
				invoker.sightsHold = invoker.ADS_Held;
				A_SetCustomScope(1);	
			}
		goto Ready.ADS;
		
		Sights.Disable:
			TNT1 A 0 A_SetCustomScope(0);
			WWG1 A 1;
		goto Ready.Active;	
		
		Spawn:
			WWP1 A -1;
		stop;
	}
}
