extend class HaloDoom_Events
{
	Array<String> weaps_basic;
	Array<String> weaps_mid;
	Array<String> weaps_power;
	Array<String> weaps_melee;
	
	class<Actor> getWeaponFromAmmoType(class<Actor> type)
	{
		for(int i = 0; i < AllActorClasses.Size(); i++)
		{
			let weap = (class<Weapon>) (AllActorClasses[i]);
			if(!weap) continue;
			
			let defs = GetDefaultByType(weap);
			if(defs.ammotype1 == type || defs.ammotype2 == type)
				return weap;
		}
		return null;
	}
	
	void populateSpawnArrays()
	{
		for(int i = 0; i < AllActorClasses.Size(); i++)
		{
			let actType = HaloGun(GetDefaultByType(AllActorClasses[i]));
			if( !actType || actType.getClass() == "HaloGun") continue;
			
			String gunName = actType.GetClassName();
			switch(actType.SpawnTier)
			{
				case HaloGun.P_NORM:  weaps_basic.Push(gunName); break;
				case HaloGun.P_MID:	  weaps_mid.Push(gunName);   break;
				case HaloGun.P_POWER: weaps_power.Push(gunName); break;	
				case HaloGun.P_MELEE: weaps_melee.Push(gunName); break;
			}
		}
	}
	
	override void CheckReplacement(ReplaceEvent e)
	{
		Array<String> weapSpawns;
		let item = e.Replacee;
		int tier = 0;
		
		// Weapon Spawning
		if( item is "Chainsaw" || item is "Fist" || item is "Berserk" )
			weapSpawns.Append(weaps_melee);
			
		if( item is "Pistol" || item is "Shotgun" || item is "Chaingun" ) 
			weapSpawns.Append(weaps_basic);
			
		if( item is "SuperShotgun" || item is "RocketLauncher" || item is "PlasmaRifle" ) 
			weapSpawns.Append(weaps_mid);
			
		if( item is "BFG9000" ) 
			weapSpawns.Append(weaps_power);
		
		if(weapSpawns.Size() > 0)
			e.Replacement = weapSpawns[random[WeapSpawner](0, weapSpawns.Size()-1)];
			
		// Nade spawning
		if( item is "HealthBonus" && random[NadeSpawner](0,6) == 6 )
			e.Replacement = "PlasmaGrenades";	
		
		if( item is "ArmorBonus" && random[NadeSpawner](0,6) == 6 )
			e.Replacement = "FragGrenades";
		else if(item is "ArmorBonus" && random[NadeSpawner](0,2) == 2)
			e.Replacement = "HealthBonus";
		
		// Powerup Spawning
		if( item is "SoulSphere" )
			e.Replacement = "Overshield_LV1";
		if( item is "MegaSphere" )
			e.Replacement = "Overshield_LV2";
		if( item is "GreenArmor" )
			e.Replacement = "Overshield_LV1";
		if( item is "BlueArmor" )
			e.Replacement = "Overshield_LV2";
			
		// Ammunition spawning
		int ammoTier = 0;
		
		if( item is "Clip" || item is "ClipBox" || item is "Shell" ) 
			ammoTier = 1;
		if( item is "ShellBox" || item is "RocketAmmo" || item is "Cell" || item is "RocketBox") 
			ammoTier = 2;
		if( item is "CellPack" )
			ammoTier = 3;
			
		string AmmoClasses[10] = 
		{
			"Halo_AR_Reserves",
			"Halo_Magnum_Reserves",
			"Halo_BAR_Reserves",
			"Halo_Bulldog_Reserves",
			"Halo_Shotgun_Reserves",
			"Halo_CarbineReserves",
			
			"Halo_DBLShotgun_Reserves",
			"Halo_Sniper_Reserves",
			"Halo_Needler_Reserves",
			
			"Halo_StanchionReserves"
		};
			
		int weapSpawnChance = 90; // (100 - n) = chance %;
		class<Actor> spawnType;
		switch(ammoTier)
		{
			case 1: // AR, Magnum, BR, Bulldog, Shotgun, Carbine	
				spawnType = AmmoClasses[random[HaloAmmoSpawns](0,5)];
				if(random[HaloAmmoWeaponSpawns](0,100) > weapSpawnChance) 
					spawnType = getWeaponFromAmmoType(spawnType);
					
				e.Replacement = spawnType;
			break;
			case 2: // Double Barrel, Sniper Rifle, Rocket Launcher (Season 2), Needler 
				spawnType = AmmoClasses[random[HaloAmmoSpawns](6,8)];
				if(random[HaloAmmoWeaponSpawns](0,100) > weapSpawnChance) 
					spawnType = getWeaponFromAmmoType(spawnType);
					
				e.Replacement = spawnType;
			break;
			case 3: // Stanchion
				e.Replacement = AmmoClasses[9];
			break;
		}
	}
}