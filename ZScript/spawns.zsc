extend class HaloDoom_Events
{
	Array<String> weaps_weak;
	Array<String> weaps_basic;
	Array<String> weaps_mid;
	Array<String> weaps_power;
	Array<String> weaps_uber;
	Array<String> weaps_melee;
	
	class<Actor> getWeaponFromAmmoType(class<Actor> type)
	{
		for(int i = 0; i < AllActorClasses.Size(); i++)
		{
			let weap = (class<Weapon>) (AllActorClasses[i]);
			if(!weap) continue;
			
			let defs = GetDefaultByType(weap);
			if(defs.ammotype1 == type || defs.ammotype2 == type)
				return weap;
		}
		return null;
	}
	
	void populateSpawnArrays()
	{
		for(int i = 0; i < AllActorClasses.Size(); i++)
		{
			let actType = HaloGun(GetDefaultByType(AllActorClasses[i]));
			if( !actType || actType.getClass() == "HaloGun") continue;
			
			String gunName = actType.GetClassName();
			switch(actType.SpawnTier)
			{
				case HaloGun.P_WEAK:  weaps_weak.Push(gunName);  break;
				case HaloGun.P_NORM:  weaps_basic.Push(gunName); break;
				case HaloGun.P_MID:	  weaps_mid.Push(gunName);   break;
				case HaloGun.P_POWER: weaps_power.Push(gunName); break;	
				case HaloGun.P_UBER:  weaps_uber.Push(gunName);  break;
				case HaloGun.P_MELEE: weaps_melee.Push(gunName); break;
			}
		}
	}
	
	bool checkItemType(class<Actor> item, string type)
	{
		string itemName = item.getClassname();
		type = type.MakeLower();
		itemName = itemName.MakeLower();
		bool success = (item is type) || (itemName.IndexOf(type) != -1);
		
		return success;
	}
	
	override void CheckReplacement(ReplaceEvent e)
	{
		Array<String> weapSpawns;
		let item = e.Replacee;
		int tier = 0;
		
		// Do not touch HaloGuns for replacement.
		if(item is "HaloGun") return;
				
		// Weapon Spawning
		if(item is "Weapon") // If this check isn't performed then projectiles can be turned into guns?!?! 
		{
			if(checkItemType(item, "Chainsaw") || checkItemType(item, "Fist"))
				weapSpawns.Append(weaps_melee);
				
			if(checkItemType(item, "Pistol") || checkItemType(item, "Chaingun")) 
				weapSpawns.Append(weaps_basic);
				
			if(checkItemType(item, "SuperShotgun") || checkItemType(item, "Shotgun") ) 
				weapSpawns.Append(weaps_mid);
				
			if(checkItemType(item, "RocketLauncher") || checkItemType(item, "PlasmaRifle"))
				weapSpawns.Append(weaps_power);
				
			if(checkItemType(item, "BFG9000") || checkItemType(item, "BFG")) 
				weapSpawns.Append(weaps_uber);
		} 
		else if(item is "Berserk")
			weapSpawns.Append(weaps_melee);
			
		if(weapSpawns.Size() > 0)
		{
			e.Replacement = weapSpawns[random[WeapSpawner](0, weapSpawns.Size()-1)];
			return;
		}
		
		// Nade spawning
		if( item is "HealthBonus" && random[NadeSpawner](0,6) == 6 )
			e.Replacement = "PlasmaGrenades";	
		
		if( item is "ArmorBonus" && random[NadeSpawner](0,6) == 6 )
			e.Replacement = "FragGrenades";
		else if(item is "ArmorBonus" && random[NadeSpawner](0,2) == 2)
			e.Replacement = "HealthBonus";
		
		// Powerup Spawning
		if( item is "SoulSphere" )
			e.Replacement = "Overshield_LV1";
		if( item is "MegaSphere" )
			e.Replacement = "Overshield_LV2";
		if( item is "GreenArmor" )
			e.Replacement = "Overshield_LV1";
		if( item is "BlueArmor" )
			e.Replacement = "Overshield_LV2";
			
		// Ammunition spawning
		int ammoTier = 0;
		
		if( item is "Clip" || item is "ClipBox" || item is "Shell" ) 
			ammoTier = 1;
		if( item is "ShellBox" || item is "RocketAmmo" || item is "Cell" || item is "RocketBox") 
			ammoTier = 2;
		if( item is "CellPack" )
			ammoTier = 3;
			
		string AmmoClasses[11] = 
		{
			"Halo_AR_Reserves",
			"Halo_Sidekick_Reserves",
			"Halo_BAR_Reserves",
			"Halo_Bulldog_Reserves",
			"Halo_Shotgun_Reserves",
			"Halo_CarbineReserves",
			
			"Halo_Magnum_Reserves",
			"Halo_DBLShotgun_Reserves",
			"Halo_Sniper_Reserves",
			"Halo_Needler_Reserves",
			
			"Halo_StanchionReserves"
		};
			
		int weapSpawnChance = 90; // (100 - n) = chance %;
		class<Actor> spawnType;
		switch(ammoTier)
		{
			case 1: // AR, Magnum, BR, Bulldog, Shotgun, Carbine	
				spawnType = AmmoClasses[random[HaloAmmoSpawns](0,5)];
				if(random[HaloAmmoWeaponSpawns](0,100) > weapSpawnChance) 
					spawnType = getWeaponFromAmmoType(spawnType);
					
				e.Replacement = spawnType;
			break;
			case 2: // Double Barrel, Sniper Rifle, Rocket Launcher (Season 2), Needler 
				spawnType = AmmoClasses[random[HaloAmmoSpawns](6,9)];
				if(random[HaloAmmoWeaponSpawns](0,100) > weapSpawnChance) 
					spawnType = getWeaponFromAmmoType(spawnType);
					
				e.Replacement = spawnType;
			break;
			case 3: // Stanchion
				e.Replacement = AmmoClasses[10];
			break;
		}
	}
}