// Globals
class LaserGlobals abstract
{
	static play Actor SpawnFX(Actor src, class<Actor> type, double xyoffs, double poffs, double zoffs, double dist=16)
	{
		vector3 spos = src.Vec3Offset(cos(src.angle+xyoffs)*dist, sin(src.angle+xyoffs)*dist, zoffs);
		let proj = Actor.Spawn(type, spos);
		if(proj)
		{
			proj.target = src; // Assign target to shooter.
			proj.angle = src.angle+xyoffs;
			proj.pitch = src.pitch+poffs;
			proj.vel = (
				cos(proj.angle)*cos(proj.pitch),
				sin(proj.angle)*cos(proj.pitch),
				-sin(proj.pitch)
			) * proj.default.speed;
		}
		return proj;
	}
}


// Laser Emitter
class LaserEmitter : Actor
{
	Actor source;
	Actor aimactor;
	Laser activelaser;
	vector3 aimpos;
	vector2 viewoffs, viewvel;
	vector3 posoffs;
	bool laser_on;
	double curdist;
	
	Default
	{
		+NOINTERACTION;
		+INTERPOLATEANGLES;
	}
	
	private double GetDist(uint flags, int maxdist = int.max)
	{
		FLineTraceData lt;
		if(!laser_on || !activelaser)
			LineTrace(angle, maxdist, pitch, flags, 0, source.radius+16, data:lt);
		else
			LineTrace(activelaser.angle, maxdist, activelaser.pitch-90, flags, 0, source.radius+16, data:lt);
		aimpos = lt.HitLocation;
		aimactor = lt.HitActor;		
		return lt.Distance-(source.radius+16);
	}
	
	override void OnDestroy()
	{
		LaserOff();
		if(activelaser) activelaser.Destroy();
	}

	override void Tick()
	{
		if(!source) return;
		
		if(source.player)
		{
			double iyaw = source.player.cmd.yaw * (360./65536.);
			double ipit = -source.player.cmd.pitch * (360./65536.);
			viewvel = (iyaw, ipit);
		}
		
		A_SetAngle(source.angle + viewoffs.x, SPF_INTERPOLATE);
		A_SetPitch(source.pitch + viewoffs.y, SPF_INTERPOLATE);
		super.Tick();
	}
	
	vector3 UpdatePos()
	{
		SetPos(posoffs, viewoffs);
		return pos;
	}
	
	void SetPos(vector3 pos_offset = (0,0,0), vector2 view_offset = (0,0))
	{
		viewoffs = view_offset;
		posoffs = pos_offset;
		vector3 finalpos;
		finalpos = HaloMath.RelativeV3Offset(source, viewoffs.x, viewoffs.y, 0, pos_offset.x, pos_offset.y, pos_offset.z, 1.);
		SetOrigin(finalpos, false);
	}
	
	static LaserEmitter Attach(Actor to)
	{
		let lasersource = LaserEmitter(Spawn("LaserEmitter"));
		if(lasersource) 
		{
			lasersource.source = to;
			lasersource.UpdatePos();
		}
		return lasersource;
	}

	Laser Fire(class<Laser> parts, class<Actor> puff = "", int maxdist = int.max, uint ltflags = 0, bool predict = false, bool fixed_dist = false)
	{
		if(!source) return null;
		curdist = fixed_dist ? maxdist : GetDist(ltflags, maxdist);	
		//vel.xy = source.vel.xy;
		Actor spwnpuff;
		if(puff)
		{
			spwnpuff = Spawn(puff, aimpos);
			if(spwnpuff) 
			{
				spwnpuff.angle = angle;
				spwnpuff.pitch = pitch;
			}
		}
		if(!laser_on)
		{
			let lasobj = Laser(Actor.Spawn(parts,pos));
			if(lasobj) 
			{
				lasobj.emitter = self;
				lasobj.angle = angle;
				lasobj.pitch = pitch+90;
				lasobj.aimpuff = spwnpuff;
				lasobj.predictaim = predict;
			}
			activelaser = lasobj;
		}
		if(spwnpuff) spwnpuff.target = activelaser;
		laser_on = true;
		return activelaser;
	}
	
	void LaserOff()
	{
		laser_on = false;
	}
}

// Laser Object
class Laser : Actor
{
	LaserEmitter emitter;
	vector3 spawnpos;
	Actor aimpuff;
	double dist;
	bool predictaim;
	bool bStatic;
	
	double interspd;
	Property Interpolation : interspd;
	
	Default
	{	
		+NOINTERACTION;
		+INTERPOLATEANGLES;
		Laser.Interpolation 0.5;
		Radius 6;
	}

	virtual void AdjustLaser() 
	{
		bInvisible = GetAge() <= 1;
	}

	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		
		vector2 toangles = (emitter.angle, emitter.pitch+90);
		A_SetAngle(toangles.x, SPF_INTERPOLATE);
		A_SetPitch(toangles.y, SPF_INTERPOLATE);
		spawnpos = pos;
	}

	override void Tick() 
	{
		if(!emitter || !emitter.laser_on) 
		{
			Destroy();
			return;
		}
		
		AdjustLaser();
		
		// Carefully follow emitter.
		if(!bStatic)
		{
			vector2 paim = (0,0);
			if(predictaim) paim = emitter.viewvel;
			vector2 toangles = (emitter.angle + paim.x, emitter.pitch+90 + paim.y);
			
			A_SetAngle(HaloMath.Lerp(angle, toangles.x, interspd), SPF_INTERPOLATE);
			A_SetPitch(HaloMath.Lerp(pitch, toangles.y, interspd), SPF_INTERPOLATE);
		}
		
		// Project position out to center
		double mid = emitter.curdist * 0.5;
		double sinp = sin(pitch-90), cosp = cos(pitch-90);
		double cosa = cos(angle), sina = sin(angle);
		vector3 dir = (cosa*cosp, sina*cosp, -sinp) * mid;

		// Scale to distance
		double radi_units = emitter.curdist * 0.75;
		Scale.Y = radi_units;
		
		// Warp to emitter.
		if(bStatic)
		{
			SetOrigin(level.vec3offset(spawnpos, dir), false);
		}
		else
		{
			dir = level.Vec3Offset(dir, emitter.UpdatePos());		
			SetOrigin(dir, true);
		}
	}
}